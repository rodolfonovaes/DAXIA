#INCLUDE "PROTHEUS.CH"
#INCLUDE "MATA415.CH"
#INCLUDE "TBICONN.CH"
#INCLUDE "FWMVCDEF.CH"
#INCLUDE "FWEVENTVIEWCONSTS.CH"
#INCLUDE "FWADAPTEREAI.CH"

Static aStackTMP
Static lShowError	:= .T.

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MATA415   ³ Autor ³ Eduardo Riera         ³ Data ³ 08.12.97  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³Mata415()                                                    ³±±
±±³          ³Rotina de atualizacao dos Orcamentos de venda                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina permite as operacoes de pesquisa, visualizacao,  ³±±
±±³          ³inclusao, alteracao, exclusao, copia, cancelamento e efetiva-³±±
±±³          ³cao de um orcamento de venda.                                ³±±
±±³          ³O objetivo principal desta rotina eh de servir como ferramen-³±±
±±³          ³ta em uma negociacao de venda e/ou numa negociacao B2B.      ³±±
±±³          ³Para tanto, a ferramenta dispoe de algumas caracteristicas   ³±±
±±³          ³que julga-se essencial para o comprimento de seu principal   ³±±
±±³          ³objetivo:                                                    ³±±
±±³          ³a) Formulacao do preco de venda: Esta caracteristica benefi- ³±±
±±³          ³cia tanto ao cliente, quando ao representante comercial que  ³±±
±±³          ³pode dizer exatamente ao seu cliente o valor do desembolso   ³±±
±±³          ³financeiro                                                   ³±±
±±³          ³b) Desconto em cascata(d1+d2+d3+d4): Os percentuais de descon³±±
±±³          ³to sao efetuados sobre o preco de lista.                     ³±±
±±³          ³c) Desconto de Indenizacao: O valor do desconto eh dado sobre³±±
±±³          ³a soma total do valor da venda.                              ³±±
±±³          ³d) Desconto por item: O valor do desconto eh dado sobre o va-³±±
±±³          ³lor total do item.                                           ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function MATA415(xAutoCab,xAutoItens,nOpcAuto,lSimulacao,lSigaDPR)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Declaracao das variaveis                                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aCores	:= {{'SCJ->CJ_STATUS=="A"', 'ENABLE'},;			 	//Orcamento em Aberto
            	    {'SCJ->CJ_STATUS=="B"', 'DISABLE'},;				//Orcamento Baixado
            	    {'SCJ->CJ_STATUS=="C"', 'BR_PRETO'},;				//Orcamento Cancelado
            	    {'SCJ->CJ_STATUS=="D"', 'BR_AMARELO'},;			//Orcamento nao Orcado
            	    {'SCJ->CJ_STATUS=="F"', 'BR_MARROM'}}				//Orcamento bloqueado

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Tratamento de Filtro para o browse                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local aIndex		:= {}
Local bFiltraBrw	:= {|| Nil}
Local cFiltro		:= ''
Local lMT415Brw		:= .F.
Local cFilSQL		:= ""

DEFAULT lSimulacao	:= .F.
DEFAULT lSigaDPR		:= .F.

Private lOnUpdate   := .T.
Private lExAutoDPR 	:= lSigaDPR
Private cCadastro	:= OemtoAnsi(STR0001) 					//"Or‡amentos de Venda"
Private l415Auto 	:= ( Valtype(xAutoCab) == "A"  .and. ValType(xAutoItens) == "A" )
Private aAutoCab	:= {}
Private aAutoItens	:= {}
Private aRotina		:= MenuDef()
Private lShowOpc	:= .T.

lShowError	:= !(AllTrim(FunName()) $ "CRMA110|FATA300" .And. !(SuperGetMV("MV_FATPROP",,"O") == "O"))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicializacao da Mbrowse ou rotina automatica                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SCJ")
dbSetOrder(1)
MsSeek(xFilial())
If ( AMIIn(5) )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se o tamanho do campo CJ_TABELA                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Len(SCJ->CJ_TABELA)<>3
		Aviso("SX3",STR0049,{"OK"}) //"O estrutura do campo CJ_TABELA esta diferente do padrao, solicitamos que seja alterado para Caracter de 3, antes de utilizar a rotina"
	Else
		If  ( Type("l415Auto") <> "U" .And. l415Auto )
			DEFAULT nOpcAuto := 3
			lOnUpdate  := !lSimulacao
			aAutoCab   := xAutoCab
			aAutoItens := xAutoItens
			MBrowseAuto(nOpcAuto,Aclone(aAutoCab),"SCJ")
			xAutoCab   := aAutoCab
			xAutoItens := aAutoItens
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ponto de Entrada para alterar cores do Browse do Cadastro    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ExistBlock("MA415COR")
				aCores := ExecBlock("MA415COR",.F.,.F.,aCores)
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Ponto de Entrada para filtro antes da apresentacao do Browse ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ExistBlock("MT415BRW")
				cFiltro := ExecBlock("MT415BRW",.F.,.F.)
				If ValType(cFiltro) == "C" .And. !Empty(cFiltro)
					CursorWait()
					bFiltraBrw := {|| FilBrowse("SCJ",@aIndex,@cFiltro) }
					Eval(bFiltraBrw)
					CursorArrow()
					lMT415Brw := .T.
				EndIf
			EndIf
		
			If ExistBlock("M415FSQL")	// Filtro SQL na MBrowser
				cFilSQL := ExecBlock("M415FSQL",.F.,.F.)
			EndIf
   		

			mBrowse( 6, 1,22,75,"SCJ",,,,,,aCores,,,,,,,,cFilSQL)
			If lMT415Brw
				EndFilBrw("SCJ",aIndex)
			Endif
		EndIf
	EndIf
EndIf
Return(.T.)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415Inclui³ Autor ³ Eduardo Riera         ³ Data ³08.12.97  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Inclusao de Orcamentos de Venda                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void A415Inclui(ExpC1,ExpN1,ExpN2,ExpX1,[ExpC2])           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN2 = Numero do registro                                 ³±±
±±³          ³ ExpN3 = Opcao selecionada                                  ³±±
±±³          ³ ExpX4 = Sem funcao                                         ³±±
±±³          ³ ExpC5 = Numero do orcamento incluido ( passado por refe-   ³±±
±±³          ³ rencia                                                     ³±±
±±³          ³ ExpC6 = Codigo do cliente                                  ³±±
±±³          ³ ExpC7 = Loja do Cliente                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415Inclui(cAlias,nReg,nOpcx,xPar,cNumOrc,cCodCli,cLoja)

Local aArea		:= GetArea("SCJ")
Local nStack    := GetSX8Len()
Local nX 		:= 0
Local nOpcA		:= 0
Local cArqSCL	:= ""
Local cArqSCK	:= ""
Local cNumAnt   := ""
Local oDlg
Local oSay
Local oGetDB
Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := {}
Local aPosGet   := {}
Local aInfo     := {}
Local nGetLin   := 0
Local lBloqueia	:= .F.
Local lGrade	:= MaGrade()
Local cEventID   := 0    // Variavel usada para armazenar o ID do EventViewer
Local cMessagem  := " " // Variavel para armazenar a mensagem utilizada no eventviewer
Local lAt600Orc	:= IsInCallStack("At600Orc")
Local lInitPad	:= .T.

Private aTela[0][0]
Private aGets[0]
Private bCampo		:= { |nField| Field(nField) }
Private aHeader		:= {}
Private aHeaderSCL	:= {}
Private aHeaderSCK	:= {}
PRIVATE aGEMCVnd    := {"",{},{}} //Template GEM - Condicao de Venda

// Protecao para nao criar a variavel se ja existir, no caso de chamadas externas.
If ValType( cCadastro ) == "U"
	Private cCadastro	:= OemToAnsi( STR0001 )					//"Or‡amentos de Venda"
EndIf

Private oGrade := MsMatGrade():New('oGrade',,"CK_QTDVEN",,"A415VlCpGr()",;
  						{ 	{VK_F4,{|| A440Saldo(.T.,oGrade:aColsAux[oGrade:nPosLinO][aScan(oGrade:aHeadAux,{|x| AllTrim(x[2])=="CK_LOCAL"})])}} },;
  						{ 	{"CK_QTDVEN",NIL,NIL},;
  							{"CK_OPC",NIL,NIL}})

l415Auto := If(Type("l415Auto")<>"U",l415Auto,.F.)
l416Auto := If(Type("l416Auto")<>"U",l416Auto,.F.)

DEFAULT cNumOrc := ""
DEFAULT cCodCli := ""
DEFAULT cLoja   := ""
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa as Variaveis da Enchoice                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SCJ")
For nX:= 1 To FCount()
	lInitPad	:= !( lAt600Orc .And. AllTrim(FieldName(nX)) == "CJ_NUM" )
	M->&(EVAL(bCampo,nX)) := CriaVar(FieldName(nX),lInitPad)
Next nX
If !Empty(cCodCli)
	M->CJ_CLIENTE := cCodCli
	M->CJ_CLIENT  := cCodCli
	M->CJ_LOJA    := cLoja
	M->CJ_LOJAENT := cLoja
	A415TabCli() //Carrega dados vinculados ao cliente na inclusão via oportunidade.
EndIf

If IsInCallStack("Ft300Orc")
	M->CJ_PROSPE := M->AD1_PROSPE
	M->CJ_LOJPRO := M->AD1_LOJPRO
	M->CJ_NROPOR := M->AD1_NROPOR
	M->CJ_REVISA := M->AD1_REVISA
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
A415Monta(@cArqSCK,@cArqSCL,.T.)
aHeader := aHeaderSCK
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza o aHeader para a Rotina Automatica                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TMP1")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Montagem da Tela                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( !l415Auto )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Habilita a Tecla F4                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Faz o calculo automatico de dimensoes de objetos     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aSize := MsAdvSize()

	aObjects := {}
	aAdd( aObjects, { 100, 100, .t., .t. } )
	aAdd( aObjects, { 100, 100, .t., .t. } )
	aAdd( aObjects, { 100, 015, .t., .f. } )

	aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
	aPosObj := MsObjSize( aInfo, aObjects )

	aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,;
		{{005,070,110,175,215,280}} )
	nGetLin := aPosObj[3,1]

	SetKey(VK_F4,{||A415F4()})

	DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL//"Or‡amentos de Venda"
	EnChoice( "SCJ" ,nReg,nOpcx,,,,,aPosObj[1],,3,,,"A415VldTOk",,,.F.)
	oGetDb := MsGetDB():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"A415LinOk","A415TudOk","+CK_ITEM",.T., , ,.T.,,"TMP1","A415FldOk")
	Private oGetDad := oGetDb
	@ nGetLin,aPosGet[1,1] SAY oSay PROMPT OemToAnsi(STR0034) 	SIZE 060,009 PIXEL OF oDlg //"Total do Or‡amento "
	@ nGetLin,aPosGet[1,3] SAY oSay PROMPT OemToAnsi(STR0035) 	SIZE 060,009 PIXEL OF oDlg //"Descontos "
	@ nGetLin,aPosGet[1,5] SAY oSay PROMPT OemToAnsi("=")		SIZE 060,009 PIXEL OF oDlg //"="
	@ nGetLin,aPosGet[1,2] SAY oSay PROMPT 0    					SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
	oSay:Cargo := "Valor"
	@ nGetLin,aPosGet[1,4] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",14) PIXEL OF oDlg
	oSay:Cargo := "Desconto"
	@ nGetLin,aPosGet[1,6] SAY oSay PROMPT 0                     SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
	oSay:Cargo := "Total"
	ACTIVATE MSDIALOG oDlg ON INIT Ma415Bar(oDlg,{||nOpcA:=1,IIf(oGetDb:TudoOk().And.Obrigatorio(aGets,aTela).And.A415VldTOk(@lBloqueia),oDlg:End(),nOpcA:=0)},{||oDlg:End()}, nOpcx)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Desabilita a Tecla F4                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SetKey(VK_F4,Nil)
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Tratamento da Rotina automatica                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nOpcA := 0
	If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)}) .And. MsGetDBAuto("TMP1",aAutoItens,"A415LinOk",{|| A415TudOk() .And. A415VldTOk(@lBloqueia)},aAutoCab,aRotina[nOpcx][4])
		nOpcA := 1
	EndIf
EndIf

If ( nOpcA == 1 )
	Begin Transaction
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Efetua tratamento do Bonus                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		cNumAnt := M->CJ_NUM
		A415Bonus(1)
		If Type("lOnUpDate") == "U" .Or. lOnUpdate
			If lGrade
				A415Gr2SCK() //Transforma grade em itens da SCK
			EndIf
			If ( A415Grava(1,lBloqueia) )
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Seta a varivel de numero ( chamada do FATA300 )              ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				cNumOrc := M->CJ_NUM
				While GetSX8Len() > nStack
					ConfirmSX8()
				EndDo
				EvalTrigger()
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Exibe mensagem caso tenha alterado o numero do orcamento     ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ( cNumAnt <> M->CJ_NUM )
					If !( Type("l415Auto") <> "U" .And. l415Auto )
						Help(" ",1,"NUMSEQ",,M->CJ_NUM,4,15)
					EndIf
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Event Viewer - Envia e-mail ou RSS para aprovacao de orcamento ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		   		cEventID  := "026" //Aprovacao de Orcamento
				cMessagem := STR0083+SCJ->CJ_NUM+"."
				EventInsert(FW_EV_CHANEL_ENVIRONMENT, FW_EV_CATEGORY_MODULES, cEventID,FW_EV_LEVEL_INFO,""/*cCargo*/,STR0082,cMessagem)

			Else
				Disarmtransaction()
			EndIf
		Else
			aAutoCab := MsAuto2Ench("SCJ")
			aAutoItens := MsAuto2GDb(aHeader,"TMP1")
		EndIf
	End Transaction
Else
	While GetSX8Len() > nStack
		RollBackSX8()
	EndDo
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Executa ponto de entrada na saida                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock( "MA415END" )
	ExecBlock( "MA415END", .F., .F., { nOpcA, 1 } )
EndIf

MaReleTabPrc()//Limpa array da tabela de preço

A415DesMonta(@cArqSCK,@cArqSCL)
MsUnLockAll()
RestArea(aArea)
Return (nOpcA == 1)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415Visual³ Autor ³ Eduardo Riera         ³ Data ³12.12.97  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Visualizacao de Orcamentos de Venda            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void A415Visual(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415Visual(cAlias,nReg,nOpcx)

Local aArea		:= GetArea()
Local nX		:= 0
Local cArqSCL	:= ""
Local cArqSCK	:= ""
Local oSay
Local oDlg
Local oGetDb
Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := {}
Local aPosGet   := {}
Local aInfo     := {}
Local nGetLin   := 0
Local lGrade    := MaGrade()
Private aTela[0][0]
Private aGets[0]
Private bCampo		:= { |nField| Field(nField) }
Private aHeader		:= {}
Private aHeaderSCL	:= {}
Private aHeaderSCK	:= {}
PRIVATE aGEMCVnd    := {"",{},{}} //Template GEM - Condicao de Venda

// Protecao para nao criar a variavel se ja existir, no caso de chamadas externas.
If ValType( cCadastro ) == "U"
	Private cCadastro	:= OemToAnsi( STR0001 )					//"Or‡amentos de Venda"
EndIf

Private oGrade := MsMatGrade():New('oGrade',,"CK_QTDVEN",,,;
  						{ 	{VK_F4,{|| A440Saldo(.T.,oGrade:aColsAux[oGrade:nPosLinO][aScan(oGrade:aHeadAux,{|x| AllTrim(x[2])=="CK_LOCAL"})])}} },;
  						{ 	{"CK_QTDVEN",NIL,NIL},;
  							{"CK_ITEM"	,NIL,NIL},;
  							{"CK_OPC",NIL,NIL}})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa as Variaveis da Enchoice                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SCJ")
For nX:= 1 To FCount()
	M->&(EVAL(bCampo,nX)) := FieldGet(nX)
Next nX
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader                                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
A415Monta(@cArqSCK,@cArqSCL,.F.)
aHeader := aHeaderSCK
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Faz o calculo automatico de dimensoes de objetos     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aSize := MsAdvSize()

aObjects := {}
aAdd( aObjects, { 100, 100, .t., .t. } )
aAdd( aObjects, { 100, 100, .t., .t. } )
aAdd( aObjects, { 100, 015, .t., .f. } )

aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
aPosObj := MsObjSize( aInfo, aObjects )

aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,;
	{{005,070,110,175,215,280}} )
nGetLin := aPosObj[3,1]

DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL
EnChoice( "SCJ", nReg,nOpcx,,,,,aPosObj[1],,3,,,,,,.F.)
oGetDb := MsGetDB():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"A415LinOk","A415TudOk","+CK_ITEM",.T., , ,.T., ,"TMP1")
If lGrade
	A415SCK2Gr() //Transforma itens da SCK em grade
EndIf
Private oGetDad := oGetDb
@ nGetLin,aPosGet[1,1] SAY oSay PROMPT OemToAnsi(STR0034) 	SIZE 060,009 PIXEL OF oDlg //"Total do Or‡amento "
@ nGetLin,aPosGet[1,3] SAY oSay PROMPT OemToAnsi(STR0035)	SIZE 060,009 PIXEL OF oDlg //"Descontos "
@ nGetLin,aPosGet[1,5] SAY oSay PROMPT OemToAnsi("=")		SIZE 060,009 PIXEL OF oDlg //"="
@ nGetLin,aPosGet[1,2] SAY oSay PROMPT 0    					SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
oSay:Cargo := "Valor"
@ nGetLin,aPosGet[1,4] SAY oSay PROMPT 0    					SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",14) PIXEL OF oDlg
oSay:Cargo := "Desconto"
@ nGetLin,aPosGet[1,6] SAY oSay PROMPT 0                     SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
oSay:Cargo := "Total"
A415Total(oDlg)
ACTIVATE MSDIALOG oDlg ON INIT Ma415Bar(oDlg,{||nOpcA:=1,IIf(oGetDb:TudoOk(),oDlg:End(),nOpcA:=0)},{||oDlg:End()}, nOpcx)
A415DesMonta(@cArqSCK,@cArqSCL)
MsUnLockAll()
RestArea(aArea)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415Altera³ Autor ³ Eduardo Riera         ³ Data ³11.12.97  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Alteracao de Orcamentos de Venda               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void A415Altera(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³03/08/2007³Norbert Waage  ³Na simulacao da alteracao via ExecAuto, nao ³±±
±±³          ³               ³considera os itens deletados.               ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415Altera(cAlias,nReg,nOpcx)

Local aArea		:= GetArea()
Local nX		:= 0
Local nOpcA		:= 0
Local nStack    := GetSX8Len()
Local lContinua	:= .T.
Local cArqSCL	:= ""
Local cArqSCK	:= ""
Local oDlg
Local oSay
Local oGetDb
Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := {}
Local aPosGet   := {}
Local aInfo     := {}
Local nGetLin   := 0
Local aTmp		:= {}
Local lBloqueia	:= .F.
Local lGrade    := MaGrade()

Private aTela[0][0]
Private aGets[0]
Private bCampo := { |nField| Field(nField) }
Private aHeader 	:= {}
Private aHeaderSCL:= {}
Private aHeaderSCK:= {}
PRIVATE aGEMCVnd    := {"",{},{}} //Template GEM - Condicao de Venda
Private nQtdEmp		:= 0

// Protecao para nao criar a variavel se ja existir, no caso de chamadas externas.
If ValType( cCadastro ) == "U"
	Private cCadastro	:= OemToAnsi( STR0001 )					//"Or‡amentos de Venda"
EndIf

Private oGrade := MsMatGrade():New('oGrade',,"CK_QTDVEN",,"A415VlCpGr()",;
  						{ 	{VK_F4,{|| A440Saldo(.T.,oGrade:aColsAux[oGrade:nPosLinO][aScan(oGrade:aHeadAux,{|x| AllTrim(x[2])=="CK_LOCAL"})])}} },;
  						{ 	{"CK_QTDVEN",NIL,NIL},;
  							{"CK_ITEM"	,NIL,NIL},;
  							{"CK_OPC",NIL,NIL}})

l415Auto := If(Type("l415Auto")<>"U",l415Auto,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Se for uma simulacao, ignora os itens deletados    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If l415Auto .AND. !(Type("lOnUpDate") == "U" .Or. lOnUpdate)
	For nX := 1 To Len(aAutoItens)
		If aScan(aAutoItens[nX],{|x| AllTrim(Upper(x[1])) == "AUTDELETA"}) == 0
			aAdd(aTmp,aClone(aAutoItens[nX]))
		EndIf
	Next nX
	aAutoItens	:= aClone(aTmp)
	aTmp		:= {}
EndIf

//-- Como e alteracao so ira mostrar opcionais
//-- caso alterere algo na linha (controle na A415FldOk)
If Type("lShowOpc") == "L"
	lShowOpc := .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ VerIfica se o Orcamento pode ser Alterado                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( MaCanAltOrc() )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa as Variaveis da Enchoice                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SCJ")
	lContinua := SoftLock("SCJ")
	For nX:= 1 To FCount()
		M->&(EVAL(bCampo,nX)) := FieldGet(nX)
	Next nX

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta aHeader                                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If A415Monta(@cArqSCK,@cArqSCL,.F.,{|| MaCanAltOrc() })
		aHeader := aHeaderSCK
		If Type("l415Auto") != "L" .or. !l415Auto
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Faz o calculo automatico de dimensoes de objetos     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aSize := MsAdvSize()

			aObjects := {}
			aAdd( aObjects, { 100, 100, .t., .t. } )
			aAdd( aObjects, { 100, 100, .t., .t. } )
			aAdd( aObjects, { 100, 015, .t., .f. } )

			aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
			aPosObj := MsObjSize( aInfo, aObjects )

			aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,;
				{{005,070,110,175,215,280}} )
			nGetLin := aPosObj[3,1]
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Habilita a Tecla F4                                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			SetKey(VK_F4,{||A415F4()})
			DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL//"Or‡amentos de Venda"
			EnChoice( "SCJ" ,nReg,nOpcx,,,,,aPosObj[1],,3,,,"A415VldTOk",,,.F.)
			oGetDb := MsGetDB():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"A415LinOk","A415TudOk","+CK_ITEM",.T., , ,.T.,,"TMP1","A415FldOk")
			If lGrade
				A415SCK2Gr() //Transforma itens da SCK em grade
			EndIf
			Private oGetDad := oGetDb
			@ nGetLin,aPosGet[1,1] SAY oSay PROMPT OemToAnsi(STR0034) 	SIZE 060,009 PIXEL OF oDlg //"Total do Or‡amento "
			@ nGetLin,aPosGet[1,3] SAY oSay PROMPT OemToAnsi(STR0035) 	SIZE 060,009 PIXEL OF oDlg //"Descontos "
			@ nGetLin,aPosGet[1,5] SAY oSay PROMPT OemToAnsi("=")        SIZE 060,009 PIXEL OF oDlg //"="
			@ nGetLin,aPosGet[1,2] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
			oSay:Cargo := "Valor"
			@ nGetLin,aPosGet[1,4] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",14) PIXEL OF oDlg
			oSay:Cargo := "Desconto"
			@ nGetLin,aPosGet[1,6] SAY oSay PROMPT 0                     SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
			oSay:Cargo := "Total"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Deleta os bonus para reavaliacao                             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			A415Bonus(2)
			A415Total(oDlg)
			ACTIVATE MSDIALOG oDlg ON INIT Ma415Bar(oDlg,{||nOpcA:=1,IIf(A415VldTOk(@lBloqueia).And.oGetDb:TudoOk().And.Obrigatorio(aGets,aTela),oDlg:End(),nOpcA:=0)},{||oDlg:End()},nOpcx)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Desabilita a Tecla F4                                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			SetKey(VK_F4,Nil)
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Tratamento da Rotina automatica                               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nOpcA := 0
			If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)}) .And. MsGetDBAuto("TMP1",aAutoItens,"A415LinOk",{|| A415TudOk() .And. A415VldTOk(@lBloqueia)},aAutoCab,aRotina[nOpcx][4])
				nOpcA := 1
			EndIf
		EndIf
	EndIf
	If ( nOpcA == 1 )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Efetua tratamento do Bonus                                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		A415Bonus(1)
		If Type("lOnUpDate") == "U" .Or. lOnUpdate
			Begin Transaction
				If lGrade
					A415Gr2SCK() //Transforma grade em itens da SCK
				EndIf
				dbSelectArea(cAlias)
				MsGoto(nReg)
				If ( A415Grava(2,lBloqueia) )
					EvalTrigger()
					While GetSX8Len() > nStack
						ConfirmSX8()
					EndDo
				EndIf
			End Transaction
		Else
			aAutoCab := MsAuto2Ench("SCJ")
			aAutoItens := MsAuto2GDb(aHeader,"TMP1")
		EndIf
	Else
		While GetSX8Len() > nStack
			RollBackSX8()
		EndDo
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Executa ponto de entrada na saida                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock( "MA415END" )
		ExecBlock( "MA415END", .F., .F., { nOpcA, 2 } )
	EndIf

	A415DesMonta(@cArqSCK,@cArqSCL)
Else
	Help(" ",1,"A415ALTERA")
EndIf
MsUnLockAll()
RestArea(aArea)
Return(nOpcA == 1)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415Exclui³ Autor ³ Eduardo Riera         ³ Data ³11.12.97  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Alteracao de Orcamentos de Venda               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void A415Exclui(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
function A415Exclui(cAlias,nReg,nOpcx)

Local aArea		:= GetArea()
Local nX		:= 0
Local nStack    := GetSX8Len()
Local nOpcA		:= 0
Local cArqSCL	:= ""
Local cArqSCK	:= ""
Local lContinua:= .T.
Local oDlg
Local oSay
Local oGetDb
Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := {}
Local aPosGet   := {}
Local aInfo     := {}
Local nGetLin   := 0
Local lGrade    := MaGrade()
Local lRet 	  := .T.

Private aTela[0][0]
Private aGets[0]
Private bCampo := { |nField| Field(nField) }
Private aHeader		:= {}
Private aHeaderSCL	:= {}
Private aHeaderSCK	:= {}
PRIVATE aGEMCVnd    := {"",{},{}} //Template GEM - Condicao de Venda

// Protecao para nao criar a variavel se ja existir, no caso de chamadas externas.
If ValType( cCadastro ) == "U"
	Private cCadastro	:= OemToAnsi( STR0001 )					//"Or‡amentos de Venda"
EndIf

Private oGrade := MsMatGrade():New('oGrade',,"CK_QTDVEN",,".T.",;
  						{ 	{VK_F4,{|| A440Saldo(.T.,oGrade:aColsAux[oGrade:nPosLinO][aScan(oGrade:aHeadAux,{|x| AllTrim(x[2])=="CK_LOCAL"})])}} },;
  						{ 	{"CK_QTDVEN",NIL,NIL},;
  							{"CK_ITEM"	,NIL,NIL},;
  							{"CK_OPC",NIL,NIL}})

l415Auto := If(Type("l415Auto")<>"U",l415Auto,.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ VerIfica se o Orcamento pode ser Excluido                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( MaNoDelOrc("SCJ") )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa as Variaveis da Enchoice                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SCJ")
	lContinua := Softlock("SCJ")
	For nX:= 1 To FCount()
		M->&(EVAL(bCampo,nX)) := FieldGet(nX)
	Next nX
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta aHeader                                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If A415Monta(@cArqSCK,@cArqSCL,.F.,{|| MaNoDelOrc() })
		aHeader := aHeaderSCK
		If Type("l415Auto") != "L" .or. !l415Auto
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Faz o calculo automatico de dimensoes de objetos     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aSize := MsAdvSize()

			aObjects := {}
			aAdd( aObjects, { 100, 100, .t., .t. } )
			aAdd( aObjects, { 100, 100, .t., .t. } )
			aAdd( aObjects, { 100, 015, .t., .f. } )

			aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
			aPosObj := MsObjSize( aInfo, aObjects )

			aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,;
				{{005,070,110,175,215,280}} )
			nGetLin := aPosObj[3,1]

			DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL//"Or‡amentos de Venda"
			EnChoice("SCJ",nReg,nOpcx,,,,,APosObj[1],,3,,,,,,.F.)
			oGetDb := MsGetDB():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"A415LinOk","A415TudOk","+CK_ITEM",.T., , ,.T., ,"TMP1")
			If lGrade
				A415SCK2Gr() //Transforma itens da SCK em grade
			EndIf
			Private oGetDad := oGetDb
			@ nGetLin,aPosGet[1,1] SAY oSay PROMPT OemToAnsi(STR0034) 	SIZE 060,009 PIXEL OF oDlg //"Total do Or‡amento "
			@ nGetLin,aPosGet[1,3] SAY oSay PROMPT OemToAnsi(STR0035) 	SIZE 060,009 PIXEL OF oDlg //"Descontos "
			@ nGetLin,aPosGet[1,5] SAY oSay PROMPT OemToAnsi("=")        SIZE 060,009 PIXEL OF oDlg //"="
			@ nGetLin,aPosGet[1,2] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
			oSay:Cargo := "Valor"
			@ nGetLin,aPosGet[1,4] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",14) PIXEL OF oDlg
			oSay:Cargo := "Desconto"
			@ nGetLin,aPosGet[1,6] SAY oSay PROMPT 0                     SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
			oSay:Cargo := "Total"
			A415Total(oDlg)
			ACTIVATE MSDIALOG oDlg ON INIT Ma415Bar(oDlg,{||nOpcA:=1,IIf(oGetDb:TudoOk(),oDlg:End(),nOpcA:=0)},{||oDlg:End()},nOpcx)
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Tratamento da Rotina automatica                               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			nOpcA := 0
			If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)}) .And. MsGetDBAuto("TMP1",aAutoItens,"AllwaysTrue",{|| .T. },aAutoCab,aRotina[nOpcx][4])
				nOpcA := 1
			EndIf
		EndIf
	Else
		Help(" ",1,"A415EXCLUI")
	EndIf
	If ( nOpcA == 1 )
		If ( ExistBlock("MT415EXC") )
			lRet:= ExecBlock("MT415EXC",.F.,.F.)
			If ValType(lRet) <> "L"
				lRet := .F.
			EndIf
		EndIf
		
		If lRet
			If Type("lOnUpDate") == "U" .Or. lOnUpdate
				Begin Transaction
					If ( A415Grava(3) )
						EvalTrigger()
						While GetSX8Len() > nStack 
							ConfirmSX8()
						EndDo
					EndIf
				End Transaction
			Else
				aAutoCab := MsAuto2Ench("SCJ")
				aAutoItens := MsAuto2GDb(aHeader,"TMP1")			
			EndIf       
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Exclui a amarracao com os conhecimentos                      ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			MsDocument( "SCJ", SCJ->( RecNo() ), 2, , 3 ) 
		EndIf	
	Else
		While GetSX8Len() > nStack 
			RollBackSX8()
		EndDo 
	EndIf               
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Executa ponto de entrada na saida                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock( "MA415END" ) 
		ExecBlock( "MA415END", .F., .F., { nOpcA, 3 } ) 
	EndIf 	
	
	A415DesMonta(@cArqSCK,@cArqSCL)
EndIf
MsUnLockAll()
RestArea(aArea)
Return(lRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415Copia ³ Autor ³ Eduardo Riera         ³ Data ³05.01.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Copia dos Orcamentos de Venda                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void A415Copia (ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415Copia(cAlias,nReg,nOpcx)

Local aArea		:= GetArea()
Local nX		:= 0
Local nOpcA		:= 0
Local nStack    := GetSX8Len()
Local cArqSCL	:= ""
Local cArqSCK	:= ""
Local lMt415Cpy := ExistBlock("MT415CPY")
Local oDlg
Local oSay
Local oGetDB
Local aPosObj   := {}
Local aObjects  := {}
Local aSize     := {}
Local aPosGet   := {}
Local aInfo     := {}
Local aCposCopy := {}
Local nGetLin   := 0
Local lContinua := .T.
Local lGrade    := MaGrade()

Private aTela[0][0]
Private aGets[0]
Private bCampo := { |nField| Field(nField) }
Private aHeader 	:= {}
Private aHeaderSCL	:= {}
Private aHeaderSCK	:= {}
PRIVATE aGEMCVnd    := {"",{},{}} //Template GEM - Condicao de Venda
Private nQtdEmp		:= 0

// Protecao para nao criar a variavel se ja existir, no caso de chamadas externas.
If ValType( cCadastro ) == "U"
	Private cCadastro	:= OemToAnsi( STR0001 )					//"Or‡amentos de Venda"
EndIf

Private oGrade := MsMatGrade():New('oGrade',,"CK_QTDVEN",,"A415VlCpGr()",;
  						{ 	{VK_F4,{|| A440Saldo(.T.,oGrade:aColsAux[oGrade:nPosLinO][aScan(oGrade:aHeadAux,{|x| AllTrim(x[2])=="CK_LOCAL"})])}} },;
  						{ 	{"CK_QTDVEN",NIL,NIL},;
  							{"CK_ITEM"	,NIL,NIL},;
  							{"CK_OPC",NIL,NIL}})

IF ( (ExistBlock("M415COPIA")) )
	If (! ExecBlock("M415COPIA",.F.,.F.) )
		lContinua := .F.
	EndIf
EndIf

If ( xFilial("SCJ") == SCJ->CJ_FILIAL ) .And. lContinua
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa as Variaveis da Enchoice                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	If lMt415Cpy
		aCposCopy := ExecBlock("MT415CPY",.F.,.F.)
	Endif

	dbSelectArea("SCJ")
	For nX:= 1 To FCount()
		If Ascan(aCposCopy, EVAL(bCampo,nX) ) == 0
			M->&(EVAL(bCampo,nX)) := FieldGet(nX)
		Else
			M->&(EVAL(bCampo,nX)) := CriaVar(EVAL(bCampo,nX),.T.)
		Endif
	Next nX
	M->CJ_NUM     	:= CriaVar("CJ_NUM",.T.)
	M->CJ_EMISSAO 	:= dDataBase
	M->CJ_VALIDA  	:= CriaVar("CJ_VALIDA",.T.)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta aHeader                                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	A415Monta(@cArqSCK,@cArqSCL,.F., , ,.T.)
	aHeader := aHeaderSCK

	//
	// Template GEM - Gestao de Empreendimentos Imobiliarios
	//
	// faz a copia da condicao de venda se a mesma tiver
	// uma vinculacao com a condicao de pagamento
	//
	If ExistTemplate("GEMCPYORC")
		// copia a condicao de venda
		aGEMCVnd := ExecTemplate("GEMCPYORC",.F.,.F.,{ aGEMCVnd ,SCJ->CJ_NUM ,SCJ->CJ_CONDPAG })
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Habilita a Tecla F4                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SetKey(VK_F4,{||A415F4()})
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Faz o calculo automatico de dimensoes de objetos     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aSize := MsAdvSize()

	aObjects := {}
	aAdd( aObjects, { 100, 100, .t., .t. } )
	aAdd( aObjects, { 100, 100, .t., .t. } )
	aAdd( aObjects, { 100, 015, .t., .f. } )

	aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
	aPosObj := MsObjSize( aInfo, aObjects )

	aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,;
		{{005,070,110,175,215,280}} )
	nGetLin := aPosObj[3,1]

	DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL//"Or‡amentos de Venda"
	EnChoice( "SCJ" ,nReg,nOpcx,,,,,aPosObj[1],,3,,,"A415VldTOk",,,.F.)
	oGetDb := MsGetDB():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"A415LinOk","A415TudOk","+CK_ITEM",.T., , ,.T., ,"TMP1", , ,.F.)
	If lGrade
		A415SCK2Gr() //Transforma itens da SCK em grade
	EndIf
	Private oGetDad := oGetDb
	@ nGetLin,aPosGet[1,1] SAY oSay PROMPT OemToAnsi(STR0034) 	SIZE 060,009 PIXEL OF oDlg //"Total do Or‡amento "
	@ nGetLin,aPosGet[1,3] SAY oSay PROMPT OemToAnsi(STR0035) 	SIZE 060,009 PIXEL OF oDlg //"Descontos "
	@ nGetLin,aPosGet[1,5] SAY oSay PROMPT OemToAnsi("=")        SIZE 060,009 PIXEL OF oDlg //"="
	@ nGetLin,aPosGet[1,2] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
	oSay:Cargo := "Valor"
	@ nGetLin,aPosGet[1,4] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",14) PIXEL OF oDlg
	oSay:Cargo := "Desconto"
	@ nGetLin,aPosGet[1,6] SAY oSay PROMPT 0                     SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
	oSay:Cargo := "Total"
	A415Total(oDlg)
	ACTIVATE MSDIALOG oDlg ON INIT Ma415Bar(oDlg,{||nOpcA:=1,IIf(oGetDb:TudoOk().And.Obrigatorio(aGets,aTela),oDlg:End(),nOpcA:=0)},{||oDlg:End()},nOpcx)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Desabilita a Tecla F4                                        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SetKey(VK_F4,Nil)
	If ( nOpcA == 1 )
		If Type("lOnUpDate") == "U" .Or. lOnUpdate
			Begin Transaction
				If lGrade
					A415Gr2SCK() //Transforma grade em itens da SCK
				EndIf
				If ( A415Grava(1) )
					EvalTrigger()
					While GetSX8Len() > nStack
						ConfirmSX8()
					EndDo
					If (ExistBlock("MT415G1"))
						ExecBlock("MT415G1",.F.,.f.)
					EndIf
				EndIf
			End Transaction
		Else
			aAutoCab := MsAuto2Ench("SCJ")
			aAutoItens := MsAuto2GDb(aHeader,"TMP1")
		EndIf
	Else
		While GetSX8Len() > nStack
			RollBackSX8()
		EndDo
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Executa ponto de entrada na saida                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ExistBlock( "MA415END" )
		ExecBlock( "MA415END", .F., .F., { nOpcA, 1 } )
	EndIf

	A415DesMonta(@cArqSCK,@cArqSCL)
	MsUnLockAll()
EndIf
RestArea(aArea)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415Cancel³ Autor ³ Eduardo Riera         ³ Data ³07.01.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Cancelamento dos Orcamentos de Venda           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void A415Cancel(ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA416                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415Cancel(cAlias,nReg,nOpcx)

Local aArea		:= GetArea()
Local nX			:= 0
Local cArqSCL		:= ""
Local cArqSCK		:= ""
Local nOpcA		:= 0
Local lContinua 	:= .T.
Local oDlg
Local oSay
Local oGetDb
Local lGrade    	:= MaGrade()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Integracao SIGAFAT e SIGADPR                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lGerouDPR	:= .F.
Local aItemDPR	:= {}
Local lIFatDpr	:= SuperGetMV("MV_IFATDPR",.F.,.F.)
Local lVExAutDPR	:= ( Type("lExAutoDPR") == "L" )

Local cFilSCK		:= xFilial("SCK")

Private aTela[0][0]
Private aGets[0]
Private bCampo 		:= { |nField| Field(nField) }
Private aHeader		:= {}
Private aHeaderSCL	:= {}
Private aHeaderSCK	:= {}
PRIVATE aGEMCVnd   	:= {"",{},{}} //Template GEM - Condicao de Venda

// Protecao para nao criar a variavel se ja existir, no caso de chamadas externas.
If ValType( cCadastro ) == "U"
	Private cCadastro	:= OemToAnsi( STR0001 )					//"Or‡amentos de Venda"
EndIf

Private oGrade := MsMatGrade():New('oGrade',,"CK_QTDVEN",,".T.",;
  						{ 	{VK_F4,{|| A440Saldo(.T.,oGrade:aColsAux[oGrade:nPosLinO][aScan(oGrade:aHeadAux,{|x| AllTrim(x[2])=="CK_LOCAL"})])}} },;
  						{ 	{"CK_QTDVEN",NIL,NIL},;
  							{"CK_ITEM"	,NIL,NIL},;
  							{"CK_OPC",NIL,NIL}})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Somente pode ser cancelados orcamentos em aberto             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( MaCanAltOrc() )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa as Variaveis da Enchoice                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SCJ")
	lContinua := Softlock("SCJ")
	For nX:= 1 To FCount()
		M->&(EVAL(bCampo,nX)) := FieldGet(nX)
	Next nX
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta aHeader                                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lContinua := A415Monta(@cArqSCK,@cArqSCL,.F.,{ || MaCanAltOrc() })
	aHeader := aHeaderSCK
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Habilita a Tecla F4                                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SetKey(VK_F4,{||A415F4()})
	If  ( lContinua )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Faz o calculo automatico de dimensoes de objetos     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aSize := MsAdvSize()

		aObjects := {}
		aAdd( aObjects, { 100, 100, .t., .t. } )
		aAdd( aObjects, { 100, 100, .t., .t. } )
		aAdd( aObjects, { 100, 015, .t., .f. } )

		aInfo := { aSize[ 1 ], aSize[ 2 ], aSize[ 3 ], aSize[ 4 ], 3, 3 }
		aPosObj := MsObjSize( aInfo, aObjects )

		aPosGet := MsObjGetPos(aSize[3]-aSize[1],315,;
			{{005,070,110,175,215,280}} )
		nGetLin := aPosObj[3,1]

		DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] of oMainWnd PIXEL//"Or‡amentos de Venda"
		EnChoice( "SCJ", nReg,nOpcx,,,,,aPosObj[1],,3,,,,,,.F.)
		oGetDb := MsGetDB():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],nOpcx,"A415LinOk","A415TudOk","+CK_ITEM",.T., , ,.T., ,"TMP1")
		If lGrade
			A415SCK2Gr()
		EndIf
		Private oGetDad := oGetDb
		@ nGetLin,aPosGet[1,1] SAY oSay PROMPT OemToAnsi(STR0034) 	SIZE 060,009 PIXEL OF oDlg //"Total do Or‡amento "
		@ nGetLin,aPosGet[1,3] SAY oSay PROMPT OemToAnsi(STR0035) 	SIZE 060,009 PIXEL OF oDlg //"Descontos "
		@ nGetLin,aPosGet[1,5] SAY oSay PROMPT OemToAnsi("=")        SIZE 060,009 PIXEL OF oDlg //"="
		@ nGetLin,aPosGet[1,2] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
		oSay:Cargo := "Valor"
		@ nGetLin,aPosGet[1,4] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",14) PIXEL OF oDlg
		oSay:Cargo := "Desconto"
		@ nGetLin,aPosGet[1,6] SAY oSay PROMPT 0                     SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
		oSay:Cargo := "Total"
		A415Total(oDlg)
		ACTIVATE MSDIALOG oDlg ON INIT Ma415Bar(oDlg,{||nOpcA:=1,IIf(oGetDb:TudoOk(),oDlg:End(),nOpcA:=0)},{||oDlg:End()}, nOpcx)
	Else
		Help(" ",1,"A415CANCEL")
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Ponto de entrada para a validacao do cancelamento             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	If ExistBlock("M415CANC")
		nOpca := ExecBlock("M415CANC",.F.,.F.,nOpca)
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Desabilita a Tecla F4                                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	SetKey(VK_F4,Nil)

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ M_SER004_CRM019_Integraçao_Faturamento_DPR                           ³
	//³ Verifica se existe item do tipo "Desenvolvimento" e solicita		   ³ 
	//³	confirmacao de cancelamento caso encontre.							   ³
	//³ Autor: Thiago Tavares													   ³
	//³ Data: 25/10/2013															   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lContinua := .T.
	If lIFatDpr .And. !(l415Auto)
		DbSelectArea("SCK")
		SCK->( DbSetOrder(1) )		// CK_FILIAL+CK_NUM+CK_ITEM+CK_PRODUTO                                                                                                                              
		SCK->( MsSeek(cFilSCK + M->CJ_NUM,.T.) )
		While ( !Eof() .And. SCK->CK_FILIAL == cFilSCK .And. SCK->CK_NUM == M->CJ_NUM )
			If (SCK->CK_TPPROD == "2")
				lGerouDPR := .T.
				Exit 
			Endif
			SCK->( dbSkip() )
		EndDo
	
		If lGerouDPR  
			lContinua := MsgYesNo(STR0085)		// Orcamento possui Pendencia de Desenvolvimento. Confirma Cancelar?
		EndIf
	EndIf	

	If lContinua
		If ( nOpcA == 1 )
			Begin Transaction
				dbSelectArea("SCJ")
				RecLock("SCJ",.F.)
				dbSelectArea("SCK")
				dbSetOrder(1)
				MsSeek(cFilSCK+SCJ->CJ_NUM)
				While ( !Eof() .And. SCK->CK_FILIAL == cFilSCK .And. SCK->CK_NUM == SCJ->CJ_NUM )
					MaAvalOrc("SCK",4)
					dbSelectArea("SCK")

					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ M_SER004_CRM019_Integraçao_Faturamento_DPR                           ³
					//³ Verifica se o item eh do tipo "Desenvolvimento" e grava num Array	   ³ 
					//³	para excluir pendencia de desenvolvimento.							   ³
					//³ Autor: Thiago Tavares													   ³
					//³ Data: 06/05/2014															   ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If (  lIFatDpr .And. (lVExAutDPR .And. !(lExAutoDPR)) .And. (SCK->CK_TPPROD == "2") )
						aItemDPR := {5, cFilSCK, SCJ->CJ_NUM, SCK->CK_ITEM, SCK->CK_PRODUTO, ""}
						If !(A415GrvDPR(aItemDPR))
							Disarmtransaction()
						EndIf
						aItemDPR := {}
					EndIf

					dbSkip()
				EndDo
				MaAvalOrc("SCJ",14)
				MsUnlock()
			End Transaction
		EndIf
		A415DesMonta(@cArqSCK,@cArqSCL)
	EndIf
Else
	Help(" ",1,"A415CANCEL")
EndIf
MsUnLockAll()
RestArea(aArea)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415Baixa ³ Autor ³ Eduardo Riera         ³ Data ³07.01.98  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Baixa dos Orcamentos de Venda                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void A415Baixa (ExpC1,ExpN1,ExpN2)                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA416                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415Baixa(cAlias,nReg,nOpcx)

Local aArea		:= GetArea()
Local nX		:= 0
Local cArqSCL	:= ""
Local cArqSCK	:= ""
Local lContinua	:= .T.
Local nOpcA    	:= 0
Local oDlg
Local oSay
Local oGetDb

Local aSize       := {}
Local aPosObj     := {}
Local aPosGet     := {}
Local aInfo       := {}
Local aObjects    := {}
Local nLinGet     := 0
Local lMt415Aut   := Existblock("MT415AUT")

Private aTela[0][0]
Private aGets[0]
Private bCampo 		:= { |nField| Field(nField) }
Private aHeader		:= {}
Private aHeaderSCL	:= {}
Private aHeaderSCK	:= {}
PRIVATE aGEMCVnd    := {"",{},{}} //Template GEM - Condicao de Venda

// Protecao para nao criar a variavel se ja existir, no caso de chamadas externas.
If ValType( cCadastro ) == "U"
	Private cCadastro	:= OemToAnsi( STR0001 )					//"Or‡amentos de Venda"
EndIf

l415Auto := If(Type("l415Auto")<>"U",l415Auto,.F.)
l416Auto := If(Type("l416Auto")<>"U",l416Auto,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Somente pode ser Baixados orcamentos em aberto               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( SCJ->CJ_STATUS == "A" )

	If lMt415Aut
		If !Execblock("MT415AUT",.F.,.F.)
			Return
		Endif
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Inicializa as Variaveis da Enchoice                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SCJ")
	lContinua := Softlock("SCJ")
	For nX:= 1 To FCount()
		M->&(EVAL(bCampo,nX)) := FieldGet(nX)
	Next nX
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Monta aHeader                                                ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	A415Monta(@cArqSCK,@cArqSCL,.F.)
	aHeader := aHeaderSCK
	If !(l416Auto)
		If  ( lContinua )
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Habilita a Tecla F4                                          ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			SetKey(VK_F4,{||A415F4()})

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Definicao automatica - Tamanho de objetos                    ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			aSize := MsAdvSize( , .F. )

			aObjects := {}

			aAdd( aObjects, { 100, 100, .T., .T. } )
			aAdd( aObjects, { 100, 100, .T., .T. } )
			aAdd( aObjects, { 100,  15, .T., .F. } )

			aInfo := { aSize[1], aSize[2], aSize[3], aSize[4], 3, 3 }

			aPosObj := MsObjSize( aInfo, aObjects )

			DEFINE MSDIALOG oDlg TITLE cCadastro From aSize[7],0 to aSize[6],aSize[5] PIXEL //"Or‡amentos de Venda"

			EnChoice( "SCJ", nReg,2,,,,,aPosObj[1],,3,,,,,,.F.)
			oGetDb := MsGetDB():New(aPosObj[2,1],aPosObj[2,2],aPosObj[2,3],aPosObj[2,4],2,"A415LinOk","A415TudOk","+CK_ITEM",.F., , ,.T., ,"TMP1")
			Private oGetDad := oGetDb

			aPosGet := MsObjGetPos(aPosObj[3,4]-aPosObj[3,2],315,;
				{{005,110,215,070,175,280}} )

			nLinGet := aPosObj[3,1]

			@ nLinGet,aPosGet[1,1] SAY oSay PROMPT OemToAnsi(STR0034) 	SIZE 060,009 PIXEL OF oDlg //"Total do Or‡amento "
			@ nLinGet,aPosGet[1,2] SAY oSay PROMPT OemToAnsi(STR0035) 	SIZE 060,009 PIXEL OF oDlg //"Descontos "
			@ nLinGet,aPosGet[1,3] SAY oSay PROMPT OemToAnsi("=")        SIZE 060,009 PIXEL OF oDlg //"="
			@ nLinGet,aPosGet[1,4] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
			oSay:Cargo := "Valor"
			@ nLinGet,aPosGet[1,5] SAY oSay PROMPT 0    						SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",14) PIXEL OF oDlg
			oSay:Cargo := "Desconto"
			@ nLinGet,aPosGet[1,6] SAY oSay PROMPT 0                     SIZE 040,009 PICTURE PesqPict("SCK","CK_VALOR",16) PIXEL OF oDlg
			oSay:Cargo := "Total"
			A415Total(oDlg)
			ACTIVATE MSDIALOG oDlg ON INIT Ma415Bar(oDlg,{||nOpcA:=1,oDlg:End()},{||oDlg:End()},nOpcx)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Desabilita a Tecla F4                                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			SetKey(VK_F4,Nil)
		EndIf
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Tratamento da Rotina automatica                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nOpcA := 0
		If EnchAuto(cAlias,aAutoCab,{|| Obrigatorio(aGets,aTela)}) .And. MsGetDBAuto("TMP1",aAutoItens,"A415LinOk",{|| .T.},aAutoCab,aRotina[nOpcx][4])
			nOpcA := 1
		EndIf
	EndIf
	If ( ExistBlock("MT415EFT") )
		If (!ExecBlock("MT415EFT",.F.,.F.,{nOpcA}))
			nOpcA := 0
		Endif
	EndIf
	If ( nOpcA == 1 )
		Begin Transaction
			a416GrvBx()
		End Transaction
	EndIf
	A415DesMonta(@cArqSCK,@cArqSCL)
ElseIf ( SCJ->CJ_STATUS == "B" )
	Help(" ",1,"A415BAIXA")
ElseIf ( SCJ->CJ_STATUS == "C" )
	Help(" ",1,"A415BAIXAC")
ElseIf ( SCJ->CJ_STATUS == "D" )
    Help(" ",1,"A415BAIXAD")
ElseIf ( SCJ->CJ_STATUS == "F" )
    Help(" ",1,"A415BAIXAF")
EndIf

MsUnLockAll()
RestArea(aArea)
Return(.F.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415Impri ³ Autor ³ Eduardo Riera         ³ Data ³22.12.97  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Programa de Impressao                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void A415Impri()                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415Impri()

Local cPrinter := GetMV("MV_ORCIMPR")
Local lPrinter := ExistBlock(cPrinter,,.T.)
Local lContinua	:= .T.

If Empty(cPrinter)
	lContinua := .F.
	Aviso(STR0075,STR0090,{ STR0092 },2) //"Favor informar o RDMAKE para impressão através do parametro MV_ORCIMPR."
EndIf

If lContinua
	If (lPrinter)
		ExecBlock(cPrinter,.F.,.F.)
	Else
		Aviso(STR0075,STR0091,{ STR0092 },2) //"RDMAKE informado no parametro MV_ORCIMPR não se encontra no repositório, favor verificar."
	EndIf
EndIf
dbSelectArea("SCJ")
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415Monta   ³ Autor ³Eduardo Riera        ³ Data ³ 08.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Monta aHeader e aCols dos arquivos Temporarios             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void A415Monta(Void)                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Arquivo temporario SCK                               ³±±
±±³          ³ExpC2: Arquivo temporario SCL                               ³±±
±±³          ³ExpL3: Indica se esta no modulo de inclusao                 ³±±
±±³          ³ExpB4: CodeBlock a ser avaliado para o retorno da funcao    ³±±
±±³          ³ExpL5: Indica se os campos de Cotacao devem ser habilitados ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: Conforme indicado pelo CodeBlock em ExpB4, quando o ³±±
±±³          ³ este indicar .F., a execucao da funcao sera interrompida.  ³±±
±±³          ³ Para o codeblock sera informado o alias do SCJ e do SCK.   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415Monta(cArqSCK,cArqSCL,lInclui,bBlock,lCotacao, lCopia)

Local aArea			:= GetArea()
Local aAreaSCK		:= SCK->(GetArea())
Local aAreaSCL		:= SCL->(GetARea())
Local aNoFields		:= {}
Local aCpoNCopia	:= {}
Local lRetorno		:= .T.
Local bCond   				// Se bCond .T. executa bAction1, senao executa bAction2
Local bAction1 				// Retornar .T. para considerar o registro e .F. para desconsiderar
Local bAction2 				// Retornar .T. para considerar o registro e .F. para desconsiderar
Local aAreaSX3		:= SX3->(GetArea())
Local aCpoVirtual 	:= {}
Local cQuery   		:= ""
Local aNoCpoSCK 	:= {}
Local lPeM415cpo 	:= ExistBlock("M415NCPO")

Local cFilSCK		:= xFilial("SCK")

DEFAULT lCotacao := !Empty(lCotacao)
DEFAULT bBlock   := {|| .T.}
DEFAULT aStackTMP:= {}
DEFAULT lCopia   := .F.

Private lRetAux := .T.

bCond    := {|| .T.}
bAction1 := bBlock
bAction2 := {|| .F. }

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aHeader do SCK                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o TMP1 esta aberto                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Select("TMP1")<>0
	dbSelectArea("TMP1")
	dbCloseArea()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta arquivo de Trabalho TMP1                               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SCK")
dbSetOrder(1)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Guarda o TMP1 na Pilha - Utilizado no Sales Tracker          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aAdd(aStackTMP ,{cArqSCK,"",""})

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Preenchimento do arquivo temporario                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³FillGetDb( nOpcx, cAlias, cTrb, cKeyInd, nOrder, cSeekKey, bSeekWhile, uSeekFor, aNoFields, aYesFields,		³
//³			  lOnlyYes, cQuery, bMountFile )																							³
//³nOpcx			- Opcao (inclusao, exclusao, etc.)																					³
//³cAlias		- Alias da tabela referente aos itens																				³
//³cTrb			- Alias da tabela temporaria																							³
//³cKeyInd		- Chave com indice a ser criado caso nao exista no SINDEX													³
//³nOrder		- Ordem do SINDEX																											³
//³cSeekKey		- Chave de pesquisa																										³
//³bSeekWhile	- Loop na tabela cALias																									³
//³uSeekFor		- Valida cada registro da tabela cALias (retornar .T. para considerar e .F. para desconsiderar)	³
//³aNoFields	- Array com nome dos campos que serao excluidos na montagem do aHeader									³
//³aYesFields	- Array com nome dos campos que serao incluidos na montagem do aHeader									³
//³lOnlyYes		- Flag indicando se considera somente os campos declarados no aYesFields + campos do usuario		³
//³cQuery		- Query para filtro da tabela cAlias (se for TOP e cQuery estiver preenchido, desconsidera 		³
//³				  parametros cSeekKey e bSeekWhiele) 																				³
//³bMountFile	- Preenchimento do aCols pelo usuario (aHeader e aCols ja estarao criados)								³
//³aNoHeader	- Array com campos que serao utilizados porem não podem se apresentados no aHeader. Ex. campos	³
//³              utilizados no cKeyInd                                                                         ³
//³lInclui		- Se inclusao passar .T. para qua aCols seja incializada com 1 linha em branco                  ³
//³aCpoEmpty	- Array com campos que serao apresentados vazios (utilizado na opcao Copia)                     ³
//³aCpoVirtual	- Array com campos virtuais que devem ser apresentados                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cQuery := ""
cSeek := ""
cWhile := ""
If ( !lInclui )

	cQuery := "SELECT SCK.*,SCK.R_E_C_N_O_ SCKRECNO "
	cQuery += "FROM "+RetSqlName("SCK")+" SCK "
	cQuery += "WHERE "
	cQuery += "SCK.CK_FILIAL='" + cFilSCK + "' AND "
	cQuery += "SCK.CK_NUM='"+SCJ->CJ_NUM+"' AND "
	cQuery += "SCK.D_E_L_E_T_ = ' ' "
	cQuery += "ORDER BY "+SqlOrder(SCK->(IndexKey()))

	cSeek := cFilSCK + SCJ->CJ_NUM
	cWhile := "SCK->CK_FILIAL + SCK->CK_NUM"
EndIf

If lCopia
	aCpoNCopia := { "CK_NUMPV","CK_NUMOP","CK_CONTRAT","CK_ITEMCON"}
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inclusao de campos virtuais em uso na tabela SCK³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aCpoVirtual := {}
DbSelectArea("SX3")
DbSetOrder(1)
DbSeek("SCK")
While !EOF() .AND. SX3->X3_ARQUIVO == "SCK"
	If X3Uso(SX3->X3_USADO) .AND. cNivel >= SX3->X3_NIVEL .AND. SX3->X3_CONTEXT == "V"
		aAdd(aCpoVirtual, AllTrim(SX3->X3_CAMPO))
	EndIf
	DbSkip()
End
RestArea(aAreaSX3)
If aScan(aCpoVirtual, {|x|x=="CK_OPER"}) <= 0
	aAdd(aCpoVirtual, "CK_OPER")
EndIf

If lPeM415cpo 
	aNoCpoSCK := ExecBlock("M415NCPO",.F.,.F.) 
EndIf

FillGetDb(2, "SCK", "TMP1",,1,cSeek, { || &cWhile },{{bCond,bAction1,bAction2}},aNoFields,,,cQuery,,aNoCpoSCK,lInclui,aCpoNCopia,aCpoVirtual)
aHeaderSCK := aClone(aHeader)

lRetorno := lRetAux
If ( lRetorno )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o TMP2 esta aberto                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Select("TMP2")<>0
		dbSelectArea("TMP2")
		dbCloseArea()
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Preenchimento do arquivo temporario                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cQuery := ""
	cSeek := ""
	cWhile := ""

	If ( !lInclui )
	
		cQuery := "SELECT SCL.*,SCL.R_E_C_N_O_ SCLRECNO "
		cQuery += "FROM "+RetSqlName("SCL")+" SCL "
		cQuery += "WHERE "
		cQuery += "SCL.CL_FILIAL='"+xFilial("SCL")+"' AND "
		cQuery += "SCL.CL_NUM='"+SCJ->CJ_NUM+"' AND "
		cQuery += "SCL.D_E_L_E_T_ = ' ' "
		cQuery := ChangeQuery(cQuery)
	
	EndIf

	aHeader := {}
	aNoHeader := {}
	aAdd(aNoHeader,"CL_NUM")
	aAdd(aNoHeader,"CL_ITEMORC")
	FillGetDb(2, "SCL", "TMP2","CL_NUM+CL_ITEMORC+CL_ITEM+CL_PRODUTO",1,cSeek, { || &cWhile }, , , , ,cQuery , ,aNoHeader)
	aHeaderSCL := aClone(aHeader)

EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Ponto de entrada para manipular as informações do orçamento quando chamado ³
//³da oportunidade de Vendas                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRetorno .And. (IsInCallStack("FATA300") .OR. IsInCallStack("CRMA110") .Or. IsInCallStack("FATA600"))  .And. (Inclui .or. Altera)
	If ExistBlock( "MA415OPOR" )
		lRet:= ExecBlock( "MA415OPOR", .F., .F.)
	   	If ValType(lRet) == "L"
	   		lRetorno:= lRet
	   	EndIf
	EndIf
EndIf
RestArea(aAreaSCK)
RestArea(aAreaSCL)
RestArea(aArea)
Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415DesMonta³ Autor ³Eduardo Riera        ³ Data ³ 08.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Desmonta os arquivos temporarios.                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void A415DesMonta( )                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Arquivo temporario SCK                               ³±±
±±³          ³ExpC2: Arquivo temporario SCL                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415DesMonta(cArqSCK,cArqSCL)

If Select("TMP1")<>0 .And. !IsInCallStack("A415TRACK")
	dbSelectArea("TMP1")
	TMP1->(dbCloseArea())
EndIf

If Select("TMP2")<>0 .And. !IsInCallStack("A415TRACK")
	dbSelectArea("TMP2")
	TMP2->(dbCloseArea())
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Controle de Pilha do Sales Tracker - Quando o orcamento e chamada varias vezes ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Len(aStackTMP) > 0
	aStackTMP  := aDel(aStackTMP ,Len(aStackTMP ))
	aStackTMP  := aSize(aStackTMP ,Len(aStackTMP )-1)
EndIf

Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415Grava   ³ Autor ³Eduardo Riera        ³ Data ³ 08.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Grava os Orcamentos de Venda.                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void A415Grava(nExpN1)                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ nExpN1 : 1 - Inclusao / 2 - Alteracao / 3 - Excluir        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415Grava(nOpcx,lBloqOrc)

Local aRecTMP1  := {}

Local nX  	   := 0
Local nPosField:= 0
Local cItem    := "00"
Local cItemAnt := ""
Local cNumOrc  := ""
Local cMay     := ""
Local lGravou  := .F.
Local l415Grava:= ExistBlock("M415GRV")
Local nCnt     := 0
Local cStatCab := "A"
Local lOrcOrc  := .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Integracao SIGAFAT e SIGADPR                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local lIFatDpr	:= SuperGetMV("MV_IFATDPR",.F.,.F.)
Local aHistProd	:= {}
Local nI		:= 0
Local lContinua	:= .T.
Local aItemDPR	:= {}
Local lGerouDPR 	:= .T.
Local nProduto		:= aScan(aHeader,{|x| Trim(x[2]) == "CK_PRODUTO" })
Local nItem		:= aScan(aHeader,{|x| Trim(x[2]) == "CK_ITEM" })
Local cCodProd		:= ""
Local cItemSeq		:= ""
Local cTpProd		:= ""
Local lVExAutDPR	:= ( Type("lExAutoDPR") == "L" )
Local lDisarmTrans	:= .F.

Local cFilSCJ		:= xFilial("SCJ")
Local cFilSCK		:= xFilial("SCK")
Local cFilSCL		:= xFilial("SCL")

Local nTmCKItem	:= GetSx3Cache("CK_ITEM","X3_TAMANHO")

Default lBloqOrc	:= .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ nOpcx: 1 - Inclusao de Registros                     ³
//³ nOpcx: 2 - Alteracao de Registros                    ³
//³ nOpcx: 3 - Exclusao de Registros                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona Cliente                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SA1") 
dbSetorder(1)
MsSeek(xFilial("SA1")+M->CJ_CLIENTE+M->CJ_LOJA)

Begin Transaction

If ( nOpcx == 1 ) // Inclusao

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se existe algum item a ser gravado                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	dbSelectArea("TMP1")
	dbGotop()
	While ( !Eof() )
		If ( !TMP1->CK_FLAG .And. !Empty(TMP1->CK_PRODUTO)  )
			lGravou := .T.
			aAdd(aRecTMP1,TMP1->(Recno()))
		Endif
	
		dbSelectArea("TMP1")
		dbSkip()
	EndDo

	dbSelectArea("TMP2")
	dbGotop()
	While ( !Eof() )
		If ( !TMP2->CL_FLAG )
			lGravou := .T.
			Exit
		Endif
		dbSelectArea("TMP2")
		dbSkip()
	EndDo

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o numero ja foi gravado                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cNumOrc := M->CJ_NUM
	cMay := "SCJ"+ Alltrim(cFilSCJ)
	SCJ->(dbSetOrder(1))
	While ( SCJ->( DbSeek(cFilSCJ + cNumOrc) ) .or. !MayIUseCode(cMay+cNumOrc) )
		cNumOrc := Soma1(cNumOrc,Len(M->CJ_NUM))
	EndDo

	M->CJ_NUM := cNumOrc
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Efetua a Gravacao do Cabecario                               ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

	If ( lGravou )
		dbSelectArea("SCJ")
		RecLock("SCJ",.T.)
		For nX := 1 To FCount()
			If ("FILIAL" $ FieldName(nX) )
				FieldPut(nX,cFilSCJ)
			ElseIf (("TABELA" $ FieldName(nX)) .And. (M->&(FieldName(nX)) == PadR("1",Len(DA0->DA0_CODTAB))))
				FieldPut(nX,"")
			Else
				FieldPut(nX,M->&(FieldName(nX)))
			EndIf
		Next nX
		
		If M->CJ_STATUS == "F"
			lBloqOrc := .T.
		EndIf
		If lBloqOrc
			MaAvalOrc("SCJ",11,"F")
		Else
			MaAvalOrc("SCJ",11)
		EndIf
	EndIf

	SCJ->(FkCommit())
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Efetua a Gravacao do Itens                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cItem := "00"

	For nCnt := 1 to Len(aRecTMP1)

		cCodProd	:= ""
		cItemSeq	:= ""
		cTpProd	:= ""

		TMP1->(MsGoto(aRecTMP1[nCnt]))

		dbSelectArea("SCK")
		RecLock("SCK",.T.)
		For nX := 1 To FCount()
			nPosField := TMP1->(FieldPos(SCK->(FieldName(nX))))
			Do Case
				Case SCK->(FieldName(nX)) == "CK_PRODUTO"
					cCodProd := TMP1->(FieldGet(nPosField))
				Case SCK->(FieldName(nX)) == "CK_ITEM"
					cItemSeq := TMP1->(FieldGet(nPosField))
				Case SCK->(FieldName(nX)) == "CK_TPPROD"
					cTpProd := TMP1->(FieldGet(nPosField))
			EndCase
			If ( nPosField <> 0 )
				FieldPut(nX,TMP1->(FieldGet(nPosField)))
			EndIf
		Next nX
		SCK->CK_FILIAL := cFilSCK
		SCK->CK_NUM    := M->CJ_NUM
		SCK->CK_CLIENTE:= M->CJ_CLIENTE
		SCK->CK_LOJA   := M->CJ_LOJA

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Aqui ‚ feita a regrava‡Æo dos itens do SCK E SCL             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("TMP2")
		dbSetOrder(1)
		MsSeek(M->CJ_NUM+TMP1->CK_ITEM,.F.)
		cItem := Soma1(cItem)
		If ( Found() )
			TMP2->CL_ITEMORC := cItem
		EndIf
		If Empty(SCK->CK_ITEMGRD)
			SCK->CK_ITEM   := cItem
		EndIf
		lGravou        := .T.

		If lBloqOrc
			MaAvalOrc("SCK",1,"F")
		Else
			MaAvalOrc("SCK",1)
		EndIf
		lOrcOrc := IIf(lOrcOrc,SCK->CK_VALOR > 0,lOrcOrc) //Item Nao Orcado

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ M_SER004_CRM019_Integraçao_Faturamento_DPR                           ³
		//³ Verifica se o item eh do tipo "Desenvolvimento" e gera Pendencia     ³
		//³ Autor: Thiago Tavares													   ³
		//³ Data: 25/10/2013															   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( lIFatDpr .And. (lVExAutDPR .And. !(lExAutoDPR)) .And. (cTpProd == "2") )
			aItemDPR := {3, cFilSCK, M->CJ_NUM, cItemSeq, cCodProd, ""}
			If !(A415GrvDPR(aItemDPR))
				DisarmTransaction()
				lDisarmTrans	:= .T.
				lGravou		:= .F.
				EXIT
			EndIf
		EndIf
		aItemDPR 	:= {}
	Next nCnt

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Efetua a Gravacao dos Sub-itens TMP2                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !( lDisarmTrans ) .AND. lGravou
		cItem := "00"
		dbSelectArea("TMP2")
		dbGotop()
		While ( !Eof() )
			If ( !TMP2->CL_FLAG .And. lGravou )
				dbSelectArea("SCK")
				dbSetOrder(1)
				MsSeek(cFilSCK + TMP2->CL_NUM + TMP2->CL_ITEMORC,.F.)
				If ( Found() )
					dbSelectArea("SCL")
					RecLock("SCL",.T.)
					For nX := 1 To FCount()
						nPosField := TMP2->(FieldPos(SCL->(FieldName(nX))))
						If ( nPosField <> 0 )
							FieldPut(nX,TMP2->(FieldGet(nPosField)))
						EndIf
					Next nX
					cItem := Soma1(cItem)
					SCL->CL_FILIAL := cFilSCL
					SCL->CL_NUM    := M->CJ_NUM
					SCL->CL_ITEM   := cItem
					SCL->CL_NUMOP  := ""
					lGravou := .T.
				EndIf
			EndIf
			cItemAnt := TMP2->CL_ITEMORC
			dbSelectArea("TMP2")
			dbSkip()
			If ( cItemAnt <> TMP2->CL_ITEMORC )
				cItem := "00"
			EndIf
		EndDo

		//
		// Template GEM - Gestao de Empreendimentos Imobiliarios
		//
		// Grava a condicao de venda customizada.
		If ExistTemplate("GEMXGRA415",,.T.)
			// atualiza o status do empreendimento
			ExecTemplate("GEMXGRA415",.F.,.F.,{ nOpcx ,SCJ->CJ_NUM ,SCJ->CJ_CONDPAG })
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄL¿
		//³Altera o status do Orcamento quando houver algum item não orçado³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄLÙ
		If !lOrcOrc
			dbSelectArea("SCJ")
			RecLock("SCJ",.F.)
			SCJ->CJ_STATUS := "D"
			MsUnlock()
		EndIf
	EndIf

ElseIf ( nOpcx == 2 ) // Alteracao

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Guarda os registro para deleta-los caso necessário            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SCK")
	dbSetOrder(1)
	DbSeek(cFilSCK + M->CJ_NUM)
	While ( !Eof() .AND. SCK->CK_FILIAL == cFilSCK .AND. M->CJ_NUM == SCK->CK_NUM )
		// gravando o historico dos itens em caso de alteracao do tipo do produto e/ou codigo do produto
		If ( lIFatDpr )
			aAdd( aHistProd, {SCK->CK_PRODUTO, SCK->CK_TPPROD})
		EndIf
		
		aAdd(aRecTMP1,RecNo())
		dbSkip()
	EndDo

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Efetua a Gravacao do Itens                                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lDisarmTrans	:= .F.
	nI := 1
	dbSelectArea("TMP1")
	dbGotop()
	While ( !Eof() )
		dbSelectArea("SCK")
		dbSetOrder(1)
		MsSeek(cFilSCK + M->CJ_NUM + TMP1->CK_ITEM + TMP1->CK_PRODUTO,.F.)
		If ( Found() )
			RecLock("SCK",.F.)
			If ( TMP1->CK_FLAG )

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ M_SER004_CRM019_Integraçao_Faturamento_DPR                           ³
				//³ Achou o item que deve ser excluido: Verifica se o eh do tipo    	   ³
				//³ "Desenvolvimento" e grava num Array para excluir uma pendencia.      ³
				//³ Autor: Thiago Tavares													   ³
				//³ Data: 25/10/2013															   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				lContinua := .T.
				If ( lIFatDpr .And. (TMP1->CK_TPPROD == "2") )
					If !(l415Auto)
						lContinua := MsgYesNo(STR0086)      // "Orcamento possui Pendencia de Desenvolvimento. Confirma Excluir?"
					EndIf

					If lContinua
						aItemDPR := {5, cFilSCK, M->CJ_NUM, TMP1->CK_ITEM, TMP1->CK_PRODUTO, ""}
						If !(A415GrvDPR(aItemDPR))
							DisarmTransaction()
							lDisarmTrans	:= .T.
							lContinua		:= .F.
							lGravou		:= .F.
							EXIT
						EndIf
					EndIf
				EndIf

				If lContinua
					MaAvalOrc("SCK",2)
					dbDelete()
				Endif
			Else
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ M_SER004_CRM019_Integraçao_Faturamento_DPR                           ³
				//³ Achou o item: Verifica se eh do tipo "Desenvolvimento" 				   ³
				//³	 Se mudou TPPROD Venda -> Desenvolvimento, insere desenvolvimento	   ³
				//³	 Se mudou TPPROD Desenvolvimento -> Venda, exclui desenvolvimento	   ³
				//³ Autor: Thiago Tavares													   ³
				//³ Data: 25/10/2013															   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If ( lIFatDpr )
					lContinua := .F.
					If ( (aHistProd[nI][2] == "1") .And. (TMP1->CK_TPPROD == "2") )
						aItemDPR := {3, cFilSCK, M->CJ_NUM, TMP1->CK_ITEM, TMP1->CK_PRODUTO, ""}
						lContinua := .T.
					ElseIf ( (aHistProd[nI][2] == "2") .And. (TMP1->CK_TPPROD == "1") )
						aItemDPR := {5, cFilSCK, M->CJ_NUM, TMP1->CK_ITEM, TMP1->CK_PRODUTO, ""}
						lContinua := .T.
					EndIf

					If lContinua
						If !(A415GrvDPR(aItemDPR))
							DisarmTransaction()
							lContinua		:= .F.
							lGravou		:= .F.
							lDisarmTrans	:= .T.
							EXIT
						EndIf
					EndIf
				EndIf
			EndIf

			nX := aScan(aRecTMP1,{|x| x == Recno()})
			If nX <> 0
				aDel(aRecTMP1,nX)
				aSize(aRecTMP1,Len(aRecTMP1)-1)
			EndIf
		Else
			If ( !TMP1->CK_FLAG .And. !Empty(TMP1->CK_PRODUTO) )//.Or.!Empty(TMP1->CK_CLIPROD)) )

				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ M_SER004_CRM019_Integraçao_Faturamento_DPR                           ³
				//³ Nao achou item: Verifica se o eh do tipo "Desenvolvimento" e  		   ³
				//³ grava num Array para alterar uma pendencia de desenvolvimento.	 	   ³
				//³ Autor: Thiago Tavares													   ³
				//³ Data: 25/10/2013													 		   ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				lGerouDPR := .T.
				If ( ( lVExAutDPR .And. lExAutoDPR )  .And. lIFatDpr .And. (TMP1->CK_TPPROD == "2") )
					If (nI <= LEN(aHistProd))
						aItemDPR := {4, cFilSCK, M->CJ_NUM, TMP1->CK_ITEM, TMP1->CK_PRODUTO, aHistProd[nI][1]}
					Else
						aItemDPR := {3, cFilSCK, M->CJ_NUM, TMP1->CK_ITEM, TMP1->CK_PRODUTO}
					EndIf
					lGerouDPR := A415GrvDPR(aItemDPR)

					// Se falhou ao gerar pendencia, recupera registro anterior
					If !(lGerouDPR)
						TMP1->CK_PRODUTO	:= aHistProd[nI][1]
						TMP1->CK_TPPROD	:= aHistProd[nI][2]
						DbSelectArea("SCK")
						SCK->( DbSetOrder(1) )
						SCK->( MsSeek(cFilSCK + M->CJ_NUM + TMP1->CK_ITEM + TMP1->CK_PRODUTO, .T.) )
						RecLock("SCK",.F.)
						nX := aScan(aRecTMP1,{|x| x == Recno()})
						aDel(aRecTMP1,nX)
						aSize(aRecTMP1,Len(aRecTMP1)-1)
					// se gerou e é ExecAuto chamado do DPR, alterar o tipo do produto para Venda
					ElseIf ((lVExAutDPR .And. lExAutoDPR) .And. lGerouDPR)
						TMP1->CK_TPPROD := "1"
					EndIf
				EndIf
				If lGerouDPR
					RecLock("SCK",.T.)
				EndIf
			EndIf
		EndIf
		If ( !TMP1->CK_FLAG .And. !Empty(TMP1->CK_PRODUTO) )//.Or.!Empty(TMP1->CK_CLIPROD)) )

			//Guarda a quantidade anterior para atualizar a quantidade empenhada
			nQtdEmp := SCK->CK_QTDVEN

			For nX := 1 To FCount()
				nPosField := TMP1->(FieldPos(SCK->(FieldName(nX))))
				If ( nPosField <> 0 )
					FieldPut(nX,TMP1->(FieldGet(nPosField)))
				EndIf
			Next nX
			SCK->CK_FILIAL := cFilSCK
			SCK->CK_NUM    := M->CJ_NUM
			SCK->CK_CLIENTE:= M->CJ_CLIENTE
			SCK->CK_LOJA   := M->CJ_LOJA
			lGravou := .T.
			If lBloqOrc
				MaAvalOrc("SCK",1,"F")
			Else
				MaAvalOrc("SCK",1,)
			EndIf
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ,¿
		//³Validação do Status do orçamento³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ,Ù
		If !(SCK->CK_VALOR > 0)
			cStatCab := "D"
		ElseIf SCK->CK_ITEM == StrZero(1, nTmCKItem) .And. (SCJ->CJ_STATUS $ "A/D")
			cStatCab := "A"
		ElseIf !(SCJ->CJ_STATUS $ "A/D")
			cStatCab := SCJ->CJ_STATUS
		EndIf
		dbSelectArea("TMP1")
		dbSkip()
		aItemDPR := {}
		nI++
	EndDo
	
	If !( lDisarmTrans )
		dbSelectArea("SCK")
		//Deleta registros alterados por outro produto
		For nX := 1 To Len(aRecTMP1)
			dbGoto(aRecTMP1[nX])
			RecLock("SCK",.F.)
			dbDelete()
			MsUnLock()
		Next nX
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Efetua a Gravacao dos Sub-itens TMP2                         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cItem := "00"
		dbSelectArea("TMP2")
		dbGotop()
		While ( !Eof() )
			dbSelectArea("SCK")
			dbSetOrder(1)
			MsSeek(cFilSCK + TMP2->CL_NUM + TMP2->CL_ITEMORC,.F.)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ VerIfico se o SCK nao foi deletado                           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( Found() )
				dbSelectArea("SCL")
				dbSetOrder(1)
				MsSeek(cFilSCL + TMP2->CL_NUM + TMP2->CL_ITEMORC + TMP2->CL_ITEM,.F.)
				If ( Found() )
					RecLock("SCL",.F.)
					If ( TMP2->CL_FLAG )
						dbDelete()
					EndIf
				Else
					If ( !TMP2->CL_FLAG )
						RecLock("SCL",.T.)
					EndIf
				EndIf
				If ( !TMP2->CL_FLAG )
					For nX := 1 To FCount()
						nPosField := TMP2->(FieldPos(SCL->(FieldName(nX))))
						If ( nPosField <> 0 )
							FieldPut(nX,TMP2->(FieldGet(nPosField)))
						EndIf
					Next nX
					cItem := Soma1(cItem)
					SCL->CL_FILIAL := cFilSCL
					SCL->CL_NUM    := M->CJ_NUM
					SCL->CL_ITEM   := cItem  // Refaz itens do SCL
					lGravou := .T.
				EndIf
			Else
				dbSelectArea("SCL")
				dbSetOrder(1)
				MsSeek(cFilSCL + TMP2->CL_NUM + TMP2->CL_ITEMORC,.F.)
				While ( !Eof() .And. SCL->CL_FILIAL  == cFilSCL .And.;
						SCL->CL_NUM     == TMP2->CL_NUM   .And.;
						SCL->CL_ITEMORC == TMP2->CL_ITEMORC )
					RecLock("SCL",.F.)
					dbDelete()
					dbSelectArea("SCL")
					dbSkip()
				EndDo
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Aqui e' controlada a quebra de itens pelo SCK                ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			cItemAnt := TMP2->CL_ITEMORC
			dbSelectArea("TMP2")
			dbSkip()
			If ( cItemAnt <> TMP2->CL_ITEMORC )
				cItem := "00"
			EndIf
		EndDo

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Efetua a Gravacao do Cabecalho                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( lGravou )
			dbSelectArea("SCJ")
			RecLock("SCJ",.F.)
			For nX := 1 To FCount()
				If ("FILIAL" $ FieldName(nX) )
					FieldPut(nX,cFilSCJ)
				ElseIf (("TABELA" $ FieldName(nX)) .And. (M->&(FieldName(nX)) == PadR("1",Len(DA0->DA0_CODTAB))))
					FieldPut(nX,"")
				Else
					If Alltrim(FieldName(nX)) <> "CJ_STATUS"
						FieldPut(nX,M->&(FieldName(nX)))
					EndIf
				EndIf
			Next nX

			If lBloqOrc
				MaAvalOrc("SCJ",11,"F")
			Elseif ! lBloqOrc .And. cStatCab == "F"
				MaAvalOrc("SCJ",11,)
			Else
				MaAvalOrc("SCJ",11,cStatCab)
			EndIf

			//
			// Template GEM - Gestao de Empreendimentos Imobiliarios
			//
			// Grava a condicao de venda customizada.
			If ExistTemplate("GEMXGRA415",,.T.)
				// atualiza o status do empreendimento
				ExecTemplate("GEMXGRA415",.F.,.F.,{ nOpcx ,SCJ->CJ_NUM ,SCJ->CJ_CONDPAG })
			EndIf
		Else
			dbSelectArea("SCJ")
			RecLock("SCJ",.F.)
			MaAvalOrc("SCJ",12)
			dbDelete()
		EndIf
	EndIf

ElseIf ( nOpcx == 3 ) // Exclui

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ M_SER004_CRM019_Integraçao_Faturamento_DPR                           ³
	//³ Verifica se existe item do tipo "Desenvolvimento" e solicita  		   ³
	//³	confirmacao de exclusao.												 	   ³
	//³ Autor: Thiago Tavares													   ³
	//³ Data: 25/10/2013															   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lContinua := .T.
	lGerouDPR := .F.
	If lIFatDpr .And. !(l415Auto)
		DbSelectArea("SCK")
		SCK->( DbSetOrder(1) )
		SCK->( MsSeek(cFilSCK + M->CJ_NUM,.T.) )
		While ( !Eof() .And. SCK->CK_FILIAL == cFilSCK .And. SCK->CK_NUM == M->CJ_NUM )
			If (SCK->CK_TPPROD == "2")
				lGerouDPR := .T.
				Exit
			Endif
			SCK->( dbSkip() )
		EndDo

		If lGerouDPR
			lContinua := MsgYesNo(STR0086)  // "Orcamento possui Pendencia de Desenvolvimento. Confirma Excluir?"
		EndIf
	EndIf

	If lContinua
		//
		// Template GEM - Gestao de Empreendimentos Imobiliarios
		//
		// Grava a condicao de venda customizada.
		If ExistTemplate("GEMXGRA415",,.T.)
			// atualiza o status do empreendimento
			ExecTemplate("GEMXGRA415",.F.,.F.,{ nOpcx ,SCJ->CJ_NUM ,SCJ->CJ_CONDPAG })
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Deleta os Sub-Itens existentes.                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SCL")
		dbSetOrder(1)
		MsSeek(cFilSCL + M->CJ_NUM,.T.)
		While ( !Eof() .And. SCL->CL_FILIAL == cFilSCL .And. SCL->CL_NUM == M->CJ_NUM )
			RecLock("SCL")
			dbDelete()
			dbSelectArea("SCL")
			dbSkip()
		EndDo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Deleta os Itens existentes.                                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		lDisarmTrans	:= .F.
		dbSelectArea("SCK")
		dbSetOrder(1)
		MsSeek(cFilSCK + M->CJ_NUM,.T.)
		While ( !Eof() .And. SCK->CK_FILIAL == cFilSCK .And. SCK->CK_NUM == M->CJ_NUM )

			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ M_SER004_CRM019_Integraçao_Faturamento_DPR                           ³
			//³ Verifica se o item eh do tipo "Desenvolvimento" e grava num Array	   ³
			//³	para excluir pendencia de desenvolvimento apos confirmacao.		 	   ³
			//³ Autor: Thiago Tavares													   ³
			//³ Data: 25/10/2013															   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If ( lIFatDpr .And. (SCK->CK_TPPROD == "2") )
				aItemDPR := {5, cFilSCK, M->CJ_NUM, SCK->CK_ITEM, SCK->CK_PRODUTO, ""}
				If !(A415GrvDPR(aItemDPR))
					DisarmTransaction()
					lDisarmTrans	:= .T.
					lGravou		:= .F.
					EXIT
				EndIf
			EndIf

			RecLock("SCK")
			MaAvalOrc("SCK",2)
			dbDelete()
			dbSelectArea("SCK")
			dbSkip()
		EndDo
		If	! lDisarmTrans
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Deleta o Cabecario                                           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SCJ")
			RecLock("SCJ")
			dbDelete()
			MaAvalOrc("SCJ",12)
		EndIf
	EndIf
EndIf

End Transaction

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de Entrada Generico (1-Inc, 2-Alt, 3-Deleta            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !( lDisarmTrans ) .AND. l415Grava
	Execblock("M415GRV",.F.,.F.,{ nOpcx } )
Endif
Return(lGravou)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415LinOk   ³ Autor ³Eduardo Riera        ³ Data ³ 08.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da Linha na GetDadosDB                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Logical A415LinOk(Void)                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415LinOk(oGetDb)

Local aArea        := GetArea()
Local aAreaTMP1    := {}
Local aContrato    := {}
Local lRetorno     := .T.
Local nValor       := 0
Local nX           := 0
Local nPrcVen      := 0
Local nPOpcional   := aScan(aHeader,{|x| AllTrim(x[2]) == "CK_OPC"})
Local cOpcional    := ""
Local cOpcioAnt    := ""
Local lCalcOpc     := .T.
Local lPrcMan	   := .F.

Local cFilSGA		:= xFilial("SGA")

l415Auto := If(Type("l415Auto")<>"U",l415Auto,.F.)
l416Auto := If(Type("l416Auto")<>"U",l416Auto,.F.)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ VerIfica se a linha foi deletada                     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If  !TMP1->CK_FLAG
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica a permissao do armazem. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	 
	lRetorno := MaAvalPerm(3,{TMP1->CK_LOCAL,TMP1->CK_PRODUTO})
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ VerIfica se os campos obrigatorios foram preenchidos ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( lRetorno .And.  Empty(TMP1->CK_PRODUTO)  .Or.;
			Empty(TMP1->CK_TES   )  .Or.;
			Empty(TMP1->CK_LOCAL) )

		HELP(" ",1,"OBRIGAT")
		lRetorno := .F.
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ VerIfica se o Valor Total da Linha ‚ valido                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( lRetorno )
		nValor := (TMP1->CK_QTDVEN * TMP1->CK_PRCVEN)
		nValor := A410Arred(nValor,"CK_VALOR")
		If ( nValor != TMP1->CK_VALOR )
			Help(" ",1,"A415TOTAL")
			lRetorno := .F.
		EndIf
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se o Valor do desconto ‚ Valido.                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
	If ( lRetorno .And. IIf(TMP1->CK_VALOR>0,TMP1->CK_VALOR <= 0,.F.) )
		Help(" ",1,"A415DESCON")
		lRetorno := .F.
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica a validade do contrato de parceria                  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRetorno
		lRetorno := A415CtrParc(M->CJ_NUM)
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Valida as colunas do browse referente ao SIGAPMS                       ³
	//³ Colunas: C6_PROJPMS, C6_EDTPMS, C6_TASKPMS                             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If lRetorno
		lRetorno := a415VldPMS(TMP1->CK_PROJPMS,TMP1->CK_EDTPMS,TMP1->CK_TASKPMS )
	EndIf
	
EndIf

If lRetorno
	If l415Auto .or. l416Auto
		A415Total()
	Else
		A415Total(oGetDb:oWnd)
	EndIf
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Atualiza os Opcionais                                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRetorno .And. If(Type("lShowOpc")=="L",lShowOpc,.F.) .And. !(l415Auto .Or. l416Auto) .And. nPOpcional > 0 .And. !MatGrdPrrf(TMP1->CK_PRODUTO,.T.) .And. !TMP1->CK_FLAG
	cOpcioAnt := TMP1->CK_OPC
	lRetorno := SeleOpc(3,"MATA415",TMP1->CK_PRODUTO,,,TMP1->CK_OPC,"M->CK_PRODUTO",,TMP1->CK_QTDVEN,TMP1->CK_ENTREG)
	If lRetorno
		cOpcional := TMP1->CK_OPC
		lShowOpc := .F.
	   	If !Empty(cOpcional)
	   		nPrcVen  := A415Tabela(TMP1->CK_PRODUTO,M->CJ_TABELA,TMP1->CK_QTDVEN)// Adiciona o valor do opcional no preço venda
			RecLock("TMP1",.F.)
			If TMP1->CK_PRCVEN > 0 .And. TMP1->CK_PRUNIT == 0
				//Se for informado somente o preço unitário (CK_PRCVEN).
				TMP1->CK_PRCVEN += nPrcVen
				TMP1->CK_VALOR  := A410Arred(TMP1->CK_QTDVEN*TMP1->CK_PRCVEN,"CK_VALOR")
				lPrcMan := .T.
				lCalcOpc := .F.
			ElseIf TMP1->CK_PRUNIT > 0 .And. TMP1->CK_PRCVEN > 0 .And. Empty(M->CJ_TABELA)
				If MaTabPrVen(M->CJ_TABELA,TMP1->CK_PRODUTO,TMP1->CK_QTDVEN,M->CJ_CLIENTE,M->CJ_LOJA) > 0
					lPrcMan := .F.
					lCalcOpc := .T.
				Else
					lPrcMan := .T.
					lCalcOpc := .T.
				EndIf
			ElseIf TMP1->CK_PRUNIT > 0 .And. TMP1->CK_PRCVEN > 0 .And. !Empty(M->CJ_TABELA)
				If TMP1->CK_PRUNIT <> MaTabPrVen(M->CJ_TABELA,TMP1->CK_PRODUTO,TMP1->CK_QTDVEN,M->CJ_CLIENTE,M->CJ_LOJA)
					lPrcMan := .T.
					lCalcOpc := .T.
				EndIf
			ElseIf TMP1->CK_PRUNIT > 0
				TMP1->CK_PRUNIT := nPrcVen
			EndIf
			If !Empty(cOpcioAnt) .And. !lPrcMan
				If cOpcional == cOpcioAnt .And. TMP1->CK_PRCVEN <> TMP1->CK_PRUNIT
					lCalcOpc := .F.
				ElseIf cOpcional == cOpcioAnt .And. TMP1->CK_PRCVEN == TMP1->CK_PRUNIT
					lCalcOpc := .F.
				ElseIf cOpcional <> cOpcioAnt  .And. TMP1->CK_PRCVEN <> TMP1->CK_PRUNIT
					TMP1->CK_PRCVEN := A410Arred(FtDescCab(TMP1->CK_PRUNIT,{M->CJ_DESC1,M->CJ_DESC2,M->CJ_DESC3,M->CJ_DESC4})*(1-(TMP1->CK_DESCONT/100)),"CK_PRCVEN")
					TMP1->CK_VALOR  := A410Arred(TMP1->CK_QTDVEN*TMP1->CK_PRCVEN,"CK_VALOR")
					lCalcOpc := .T.
				Else
					lCalcOpc := .T.
				EndIf
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Aqui ‚ efetuado o tratamento diferencial de Precos para os   ³
			//³ Opcionais do Produto.                                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SGA")
			dbSetOrder(1)
			cOpcional := TMP1->CK_OPC
			While !Empty(cOpcional) .And. lCalcOpc
				cOpc      := SubStr(cOpcional,1,At("/",cOpcional)-1)
				cOpcional := SubStr(cOpcional,At("/",cOpcional)+1)
				If ( MsSeek(cFilSGA + cOpc) )
					TMP1->CK_PRCVEN += SGA->GA_PRCVEN
					TMP1->CK_VALOR  := A410Arred(TMP1->CK_QTDVEN*TMP1->CK_PRCVEN,"CK_VALOR")
				EndIf
			EndDo
			TMP1->(MsUnlock())
			A415Total(oGetDb:oWnd) // Atualiza o rodapé(totais) do orçamento
		EndIf
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Validacao do ponto de entrada                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRetorno .And. ExistBlock("A415LIOK")
	lRetorno := ExecBlock("A415LIOK",.F.,.F.)
EndIf

RestArea(aArea)
Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415TudOk   ³ Autor ³Eduardo Riera        ³ Data ³ 08.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da GetDadosDB                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Logical A415TudOk(Void)                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415TudOk(oGetd)

Local aArea    := GetArea()
Local aContrato:= {}
Local lRetorno := .T.
Local nValor   := 0
Local nTotOrc  := 0
Local nX       := 0
Local oDlg
Local lIFatDpr	:= SuperGetMV("MV_IFATDPR",.F.,.F.)
Local nProdVnd	:= 0
Local nProdDsv	:= 0

Local cFilSCK		:= xFilial("SCK")
Local cFilADB		:= xFilial("ADB")

l415Auto := If(Type("l415Auto")<>"U",l415Auto,.F.)
l416Auto := If(Type("l416Auto")<>"U",l416Auto,.F.)

If !(l415Auto .or. l416Auto)
	oDlg := oGetd:oWnd
EndIf

dbSelectArea("TMP1")
dbGotop()
While ( !Eof() .And. lRetorno )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se a linha foi deletada                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( !TMP1->CK_FLAG .And. !Empty(TMP1->CK_PRODUTO) )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se os camos obrigatorios foram preenchidos  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( lRetorno .And. Empty(TMP1->CK_TES   ) .Or. Empty(TMP1->CK_LOCAL ) )
			HELP(" ",1,"OBRIGAT")
			lRetorno := .F.
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o Valor Total da Linha ‚ valido                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( lRetorno )
			nValor := A410Arred((TMP1->CK_QTDVEN * TMP1->CK_PRCVEN),"CK_VALOR")
			If ( nValor <> TMP1->CK_VALOR )
				Help(" ",1,"A415TOTAL")
				lRetorno := .F.
			EndIf
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ VerIfica se o Valor do desconto ‚ Valido.                    ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( lRetorno .And. ( IIf(TMP1->CK_VALOR>0,TMP1->CK_VALOR <= 0,.F.) .Or. M->CJ_DESC4 >= 100) )
			Help(" ",1,"A415DESCON")
			lRetorno := .F.
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica o contrato de parceria                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(TMP1->CK_CONTRAT)
			nX := aScan(aContrato,{|x| x[1] == TMP1->CK_CONTRAT .And. x[2] == TMP1->CK_ITEMCON})
			If nX == 0
				aAdd(aContrato,{TMP1->CK_CONTRAT,TMP1->CK_ITEMCON,TMP1->CK_QTDVEN})
				nX := Len(aContrato)
			Else
				aContrato[nX][3] += TMP1->CK_QTDVEN
			EndIf
			dbSelectArea("SCK")
			dbSetOrder(1)
			If MsSeek(cFilSCK + M->CJ_NUM + TMP1->CK_ITEM) .And. !Empty(SCK->CK_CONTRAT)
				nX := aScan(aContrato,{|x| x[1] == SCK->CK_CONTRAT .And. x[2] == SCK->CK_ITEMCON})
				If nX == 0
					aAdd(aContrato,{SCK->CK_CONTRAT,SCK->CK_ITEMCON,0})
					nX := Len(aContrato)
				EndIf
				aContrato[nX][3] -= SCK->CK_QTDVEN
			EndIf
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Para integracao SIGAFAT com SIGADPR somente um item do tipo desenvolvimento por pedido de venda. ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lIFatDpr
			If TMP1->CK_TPPROD == "1"
				nProdVnd++
			ElseIf TMP1->CK_TPPROD == "2"
				nProdDsv++ 
			EndIf
			If nProdDsv > 1 .OR. ( nProdVnd > 0 .AND. nProdDsv > 0 )
				MsgAlert(STR0089) //"É permitido somente um item do tipo Desenvolvimento por Orçamento!"
				lRetorno := .F.
				Exit
			EndIf
		EndIf
		 
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica as faixas da condicao de pagamento                  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nTotOrc += TMP1->CK_VALOR
		
	EndIf
	dbSelectArea("TMP1")
	dbSkip()
EndDo
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica o contrato de parceria                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRetorno
	For nX := 1 To Len(aContrato)
		dbSelectArea("ADB")
		dbSetOrder(1)
		MsSeek(cFilADB + aContrato[nX][1] + aContrato[nX][2])
		If aContrato[nX][3] > ADB->ADB_QUANT-ADB->ADB_QTDEMP
			Help(" ",1,"A410CTRQT2")
			lRetorno := .F.
		EndIf
	Next nX
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica a Condicao de Pagamento Tipo 9                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !A415Tipo9()
	lRetorno  := .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se a tabela de precos eh valida                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRetorno
	lRetorno := MaVldTabPrc(M->CJ_TABELA,M->CJ_CONDPAG,Nil,M->CJ_EMISSAO)
EndIf
If lRetorno
	dbSelectArea("SA1")
	dbSetOrder(1)
	If MsSeek(xFilial("SA1")+M->CJ_CLIENTE+M->CJ_LOJA)
		If !RegistroOk("SA1")
			lRetorno	 := .F.
		Endif	
	Endif
Endif	
If lRetorno
	a415Total(oDlg)
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as faixas da condicao de pagamento                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lRetorno
	dbSelectArea("SE4")
	dbSetOrder(1)
	MsSeek(xFilial("SE4")+M->CJ_CONDPAG)
	If nTotOrc > SE4->E4_SUPER .AND. SE4->E4_SUPER <> 0
		Help(" ","1","LJLIMSUPER")
		lRetorno := .F.
	ElseIf nTotOrc < SE4->E4_INFER .AND. SE4->E4_INFER <> 0
		Help(" ","1","LJLIMINFER")
		lRetorno := .F.
	Endif
EndIf

If lRetorno .And. ExistBlock("A415TDOK")
	lRetorno := ExecBlock("A415TDOK",.F.,.F.)
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415F4      ³ Autor ³Eduardo Riera        ³ Data ³ 08.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Tratamento da tecla F4.                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void A415F4(Void)                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ a,b,c                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415F4(a,b,c)

Local lRetorno	:= .T.
Local oDlg
Local nOpcA		:= 0
Local nOpcx		:= 0
Local aArea		:= GetArea()

Local bSetKey	:= SetKey(VK_F4,Nil)
Local cCadastro:= OemToAnsi(STR0020) //"Sugest„o de Or‡amento"

If ! Empty(TMP1->CK_PRODUTO)
	If !Empty(TMP1->CK_QTDVEN) .And. "CK_QTDVEN" $ Trim(ReadVar())
		Private oGet
		Private aCols  := {}
		Private aHeader := {}
		SetKey(VK_F4,{|a,b,c| a415F4Stoq(a,b,c)})
		If ( INCLUI .Or. ALTERA )
			// array aRotina redimensionado para 3 posicoes na funcao a415PCpy.
			// Por este motivo nao podemos atribuir 4 para nOpcx e sim 3. Se atribuirmos 4 ocorre erro na MsGetDados.
			If len(aRotina) > 3
				nOpcx := 4
			Else
				nOpcx := 3
			EndIf
		Else
			nOpcx := 2
		EndIf
		aHeader := aHeaderSCL
		aCols   := a415GeraCL()
		DEFINE MSDIALOG oDlg TITLE cCadastro From 15,0 to 28,80
		@ 01,01 SAY STR0021+TMP1->CK_PRODUTO+" "+SubStr(TMP1->CK_DESCRI,1,30)   //"Produto : "
		@ 1.5,01 SAY STR0022+TransForm(TMP1->CK_QTDVEN,PesqPict("SCK","CK_QTDVEN")) //"Qtd.Base: "
		oGet := MsGetDados():New(32,1,094,316,nOpcx,"AllwaysTrue","AllwaysTrue","+CL_ITEM",.T.)
		ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{||nOpcA:=1,IIf(oGet:TudoOk(),oDlg:End(),nOpcA:=0)},{||oDlg:End()})
		If ( nOpcA == 1  .And. (INCLUI .Or. ALTERA) .And.;
		!Empty(aCols) )
			a415GrvSCL(aHeader,aCols)
		EndIf
	Else
		a415F4Stoq(a,b,c)
	EndIf
EndIf
//__MRestore( cArqMem, .T. )
//Ferase(Trim(cArqMem)+".mem")
aHeader := aHeaderSCK
SetKey(VK_F4,bSetKey)
RestArea(aArea)
Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415F4STOQ  ³ Autor ³Eduardo Riera        ³ Data ³ 13.01.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Tratamento da tecla F4.                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void A415F4STOQ(Void)                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ a,b,c                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415F4Stoq(a,b,c)

Local aArea	   := GetArea()
Local cProduto := ""
Local cFilEnt  := Nil
Local cCampo   := AllTrim(StrTran(ReadVar(),"M->",""))
Local nPosPrd  := 0

If ( SubStr(cCampo,1,2) $ "CL" .And. cCampo <> "CL_PRODUTO" )
	nPosPrd  := aScan(aHeader,{|x| AllTrim(x[2]) == "CL_PRODUTO" })
	If ( nPosPrd > 0 )
		cProduto := aCols[n,nPosPrd]
	EndIf
ElseIf ( SubStr(cCampo,1,2) $ "CK" .And. cCampo <> "CK_PRODUTO")
	nPosPrd  := aScan(aHeader,{|x| AllTrim(x[2]) == "CK_PRODUTO" })
	If ( nPosPrd > 0 )
		cProduto := TMP1->CK_PRODUTO
	EndIf
EndIf

cFilEnt := TMP1->CK_FILENT

If !Empty(cProduto)
	MaViewSB2(cProduto,cFilEnt)
EndIf

RestArea(aArea)
Return(Nil)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415Prod    ³ Autor ³Eduardo Riera        ³ Data ³ 08.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da Digitacao do Produto                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Logical A415Prod(Void)                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415Prod(cProduto)

Local lRetorno    := .T.
Local aArea       := GetArea()
Local cDescri     := ""
Local cAnterior   := ""
Local nITEMGRD    := aScan(aHeader,{|x| AllTrim(x[2]) == "CK_ITEMGRD"})
Local lGrade      := !Empty(nITEMGRD) .And. MaGrade()
Local lReferencia := .F.

Local nTmCKItem	:= GetSX3Cache("CK_ITEM","X3_TAMANHO")

cProduto := IIf(cProduto==Nil,M->CK_PRODUTO,cProduto)

If lGrade
	lReferencia := MatGrdPrrf(@cProduto)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Posiciona Registros                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SA7")
dbSetOrder(1)
If ( MsSeek(xFilial()+M->CJ_CLIENTE+M->CJ_LOJA+cProduto,.T.) )
	cDescri := SA7->A7_DESCCLI
EndIf

dbSelectArea("SB1")
dbSetOrder(1)
MsSeek(xFilial()+cProduto,.T.)

If !lReferencia .And. !RegistroOk("SB1")
	lRetorno  := .F.
Endif

If ( Found() )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Preenche os dados vidos do Produto                   ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("TMP1")
	If (FieldPos("CK_UM") > 0 )
		TMP1->CK_UM       := SB1->B1_UM
	EndIf
	If (FieldPos("CK_TES") > 0 ) .And. TMP1->CK_PRODUTO <> cProduto
		If (!Empty(RetFldProd(SB1->B1_COD,"B1_TS")))
			TMP1->CK_TES      := A415TesPad(RetFldProd(SB1->B1_COD,"B1_TS"))
		Else
			TMP1->CK_TES      := A415TesPad()
		EndIf
	EndIf
	If (FieldPos("CK_LOCAL") > 0 )
		TMP1->CK_LOCAL    := RetFldProd(SB1->B1_COD,"B1_LOCPAD")
	EndIf
	If (FieldPos("CK_COMIS1") > 0 )
		TMP1->CK_COMIS1   := SB1->B1_COMIS
	EndIf
	If (FieldPos("CK_DESCRI") > 0 )
		TMP1->CK_DESCRI   := PadR(IIf(Empty(cDescri),SB1->B1_DESC,cDescri),Len(TMP1->CK_DESCRI))
	EndIf
	If (FieldPos("CK_CONTRAT") > 0 )
		TMP1->CK_CONTRAT := ""
	EndIf
	If (FieldPos("CK_ITEMCON") > 0 )
		TMP1->CK_ITEMCON := ""
	EndIf

	If cPaisLoc == "BRA"
		TMP1->CK_FCICOD := Upper( XFciGetOrigem( SB1->B1_COD , M->CJ_EMISSAO )[2] )
	EndIf

ElseIf lReferencia
	If (TMP1->(FieldPos("CK_DESCRI")) > 0 )
		TMP1->CK_DESCRI := PadR(oGrade:GetDescProd(cProduto),Len(TMP1->CK_DESCRI))
	EndIf
	If (TMP1->(FieldPos("CK_UM")) > 0 )
		If MatOrigGrd() == "SB4"
			TMP1->CK_UM := SB4->B4_UM
		Else
			TMP1->CK_UM := Posicione("SBR",1,xFilial("SBR")+SBP->BP_BASE,"BR_UM")
		EndIf
	EndIf
	If (TMP1->(FieldPos("CK_LOCAL")) > 0 )
		If MatOrigGrd() == "SB4"
			TMP1->CK_LOCAL := SB4->B4_LOCPAD
		Else
			TMP1->CK_LOCAL := Posicione("SBR",1,xFilial("SBR")+SBP->BP_BASE,"BR_LOCPAD")
		EndIf
	EndIf
	If (TMP1->(FieldPos("CK_CONTRAT")) > 0 )
		TMP1->CK_CONTRAT := ""
	EndIf
	If (TMP1->(FieldPos("CK_ITEMCON")) > 0 )
		TMP1->CK_ITEMCON := ""
	EndIf
Else
	lRetorno := .F.
EndIf

If lRetorno .And. lGrade
	oGrade:MontaGrade(Val(RetAsc(TMP1->CK_ITEM, nTmCKItem, .F.)),cProduto,.T.,,lReferencia)
	If !lReferencia .And. !Empty(nITEMGRD)
		TMP1->CK_ITEMGRD := CriaVar("CK_ITEMGRD")
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Qdo e'dIferente apaga os sub-itens.                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( lRetorno .And. TMP1->CK_PRODUTO <> cProduto )
	cAnterior := TMP1->CK_PRODUTO
	TMP1->CK_PRODUTO := cProduto
	a415GrvSCL(aHeaderSCL,{})
	TMP1->CK_PRODUTO := cAnterior
EndIf

RestArea(aArea)
Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415SCLPrd  ³ Autor ³Eduardo Riera        ³ Data ³ 08.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da Digitacao do Produto                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void A415SCLPrd(Void)                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415SCLPrd()

Local lRetorno	:= .T.
Local nPosDesc	:= 0
Local aArea		:= GetArea()
Local cFilSBH		:= xFilial("SBH")

dbSelectArea("SBH")
dbSetOrder(1)
MsSeek(cFilSBH + M->CL_PRODUTO,.T.)
If ( !Eof() .And. SBH->BH_FILIAL == cFilSBH .And.;
		M->CL_PRODUTO == SBH->BH_PRODUTO )
	Help(" ",1,"A415SCLPRD")
	lRetorno := .f.
EndIf

If ( M->CL_PRODUTO == TMP1->CK_PRODUTO )
	lRetorno := .F.
	Help(" ",1,"A415SCLPRD")
Else
	nPosDesc := aScan(aHeader,{|x| Trim(x[2])=="CL_DESCRI" })
	If ( nPosDesc > 0 )
		dbSelectArea("SB1")
		dbSetOrder(1)
		MsSeek(xFilial()+M->CL_PRODUTO,.T.)
		aCols[n,nPosDesc] := SB1->B1_DESC
	EndIf
EndIf
RestArea(aArea)
Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415SCLQtd  ³ Autor ³Eduardo Riera        ³ Data ³ 08.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da Digitacao da Quantidade                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void A415SCLQtd(Void)                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415SCLQtd()

Local nPos := 0
nPos := aScan(aHeader,{|x| Trim(x[2]) == "CL_QTDVEN" })
If ( nPos > 0 )
	aCols[n,nPos]  := M->CL_QUANT * TMP1->CK_QTDVEN
EndIf
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415Descon  ³ Autor ³Eduardo Riera        ³ Data ³ 09.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da Digitacao do Percentual de Desconto           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpL1 := A415Descon( [ ExpL2 ] )                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415Descon()

Local aArea		:= GetArea()
Local lRetorno	:= .T.
Local nValor    := TMP1->CK_VALOR
Local nPDesc    := TMP1->CK_DESCONT
Local nVlDesc   := TMP1->CK_VALDESC
Local nPrcVen	:= 0
Local bSemPrcTBL:= .F.
Local nIndexPrd	:= 0
Local nIndexPrc := 0

If TMP1->CK_PRUNIT < TMP1->CK_PRCVEN .And. Empty(TMP1->CK_OPC)
	TMP1->CK_PRUNIT := TMP1->CK_PRCVEN
EndIf

If l415Auto .AND. TMP1->CK_PRCVEN == 0
    For nIndexPrd:= 1 to Len(aAutoItens)
		If aAutoItens[nIndexPrd][aScan(aAutoItens[nIndexPrd],{|x| Alltrim(Upper(x[1]))=="CK_PRODUTO"})][2] == TMP1->CK_PRODUTO
			nIndexPrc := aScan( aAutoItens[nIndexPrd], {|x| Alltrim( Upper( x[1] ) ) == "CK_PRCVEN" } )
	   	 	nPrcVen	  := aAutoItens[nIndexPrd][nIndexPrc][2]
		EndIf
	Next nIndexPrd
	bSemPrcTBL	:= .T.
EndIf

If "CK_DESCONT"$ReadVar()
	nPDesc := M->CK_DESCONT
EndIf

If ( TMP1->CK_QTDVEN>0 .And. IIF( bSemPrcTBL, nPrcVen>0, TMP1->CK_PRCVEN>0 ))

	If bSemPrcTBL
		nPrcVen  := FtDescItem(FtDescCab(nPrcVen,{M->CJ_DESC1,M->CJ_DESC2,M->CJ_DESC3,M->CJ_DESC4}),nPrcVen,TMP1->CK_QTDVEN,@nValor,@M->CK_DESCONT,@nVlDesc,nVlDesc,1,,If(cPaisLoc=="CHI",M->CJ_MOEDA,NIL))
	Else
		nPrcVen  := FtDescItem(FtDescCab(TMP1->CK_PRUNIT,{M->CJ_DESC1,M->CJ_DESC2,M->CJ_DESC3,M->CJ_DESC4}),TMP1->CK_PRCVEN,TMP1->CK_QTDVEN,@nValor,@M->CK_DESCONT,@nVlDesc,nVlDesc,1,,If(cPaisLoc=="CHI",M->CJ_MOEDA,NIL))
	EndIf

	If ( nPrcVen <= 0 )
		TMP1->CK_DESCONT  := 0
		TMP1->CK_VALDESC  := 0
		Help(" ",1,"A415DESCON")
		lRetorno := .F.
	Else
		TMP1->CK_PRCVEN  := nPrcVen
		TMP1->CK_VALOR   := nValor
		TMP1->CK_DESCONT := nPDesc
		TMP1->CK_VALDESC := nVlDesc
	EndIf
EndIf
RestArea( aArea )
Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415VlDesc  ³ Autor ³Eduardo Riera        ³ Data ³ 09.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da Digitacao do Valor de Desconto                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Logical A415VlDesc(Void)                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415VlDesc()

Local aArea       := GetArea()
Local lRetorno    := .T.
Local nValor      := TMP1->CK_VALOR
Local nPDesc      := TMP1->CK_DESCONT
Local nVlDesc     := TMP1->CK_VALDESC

If ((nVlDesc == 0) .AND. ( M->CK_VALDESC >= nValor )) .OR.;
	(nVlDesc > 0) .AND. ( M->CK_VALDESC >= (nValor + nVlDesc))
	Help(" ",1,"A415DESCON")
	lRetorno := .F.
Else
	TMP1->CK_PRCVEN  := FtDescItem(FtDescCab(TMP1->CK_PRUNIT,{M->CJ_DESC1,M->CJ_DESC2,M->CJ_DESC3,M->CJ_DESC4}),TMP1->CK_PRCVEN,TMP1->CK_QTDVEN,@nValor,@nPDesc,@M->CK_VALDESC,@nVlDesc,2,,If(cPaisLoc=="CHI",M->CJ_MOEDA,NIL))
	TMP1->CK_VALOR   := nValor
	TMP1->CK_DESCONT := nPDesc
	TMP1->CK_VALDESC := nVlDesc
EndIf

RestArea( aArea )
Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415QtdVen  ³ Autor ³Eduardo Riera        ³ Data ³ 09.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da Digitacao da Quantidade.                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Logical A415QtdVen(Void)                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415QtdVen()

Local aArea    	   := GetArea()
Local aAreaTMP1	   := {}
Local aContrato	   := {}
Local lRetorno 	   := .T.
Local nQtdVen  	   := M->CK_QTDVEN
Local nValor   	   := TMP1->CK_VALOR
Local nX       	   := 0
Local lGrade		:= MaGrade()
Local lReferencia	:= MatGrdPrrf(TMP1->CK_PRODUTO)
Local nTmCKItem	:= GetSX3Cache("CK_ITEM","X3_TAMANHO")

Private CK_DESCONT := 0

l415Auto := If(Type("l415Auto")<>"U",l415Auto,.F.)
l416Auto := If(Type("l416Auto")<>"U",l416Auto,.F.)

A415FldOk()

If !lReferencia
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica a validade dos contratos de parceria        ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lRetorno := A415CtrParc(M->CJ_NUM,nQtdVen)
EndIf

If !lReferencia .And. lRetorno
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Qdo for rotina automatica (sigaauto), fara as operacoes ³
	//³ necessarias e retornara com o acols de origem           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	PRIVATE aCols
	aCols := a415GeraCL()
	a415GrvSCL(aHeaderSCL,aCols)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Atualiza Valor Total                                 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	TMP1->CK_QTDVEN   := M->CK_QTDVEN // Forca a gravacao para usar as funcoes internas
	TMP1->CK_VALOR    := A410Arred(TMP1->CK_QTDVEN * TMP1->CK_PRCVEN,"CK_VALOR")
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Atualiza Desconto pelo percentual                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	M->CK_DESCONT := 0 // Para compatibilizar com A415Descon
	lRetorno := a415Descon()
	If ( !lRetorno )
		TMP1->CK_VALOR    := nValor
		TMP1->CK_QTDVEN   := nQtdVen
	EndIf
	If ( lRetorno )
		TMP1->CK_QTDVEN := M->CK_QTDVEN
	EndIf
EndIf

//Chama tela da grade
If lGrade .And. lReferencia
	oGrade:cProdRef	:= TMP1->CK_PRODUTO
	oGrade:nPosLinO	:= Val(RetAsc(TMP1->CK_ITEM, nTmCKItem, .F.))   
	oGrade:lShowMsgDiff := .T.
	oGrade:Show("CK_QTDVEN")
	&(ReadVar()) := oGrade:SomaGrade("CK_QTDVEN",oGrade:nPosLinO)
	TMP1->CK_QTDVEN := &(ReadVar())
	oGrade:lShowMsgDiff := .T.
EndIf

RestArea(aArea)
Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415PrcVen  ³ Autor ³Eduardo Riera        ³ Data ³ 09.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Validacao da Digitacao do Preco de Venda.                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void A415PrcVen(Void)                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415PrcVen()

Local lRetorno  := .T.
Local nAnterior:= 0
Private CK_DESCONT := 0
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza Valor Total                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
TMP1->CK_VALOR := a410Arred(TMP1->CK_QTDVEN * M->CK_PRCVEN,"CK_VALOR")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza Desconto pelo percentual                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nAnterior       := M->CK_PRCVEN // Para compatibilizar com A415Descon
TMP1->CK_PRCVEN := M->CK_PRCVEN // Para compatibilizar com A415Descon
If TMP1->CK_DESCONT > 0
	TMP1->CK_DESCONT := 0  // Para compatibilizar com A415Descon
	TMP1->CK_VALDESC := 0
EndIf

Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡Æo    ³A415CliPad  ³ Autor ³Eduardo Riera        ³ Data ³ 09.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Inicializacao do Cliente Padrao                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void A415CliPad(Void)                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415CliPad()

Local aArea        := GetArea()
Local cCliente     := GetMv("MV_ORCLIPD")
Local aAreaSX3     := {}   // Armazena a Area do SX3
Local cA1Tipo      := "R"  // Valor Padrao do Campo A1_TIPO
Local cCodCli      := ""
Local cLojCli      := ""
Local cAliasSA1    := GetNextAlias()

Local cFilSA1		:= xFilial("SA1")

cCodCli := Substr(cCliente,1,Len(SA1->A1_COD))
cLojCli := Substr(cCliente,1+Len(SA1->A1_COD),Len(SA1->A1_LOJA))

If !Empty( cCodCli ) .And. !Empty( cLojCli )

	
	dbSelectArea("SA1")
	dbSetOrder(1)
		
	cQuery := "SELECT COUNT(A1_COD) QTD "
	cQuery += "FROM "+RetSqlName("SA1")+" SA1 "
	cQuery += "WHERE A1_FILIAL='"+cFilSA1+"' AND "
	cQuery += "A1_COD='"+cCodCli+"' AND "
	cQuery += "A1_LOJA='"+cLojCli+"' AND "
	cQuery += "D_E_L_E_T_=' '	

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSA1)
	
	If (cAliasSA1)->QTD == 0
	
		If cPaisLoc <> "BRA"	
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Seleciona a Primeira opcao do ComboBox ref. ao Campo A1_TIPO ³
			//³ 25/10/2001 - Sergio Camurca                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
			dbSelectArea("SX3")
			aAreaSX3 := GetArea()
			dbSetOrder(2)
			If ( MsSeek("A1_TIPO") )
				cA1Tipo := Substr(X3CBOX(),1,1)
			EndIf
			RestArea(aAreaSX3)
			dbSelectArea("SA1")
	
		EndIf
	
	 	Reclock("SA1",.T.)
		SA1->A1_FILIAL    := cFilSA1
		SA1->A1_COD       := Substr(cCliente,1,Len(SA1->A1_COD))
		SA1->A1_TIPO      := cA1Tipo
		SA1->A1_LOJA      := Substr(cCliente,1+Len(SA1->A1_COD),Len(SA1->A1_LOJA))
		SA1->A1_NOME      := STR0032 //"CLIENTE PADRAO P/ ORCAMENTO"
		SA1->A1_NREDUZ    := STR0033 //"ORCAMENTO"
		SA1->A1_MUN       := "."
		SA1->A1_EST       := GetMv("MV_ESTADO")
		SA1->A1_BAIRRO    := "."
		SA1->A1_END       := "."
		MsUnlock()
	EndIf
	dbCloseArea()
	RestArea(aArea)

EndIf

Return(cCliente)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡Æo    ³A415CondPad ³ Autor ³Eduardo Riera        ³ Data ³ 09.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Inicializacao do Cliente Padrao                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void A415CondPad(Void)                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415CondPad()

Local aArea		:= GetArea()
Local cCondPag	:= GetMv("MV_ORCPGPD")
Local cFilSE4		:= xFilial("SE4")

dbSelectArea("SE4")
dbSetOrder(1)
MsSeek(cFilSE4+cCondPag)
If ( !Found() .And. !Empty(cCondPag))
	Reclock("SE4",.T.)
	SE4->E4_FILIAL   := cFilSE4
	SE4->E4_CODIGO   := cCondPag
	SE4->E4_TIPO     := "1"
	SE4->E4_COND     := "0"
	SE4->E4_DESCRI   := STR0033 //"ORCAMENTO"
	MsUnlock()
EndIf
RestArea(aArea)
Return(cCondPag)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡Æo    ³A415TesPad  ³ Autor ³Eduardo Riera        ³ Data ³ 09.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Inicializacao do Tes Padrao                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Void A415TesPad(Void)                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415TesPad(cTes)

Local aArea    := GetArea()
DEFAULT cTes     := GetMv("MV_TESSAI")

dbSelectArea("SF4")
dbSetOrder(1)
MsSeek(xFilial()+cTes)
If (!Found()) .or. !RegistroOk("SF4", lShowError)
	cTes := Space(3)
EndIf
RestArea(aArea)
Return(cTes)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A415Tabela ³ Autor ³Eduardo Riera          ³ Data ³09.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Calculo da Tabela de Preco                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Codigo do Produto                                    ³±±
±±³          ³ExpC2: Codigo da Tabela                                     ³±±
±±³          ³ExpN3: Quantidade                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpN1: Preco do produto                                     ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo retornar a preco de venda     ³±±
±±³          ³do produto                                                  ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³  Usado nos campos C6_PRODUTO e C6_QTDVEN                   ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais                                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415Tabela(cProduto,cTabprec,nQtde)

Local aArea     := GetArea()
Local aAreaSB1  := SB1->(GetArea())
Local aAreaADA  := ADA->(GetArea())
Local aAreaADB  := ADB->(GetArea())
Local aAreaTMP1 := {}
Local aContrato := {}
Local nX        := 0
Local nPrcVen   := 0
Local cOpcional := TMP1->CK_OPC
Local cOpc      := ""
Local cCliente  := M->CJ_CLIENTE
Local cLoja     := M->CJ_LOJA
Local cAliasADA := "A415TABELA"
Local cAliasADB := "A415TABELA"
Local lContrat  := SuperGetMV("MV_PRCCTR")
Local aStruADB  := {}
Local cQuery    := ""
Local cFilADB	:= ""
Local cFilADA	:= ""
Local cFilSCK	:= ""
Local cFilSGA	:= ""
Local nDcPrcVen	:= 0

DEFAULT nQtde   := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se ha contrato de parceria                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lContrat
	cFilADB		:= xFilial("ADB")
	cFilADA		:= xFilial("ADA")
	cFilSCK		:= xFilial("SCK")
	nDcPrcVen	:= GetSx3Cache("CK_PRCVEN","X3_DECIMAL")

	dbSelectArea("ADB")
	dbSetOrder(2)

	aStruADB := ADB->(dbStruct())

	cQuery := "SELECT ADB.*,ADA.ADA_MOEDA "
	cQuery += "FROM "+RetSqlName("ADB")+" ADB, "
	cQuery += RetSqlName("ADA")+" ADA "
	cQuery += "WHERE ADB.ADB_FILIAL='"+cFilADB+"' AND "
	cQuery += "ADB.ADB_CODCLI='"+cCliente+"' AND "
	cQuery += "ADB.ADB_LOJCLI='"+cLoja+"' AND "
	cQuery += "ADB.ADB_CODPRO='"+cProduto+"' AND "
	cQuery += "ADB.D_E_L_E_T_=' ' AND "
	cQuery += "ADA.ADA_FILIAL='"+cFilADA+"' AND "
	cQuery += "ADA.ADA_NUMCTR=ADB.ADB_NUMCTR AND "
	cQuery += "ADA.ADA_STATUS IN ('B','C') AND "
	cQuery += "ADA.D_E_L_E_T_=' '  "

	cQuery := ChangeQuery(cQuery)

	dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasADB)

	For nX := 1 To Len(aStruADB)
		If aStruADB[nX][2] <> "C"
			TcSetField(cAliasADB,aStruADB[nX][1],aStruADB[nX][2],aStruADB[nX][3],aStruADB[nX][4])
		EndIf
	Next nX
	While (cAliasADB)->(! Eof())
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica o saldo de contratos deste orcamento        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If Empty(aContrato)
			dbSelectArea("TMP1")
			aAreaTMP1 := GetArea()
			dbGotop()
			While ( !Eof() )
				If !TMP1->CK_FLAG .And. TMP1->(RecNo()) <> aAreaTMP1[3] .And. !Empty(TMP1->CK_CONTRAT)
					nX := aScan(aContrato,{|x| x[1] == TMP1->CK_CONTRAT .And. x[2] == TMP1->CK_ITEMCON})
					If nX == 0
						aAdd(aContrato,{TMP1->CK_CONTRAT,TMP1->CK_ITEMCON,TMP1->CK_QTDVEN})
						nX := Len(aContrato)
					Else
						aContrato[nX][3] += TMP1->CK_QTDVEN
					EndIf
				EndIf
				dbSelectArea("SCK")
				dbSetOrder(1)
				If MsSeek(cFilSCK + M->CJ_NUM + TMP1->CK_ITEM) .And. !Empty(SCK->CK_CONTRAT)
					nX := aScan(aContrato,{|x| x[1] == SCK->CK_CONTRAT .And. x[2] == SCK->CK_ITEMCON})
					If nX == 0
						aAdd(aContrato,{SCK->CK_CONTRAT,SCK->CK_ITEMCON,0})
						nX := Len(aContrato)
					EndIf
					aContrato[nX][3] -= SCK->CK_QTDVEN
				EndIf
				dbSelectArea("TMP1")
				dbSkip()
			EndDo
			RestArea(aAreaTMP1)
		EndIf
		nX := aScan(aContrato,{|x| x[1] == (cAliasADB)->ADB_NUMCTR .And. x[2] == (cAliasADB)->ADB_ITEM})
		If (cAliasADB)->ADB_QUANT > (cAliasADB)->ADB_QTDEMP+If(nX>0,aContrato[nX][3],0)
			nPrcVen := xMoeda((cAliasADB)->ADB_PRCVEN,(cAliasADA)->ADA_MOEDA,M->CJ_MOEDA,dDataBase,nDcPrcVen)
			If TMP1->CK_QTDVEN > (cAliasADB)->ADB_QUANT-(cAliasADB)->ADB_QTDEMP-If(nX>0,aContrato[nX][3],0)
				TMP1->CK_QTDVEN := (cAliasADB)->ADB_QUANT-(cAliasADB)->ADB_QTDEMP-If(nX>0,aContrato[nX][3],0)
			EndIf
			TMP1->CK_CONTRAT := (cAliasADB)->ADB_NUMCTR
			TMP1->CK_ITEMCON := (cAliasADB)->ADB_ITEM
			Exit
		EndIf
		dbSelectArea(cAliasADB)
		(cAliasADB)->(dbSkip())
	EndDo
	
	(cAliasADB)->(dbCloseArea())
	dbSelectArea("ADB")
	
EndIf
If nPrcVen == 0
	dbSelectArea("SB1")
	dbSetOrder(1)
	If ( !Empty(cProduto))
		nPrcVen := MaTabPrVen(cTabPrec,cProduto,nQtde,cCliente,cLoja,M->CJ_MOEDA,M->CJ_EMISSAO)
	EndIf
	If !(ReadVar() $ "M->CK_PRODUTO|M->CK_QTDVEN|M->CJ_TABELA")
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Aqui ‚ efetuado o tratamento dIferencial de Precos para os   ³
		//³ Opcionais do Produto.                                        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If At("/",cOpcional)>0
			cFilSGA		:= xFilial("SGA")
			SGA->(dbSetOrder(1))
			While !Empty(cOpcional)
				cOpc      := SubStr(cOpcional,1,At("/",cOpcional)-1)
				cOpcional := SubStr(cOpcional,At("/",cOpcional)+1)
				If SGA->(MsSeek(cFilSGA + cOpc))
					nPrcVen += SGA->GA_PRCVEN
				EndIf
			EndDo
		EndIf
	EndIf
EndIf
RestArea(aAreaADA)
RestArea(aAreaADB)
RestArea(aAreaSB1)
RestArea(aArea)
aSize(aAreaADA,0)
aSize(aAreaADB,0)
aSize(aAreaSB1,0)
aSize(aArea,0)
Return (nPrcVen)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A415GeraCL³ Autor ³ Eduardo Riera         ³ Data ³ 05.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de Preenchimento da aCols                         ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ aCols A415GeraCL(Void)                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A415GeraCL()

Local aArea		:= GetArea()
Local aHeader  := {}
Local aCols    := {}
Local nUsado	:= 0
Local nX	:= 0
Local nAcols	:= 0
Local nPos		:= 0
Local nPos2		:= 0
Local nPosItem  := 0
Local nItem		:= 0
Local cFilSBH		:= xFilial("SBH")
Local cFilSB1		:= xFilial("SB1")
Local nTmCLDescr	:= GetSX3Cache("CL_DESCRI","X3_TAMANHO")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta aCols                                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
aHeader := aHeaderSCL
nUsado  := Len(aHeader)
nPosItem:= aScan(aHeader,{|x| Trim(x[2]) == "CL_ITEM" })
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ VerIfico se existe no temporario registros validos           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TMP2") // SCL
dbSetOrder(1)
MsSeek(M->CJ_NUM+TMP1->CK_ITEM,.T.)
While  ( !Eof() .And. M->CJ_NUM+TMP1->CK_ITEM==TMP2->CL_NUM+TMP2->CL_ITEMORC )
	aAdd( aCols , Array(nUsado+1) )
	nACols := Len(aCols)
	For nX := 1 To Len(aHeader)
		If ( aHeader[nX][10] <> "V")
			aCols[nAcols][nX] := FieldGet(FieldPos(aHeader[nX][2]))
		Else
			aCols[nAcols][nX] := CriaVar(aHeader[nX][2],.T.)
		EndIf
	Next nX
	aCols[nAcols,nUsado+1] := TMP2->CL_FLAG
	dbSelectArea("TMP2")
	dbSkip()
EndDo
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifico se o Produto e'uma sugestao de venda e se o acols   ³
//³ esta vazia                                                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( Empty(aCols) )
	nItem := 0
	dbSelectArea("SBH")
	dbSetOrder(1)
	MsSeek(cFilSBH + TMP1->CK_PRODUTO,.T.)
	While  ( !Eof() .And. SBH->BH_FILIAL == cFilSBH .And. ;
			SBH->BH_PRODUTO == TMP1->CK_PRODUTO )
		aAdd( aCols , Array(nUsado+1) )
		nACols := Len(aCols)
		nPos   := 0
		For nX := 1 To Len(aHeader)
			Do Case
			Case Trim(aHeader[nX,2])=="CL_PRODUTO"
				aCols[nACols,nX] := SBH->BH_CODCOMP
				If ( nPos > 0 )
					dbSelectArea("SB1")
					dbSetOrder(1)
					MsSeek(cFilSB1 + aCols[nAcols,nX])
					aCols[nAcols,nPos] := PadL(SB1->B1_DESC, nTmCLDescr)
				EndIf
				nPos := nX
			Case Trim(aHeader[nX,2])=="CL_QUANT"
				aCols[nACols,nX] := SBH->BH_QUANT
			Case Trim(aHeader[nX,2])=="CL_DESCRI"
				If ( nPos > 0 )
					dbSelectArea("SB1")
					dbSetOrder(1)
					MsSeek(cFilSB1 + aCols[nAcols,nPos])
					aCols[nAcols,nX] := PadL(SB1->B1_DESC, nTmCLDescr)
				EndIf
				nPos := nX
			OtherWise
				If !( AllTrim(aHeader[nX,2]) $ "CL_ALI_WT/CL_REC_WT")
					aCols[nAcols][nX] := CriaVar(aHeader[nX][2],.T.)
				EndIf
			EndCase
		Next nX
		aCols[nAcols,nPosItem] := StrZero(++nItem,2)
		aCols[nAcols,nUsado+1] := .F.
		dbSelectArea("SBH")
		dbSkip()
	EndDo
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se nÆo existir registros validos e'criada uma aCols vazia    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ( Empty(aCols) )
	aAdd( aCols , Array(nUsado+1) )
	nACols := Len(aCols)
	For nX := 1 To nUsado
		If !( AllTrim(aHeader[nX,2]) $ "CL_ALI_WT/CL_REC_WT")
			aCols[nAcols][nX] := CriaVar(aHeader[nX][2],.T.)
		EndIf
	Next nX
	aCols[nAcols,nUsado+1] := .F.
	aCols[nAcols,nPosItem] := "01"
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa o a Qtd Necessaria para o orcamento               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nPos := aScan(aHeader,{|x| Trim(x[2]) == "CL_QTDVEN" })
nPos2:= aScan(aHeader,{|x| Trim(x[2]) == "CL_QUANT" })
nACols := Len(aCols)
If ( nPos > 0 .And. nPos2 > 0 )
	For nX := 1 To nACols
		aCols[nX,nPos]  := aCols[nX,nPos2] * TMP1->CK_QTDVEN
	Next
EndIf
RestArea(aArea)
Return(aCols)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A415GrvSCL³ Autor ³ Eduardo Riera         ³ Data ³ 05.12.97 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de Atualizacao do Arquivo Temporario referente ao ³±±
±±³          ³ SCL                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void A415GrvSCL(aHeader,aCols)                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ aHeader e Acols                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A415GrvSCL(aHeader,aCols)

Local nX  := 0
Local nX2 := 0
Local nUsado  := nAcols   := 0
Local nProduto:= aScan(aHeader,{|x| Trim(x[2])=="CL_PRODUTO" })
Local aArea   := GetArea()

dbSelectArea("TMP2")
dbSetOrder(1)
MsSeek(M->CJ_NUM+TMP1->CK_ITEM,.T.)
While  ( !Eof() .And. M->CJ_NUM+TMP1->CK_ITEM==TMP2->CL_NUM+TMP2->CL_ITEMORC )
	RecLock("TMP2")
	dbDelete()
	MsUnlock()
	dbSelectArea("TMP2")
	dbSkip()
EndDo
nAcols := Len(aCols)
For nX := 1 To nAcols
	If ( nProduto > 0 )
		If ( !Empty(aCols[nX,nProduto]) )
			nUsado := Len(aHeader)
			RecLock("TMP2",.T.)
			For nX2 := 1 To nUsado
				If !( AllTrim(aHeader[nX2,2]) $ "CL_ALI_WT/CL_REC_WT")
					nPosField := FieldPos(Trim(aHeader[nX2,2]))
					If ( nPosField > 0 )
						FieldPut(nPosField,aCols[nX,nX2])
					EndIf
				EndIf
			Next nX2
			TMP2->CL_NUM      := M->CJ_NUM
			TMP2->CL_ITEMORC  := TMP1->CK_ITEM
			TMP2->CL_FLAG     := aCols[nX,nUsado+1]
			MsUnlock()
		EndIf
	EndIf
Next nX
RestArea(aArea)
Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A415PrcLis³ Autor ³ Eduardo Riera         ³ Data ³ 06.01.98 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de Calculo do Preco de Lista                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void A415PrcLis(Void)                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ nExpN1: CK_PRUNIT - Preço da Lista                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415PrcLista(nPrcLista)

Local aArea		:= GetArea()
Local cFilSB1		:= xFilial("SB1")

DEFAULT nPrcLista := 0

dbSelectArea("TMP2")
dbSetOrder(1)
MsSeek(M->CJ_NUM+TMP1->CK_ITEM,.T.)

While ( !Eof() .And. M->CJ_NUM == TMP2->CL_NUM .And. ;
		TMP2->CL_ITEMORC == TMP1->CK_ITEM )

	dbSelectArea("SB1")
	dbSetOrder(1)
	MsSeek(cFilSB1 + TMP2->CL_PRODUTO)

	If ( !TMP2->CL_FLAG )

		nPrcLista += (TMP2->CL_QUANT*a415Tabela(TMP2->CL_PRODUTO,M->CJ_TABELA,TMP1->CK_QTDVEN*TMP2->CL_QUANT))

	EndIf

	dbSelectArea("TMP2")
	dbSkip()
EndDo

RestArea(aArea)
Return(nPrcLista)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415Total   ³ Autor ³Viviani Barcellos    ³ Data ³ 15.01.99 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Totalizacao do orcamento                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³Void A415Total(Void)                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ oDlg: Objeto Windows                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A415Total(oDlg)

Local aArea   	:= GetArea()
Local aAreaTmp1	:= TMP1->(GetArea())
Local nTotVal 	:= 0
Local nTotDesc	:= 0
Local nPerDesc  := M->CJ_DESC4
Local nX  := 0
Local aControl

l415Auto := If(Type("l415Auto")<>"U",l415Auto,.F.)
l416Auto := If(Type("l416Auto")<>"U",l416Auto,.F.)

If !(l415Auto) .and. !(l416Auto)
	aControl := oDlg:aControls
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Soma o os valores e os descontos, mostrando-os na tela³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TMP1")
dbGotop()
While ( !Eof() )
	If ( !TMP1->CK_FLAG )
		nTotVal  += TMP1->CK_VALOR
		If (TMP1->CK_PRUNIT > TMP1->CK_PRCVEN)
			nTotDesc += A410Arred((TMP1->CK_PRUNIT * TMP1->CK_QTDVEN),"CK_VALOR") - A410Arred((TMP1->CK_PRCVEN * TMP1->CK_QTDVEN),"CK_VALOR")
		Else
			nTotDesc += TMP1->CK_VALDESC
		EndIf
	EndIf
	dbSelectArea("TMP1")
	dbSkip()
EndDo
nTotDesc += M->CJ_DESCONT
nTotVal  -= M->CJ_DESCONT
nTotDesc += A410Arred(nTotVal*M->CJ_PDESCAB/100,"C6_VALOR")
nTotVal  -= A410Arred(nTotVal*M->CJ_PDESCAB/100,"C6_VALOR")
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Calcula o Desconto por Total                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nTotVal > 0 .And. FtRegraDesc(4,nTotVal+nTotDesc,@M->CJ_DESC4) <> nPerDesc
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Soma o os valores e os descontos, mostrando-os na tela³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nTotVal := 0
	nTotDesc:= 0
	dbSelectArea("TMP1")
	dbGotop()
	While ( !Eof() )
		If ( !TMP1->CK_FLAG )
			nTotVal  += TMP1->CK_VALOR
			If (TMP1->CK_PRUNIT > TMP1->CK_PRCVEN)
				nTotDesc += A410Arred((TMP1->CK_PRUNIT * TMP1->CK_QTDVEN),"CK_VALOR") - A410Arred((TMP1->CK_PRCVEN * TMP1->CK_QTDVEN),"CK_VALOR")
			Else
				nTotDesc += TMP1->CK_VALDESC
			EndIf
		EndIf
		dbSelectArea("TMP1")
		dbSkip()
	EndDo
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Soma as variaveis da Enchoice                                  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nTotVal += M->CJ_FRETE
nTotVal += M->CJ_SEGURO
nTotVal += M->CJ_DESPESA
nTotVal += M->CJ_FRETAUT

If !( l415Auto .or. l416Auto)
	For nX := 1 To Len(aControl)
		If ValType(aControl[nX]) <> "U" .AND. ValType(aControl[nX]:Cargo)=="C"
			Do Case
			Case ( aControl[nX]:Cargo $ "Total" )
				aControl[nX]:SetText(nTotVal)
			Case ( aControl[nX]:Cargo $ "Desconto" )
				aControl[nX]:SetText(nTotDesc)
			Case ( aControl[nX]:Cargo $ "Valor" )
				aControl[nX]:SetText(nTotVal+nTotDesc)
			EndCase
		EndIf
	Next nX
EndIf
RestArea(aAreaTmp1)
RestArea(aArea)
Return(Nil)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A415DesCab³ Autor ³ Eduardo Riera         ³ Data ³18.02.99  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Recalculo automatico do desconto de cabecalho sobre o preco ³±±
±±³          ³de lista.                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Sempre .T.                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpL1 : Default (.T.)                                       ³±±
±±³          ³ExpL2 : Recalcular ( .T. )                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function a415DesCab(lTodos,lRecal,lRecItens)

Local aArea		:= GetArea()
Local aAreaTmp1:= TMP1->(GetArea())
Local nPrcVen   := 0
Local nX        := 0
Local cOpcional	:= ""
Local cOpc		:= ""
Local oDlg
Local cFilSGA		:= xFilial("SGA")
Local nDcDescont	:= GetSx3Cache("CK_DESCONT","X3_DECIMAL")

DEFAULT lTodos	:= .T.
DEFAULT lRecal  := .T.
DEFAULT lRecItens  := .F.

dbSelectArea("TMP1")
If ( lTodos )
	dbGotop()
EndIf
While ( !Eof() )
	nPrcVen := TMP1->CK_PRUNIT
	If ( nPrcVen > 0.00 )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Deve re-calcular o desconto dos items para pegar o desconto escalavel, ³
		//³que eh por item, mais calculado no total.                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If lRecItens
			TMP1->CK_DESCONT	:=	FtRegraDesc(2)
		Endif
		nPrcVen := a415PrcDes(nPrcVen)
		nPrcVen := (nPrcVen*(1-TMP1->CK_DESCONT/100))
		nPrcVen := a410Arred(nPrcVen,"CK_PRCVEN")
		TMP1->CK_PRCVEN := nPrcVen
		TMP1->CK_VALOR  := A410Arred(TMP1->CK_QTDVEN * TMP1->CK_PRCVEN,"CK_VALOR")
		TMP1->CK_VALDESC:= (a415PrcDes(TMP1->CK_PRUNIT)-nPrcVen)*TMP1->CK_QTDVEN
		TMP1->CK_DESCONT:= NoRound((1-(nPrcVen/a415PrcDes(TMP1->CK_PRUNIT)))*100, nDcDescont)

		If !Empty(TMP1->CK_OPC)
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Aqui ‚ efetuado o tratamento diferencial de Precos para os   ³
			//³ Opcionais do Produto.                                        ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			dbSelectArea("SGA")
			dbSetOrder(1)
			cOpcional := TMP1->CK_OPC
			While !Empty(cOpcional)
				cOpc      := SubStr(cOpcional,1,At("/",cOpcional)-1)
				cOpcional := SubStr(cOpcional,At("/",cOpcional)+1)
				If ( MsSeek(cFilSGA + cOpc) )
					TMP1->CK_PRCVEN += SGA->GA_PRCVEN
					TMP1->CK_VALOR  := A410Arred(TMP1->CK_QTDVEN*TMP1->CK_PRCVEN,"CK_VALOR")
				EndIf
			EndDo
        	dbSelectArea("TMP1")
        EndIf
	EndIf
	If ( lTodos )
		dbSelectArea("TMP1")
		dbSkip()
	Else
		Exit
	EndIf
EndDo
RestArea(aAreaTmp1)
If lRecal
	If (Type("l415Auto") == "L" .and. l415Auto) .or. (Type("l416Auto") == "L" .and. l416Auto)
		A415Total()
	Else
		oDlg := GetWndDefault()
		A415Total(oDlg)
		For nX := 1 To Len(oDlg:aControls)
			If ValType(oDlg:aControls[nX]) <> "U"
				oDlg:aControls[nX]:ReFresh()
			EndIf
		Next nX
	EndIf
EndIf
RestArea(aAreaTmp1)
RestArea( aArea )
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A415PrcDes³ Autor ³ Eduardo Riera         ³ Data ³18.02.99  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Calculo do Preco de Venda com desconto de cabecalho sobre o ³±±
±±³          ³preco de Lista.                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpN1: Valor com desconto de cabecalho                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: Preco de Lista                                       ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A415PrcDes(nPrcLista)

nPrcLista := FtDescCab(nPrcLista,{M->CJ_DESC1,M->CJ_DESC2,M->CJ_DESC3,M->CJ_DESC4})

Return(nPrcLista)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma415Bar  ³ Autor ³ Eduardo Riera         ³ Data ³24.03.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ EnchoiceBar especIfica do Mata415                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpC1 -> Tabela alterada - p/ gatilho                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpO1: Objeto Dialog                                       ³±±
±±³          ³ ExpB2: Code Block para o Evento Ok                         ³±±
±±³          ³ ExpB3: Code Block para o Evento Cancel                     ³±±
±±³          ³ ExpN4: nOpc transmitido pela mbrowse                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ma415Bar(oDlg,bOk,bCancel,nOpc)

Local aUsButtons:= {}
Local aButtons 	:= {}
Local lOpcPadrao:= GetNewPar("MV_REPGOPC","N") == "N"
Local nPOpcional:= If(lOpcPadrao,aScan(aHeader,{|x| AllTrim(x[2])=="CK_OPC"}),aScan(aHeader,{|x| AllTrim(x[2])=="CK_MOPC"}))

aAdd(aButtons,{"POSCLI",{|| If(!Empty(M->CJ_CLIENTE),a450F4Con(),.F.)},OemToAnsi(STR0041), OemToAnsi(STR0058) })	//"Posi‡„o de Cliente"
aAdd(aButtons,{"RELATORIO",{||Ma415Impos(aRotina[ nOpc, 4 ])},OemToAnsi(STR0042),OemToAnsi(STR0059) })	//"Planilha Financeira"
If ( RpcCheckTbi() )
	aAdd(aButtons,{"VENDEDOR",{|| Ma415TbiCl()},OemToAnsi(STR0036),OemToAnsi(STR0060)}) //"Inclusao de cliente TBI"
EndIf

If aRotina[ nOpc, 4 ] == 2
	aAdd(aButtons,{ "BMPVISUAL", {|| A415Track() }, OemToAnsi(STR0050),OemToAnsi(STR0061) } )  // "System Tracker"
EndIf
If ( nOpc == 1 .Or. nOpc == 2 .Or. nOpc == 5 ) .And. nPOpcional > 0
	aAdd(aButtons,{"SDUCOUNT", {|| SeleOpc(2,"MATA415",TMP1->CK_PRODUTO,,,Ma415Opc(lOpcPadrao),"M->CK_PRODUTO",.T.,TMP1->CK_QTDVEN,TMP1->CK_ENTREG) } ,STR0072,STR0073}) //"Opcionais Selecionados"###"Opcionais"
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica motivo de bloqueio por regra/verba                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If	SuperGetMv("MV_VEBLQRG", .F., .F.)
	aAdd(aButtons,{"REGRA",{|| OrcBlqRegB("M",nil,nil, "TMP1")},OemToAnsi(STR0084),OemToAnsi(STR0084) }) //"Blq. Regra"
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Adiciona botoes do usuario na EnchoiceBar                              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("MA415BUT")
	If ValType( aUsButtons := ExecBlock( "MA415BUT", .F., .F. ) ) == "A"
		AEval( aUsButtons, { |x| aAdd( aButtons, x ) } )
	EndIf
EndIf
Return (EnchoiceBar(oDlg,bOK,bcancel,,aButtons))

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma415TbiCl³ Autor ³ Eduardo Riera         ³ Data ³24.03.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de atualizacao do Orcamento para clientes nao cadas- ³±±
±±³          ³trados quando vindos do TBI.                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ma415TbiCl()

Local aArea		:= GetArea()
Local lIncSA1		:= .F.
Local cFilSA1		:= xFilial("SA1")

If ( Empty(M->CJ_CLIENTE) .And. !Empty(M->CJ_CNPJCLI) )
	dbSelectArea("SA1")
	dbSetOrder(3)
	If ( !MsSeek(cFilSA1 + M->CJ_CNPJCLI) )
		cCadastro := OemToAnsi(STR0036) //"Inclusao de cliente TBI"
		Processa({|| lIncSA1 := TbSocioCli(M->CJ_CNPJCLI)})
		If ( lIncSA1 )
			dbSelectArea("SA1")
			dbSetOrder(3)
			MsSeek(cFilSA1 + M->CJ_CNPJCLI)
			M->CJ_CLIENTE := SA1->A1_COD
			M->CJ_LOJA := SA1->A1_LOJA
			M->CJ_NOMCLI	:= SA1->A1_NOME
		Else
			Help(" ",1,"TBIfAULT")
		EndIf
	Else
		M->CJ_CLIENTE	:= SA1->A1_COD
		M->CJ_LOJA		:= SA1->A1_LOJA
		M->CJ_NOMCLI	:= SA1->A1_NOME
	EndIf
EndIf
lRefresh := .T.
RestArea(aArea)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415B2BC_R³ Autor ³ Eduardo Riera         ³ Data ³04.04.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de Recepcao das cotacoes via Portal                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void A415B2BC(Void)                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 = [1] IdentIficacao do Campo                         ³±±
±±³          ³         [2] Conteudo do Campo                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Portal TBI                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415B2BC_R()

Processa({|| TbiCot2Orc(,,.T.)}) //"Recebendo dados do Portal TBI"
Processa({|| TbiPC2Orc(,,.T.)}) //"Recebendo dados do Portal TBI"
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415Envia ³ Autor ³ Eduardo Riera         ³ Data ³04.04.2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de Envio dos dados via TBI                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe e ³ Void A415B2BC(Void)                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 = [1] IdentIficacao do Campo                         ³±±
±±³          ³         [2] Conteudo do Campo                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Portal TBI                                                 ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415Envia()

Processa({|| TbiSendOrc(,,,,.T.)})
CloseBrowse()
Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415TabAlt  ³ Autor ³Sergio Silveira      ³ Data ³22/05/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao de Alteracao da tabela de precos.                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpC1 := A415TabAlt()                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpC1 -> Tabela alterada - p/ gatilho                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A415TabAlt()

Local aArea      := GetArea()
Local aAreaSB1   := {}
Local nRegAtu    := 0
Local nPrcVen    := 0
Local nVlrTabela := 0
Local lPrcOrc    := .F.
Local cFilSB1	 := ""

If !( SuperGetMV("MV_ORRECAL", , .T.) ) .AND. Select("TMP1") > 0
	//O recálculo do Orçamento de Venda está HABILITADO.

	aAreaSB1	:= SB1->(GetArea())
	nRegAtu		:= TMP1->( RecNo() )
	TMP2->(dbSetOrder(1))
	SB1->(dbSetOrder(1))
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Atualiza os valores dos itens com base na tabela     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cFilSB1		:= xFilial("SB1")
	lPrcOrc		:= GetMv("MV_PRCORC")
	TMP1->( dbGoTop() )
	While TMP1->( !Eof() )
		If lPrcOrc

			dbSelectArea("TMP2")
			dbSetOrder(1)
			If MsSeek(M->CJ_NUM+TMP1->CK_ITEM,.T.)

				nPrcVen := 0

				While ( !Eof() .And. M->CJ_NUM == TMP2->CL_NUM .And. ;
				      TMP2->CL_ITEMORC == TMP1->CK_ITEM )

				   dbSelectArea("SB1")
				   dbSetOrder(1)
				   MsSeek(cFilSB1 + TMP2->CL_PRODUTO)

				   If ( !TMP2->CL_FLAG )
				   		nPrcVen += (TMP2->CL_QUANT*A415Tabela(TMP2->CL_PRODUTO,M->CJ_TABELA,TMP2->CL_QUANT*TMP1->CK_QTDVEN))
				   EndIf

				   dbSelectArea("TMP2")
				   dbSkip()
				EndDo
			Else
				nPrcVen := A415Tabela(TMP1->CK_PRODUTO,M->CJ_TABELA,TMP1->CK_QTDVEN)
			Endif

		Else
			nPrcVen := A415Tabela(TMP1->CK_PRODUTO,M->CJ_TABELA,TMP1->CK_QTDVEN)
		Endif

		nVlrTabela := nPrcVen

		RecLock("TMP1",.F.)
		TMP1->CK_PRUNIT  := nVlrTabela
		TMP1->CK_PRCVEN  := nPrcVen
		TMP1->CK_VALOR   := A410Arred(TMP1->CK_QTDVEN*TMP1->CK_PRCVEN,"CK_VALOR")
		TMP1->(MsUnlock())

		TMP1->(dbSkip())
	EndDo

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Aplica os descontos de cabecalho e de item           ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	A415DesCab( .T. )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Posiciona na linha original                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	TMP1->( MsGoto( nRegAtu ) )
	If Type("oGetDad") <> "U"
		oGetDad:oBrowse:Refresh()
	Endif

	RestArea(aAreaSB1)
Endif

RestArea(aArea)
aSize(aArea,    0)
aSize(aAreaSB1, 0)
Return( M->CJ_TABELA )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415TabCli  ³ Autor ³Sergio Silveira      ³ Data ³22/05/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Alteracao da tabela de precos com base na tabela do cliente³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpC1 := A415TabCli()                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpC1 -> Tabela do cliente                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³13/08/2007³Norbert Waage  ³Bops 130060: Atualiza a tabela de precos no ³±±
±±³          ³               ³cabecalho da ExecAuto, quando houver.       ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function A415TabCli()

Local cTabCli		// Codigo da tabela do cliente

If ( ExistBlock("A415TBPR") )
	cTabCli := ExecBlock("A415TBPR",.F.,.F.)
	M->CJ_TABELA := cTabCli
Else 
	cTabCli      := SA1->A1_TABELA
	M->CJ_TABELA := cTabCli
Endif

If !Empty(SA1->A1_DESC)
	M->CJ_DESC1 := SA1->A1_DESC 
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿ 
//³ Atualiza os valores com base na nova tabela ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
A415TabAlt()
Return( cTabCli )

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MA415Impos³ Autor ³ Eduardo Riera         ³ Data ³24.07.2000 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Ma415Impos(nOpc)                                             ³±±
±±³          ³Funcao de calculo dos impostos contidos no orcamento de venda³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ nOpc                                                        ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta funcao efetua os calculos de impostos (ICMS,IPI,ISS,etc)³±±
±±³          ³com base nas funcoes fiscais, a fim de possibilitar ao usua- ³±±
±±³          ³rio o valor de desembolso financeiro.                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ma415Impos( nOpc, lRetTotal, aRefRentab )

Local aArea		:= GetArea()
Local aAreaSA1	:= SA1->(GetArea())
Local aAreaTMP1 := TMP1->(GetArea())
Local aTitles   := {STR0043,STR0044,STR0062} //"Nota Fiscal"###"Duplicatas###"Rentabilidade"
Local aDupl     := {}
Local aFlHead   := { STR0045,STR0046 } //"Vencimento"###"Valor"
Local aVencto   := {}
Local aRFHead   := { RetTitle("C6_PRODUTO"),RetTitle("C6_VALOR"),STR0063,STR0064,STR0065,STR0066}//"C.M.V"###"Vlr.Presente"###"Lucro Bruto"###"Margem de Contribuição(%)"
Local aRentab   := {}
Local nX        := 0
Local nAcerto   := 0
Local nPrcLista := 0
Local nValMerc  := 0
Local nDesconto := 0
Local nAcresFin := 0
Local nQtdPeso  := 0
Local nItem     := 0
Local cParc     := ""
Local dDataCnd  := M->CJ_EMISSAO
Local lCondVenda := .F.  // Template GEM - Se existe condicao de venda
Local lM415Ipi	:= ExistBlock("M415IPI")
Local lM415Icm	:= ExistBlock("M415ICM")
Local lM415Soli	:= ExistBlock("M415SOLI")
Local oDlg
Local oDupl
Local oFolder
Local oRentab
Local nValTotal := 0 //Valor total utilizado no retorno quando lRetTotal for .T.
Local lProspect := .F.
Local cTipo	  	:= ""
Local cFil		:= cFilAnt
Local nNumParc	:= SuperGetMv("MV_NUMPARC")
Local cFilSB1		:= xFilial("SB1")
Local cFilSB2		:= xFilial("SB2")
Local cFilSF4		:= xFilial("SF4")
Local lDescSai	:= Iif(GetNewPar('MV_DESCSAI','1') == "2",.T.,.F.)

Default lRetTotal := .F.
Default aRefRentab := {}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona Registros                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SA1")
dbSetOrder(1)
MsSeek(xFilial("SA1")+If(!Empty(M->CJ_CLIENT),M->CJ_CLIENT,M->CJ_CLIENTE)+M->CJ_LOJAENT)

dbSelectArea("SE4")
dbSetOrder(1)
MsSeek(xFilial("SE4")+M->CJ_CONDPAG)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inicializa a funcao fiscal                   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(M->CJ_PROSPE) .And. !Empty(M->CJ_LOJPRO)
	cTipo := Posicione("SUS",1,xFilial("SUS") + M->CJ_PROSPE + M->CJ_LOJPRO,"US_TIPO")
	lProspect := .T.
Endif

//Altera cFilAnt para calcular os impostos na filial de entrega.
If cFilAnt != M->CJ_FILENT
	cFilAnt := M->CJ_FILENT
EndIf

MaFisSave()
MaFisEnd()
MaFisIni(Iif(Empty(M->CJ_CLIENT),M->CJ_CLIENTE,M->CJ_CLIENT),;// 1-Codigo Cliente/Fornecedor
	M->CJ_LOJAENT,;		// 2-Loja do Cliente/Fornecedor
	"C",;				// 3-C:Cliente , F:Fornecedor
	"N",;				// 4-Tipo da NF
	Iif(lProspect,cTipo,Iif(SCJ->(ColumnPos("CJ_TIPOCLI")) > 0,M->CJ_TIPOCLI,SA1->A1_TIPO)),;		// 5-Tipo do Cliente/Fornecedor
	Nil,;
	Nil,;
	Nil,;
	Nil,;
	"MATA461",;
	Nil,;
	Nil,;
	IiF(lProspect,M->CJ_PROSPE+M->CJ_LOJPRO,""))

//Na argentina o calculo de impostos depende da serie.
If cPaisLoc == 'ARG'
	MaFisAlt('NF_SERIENF',LocXTipSer('SA1',MVNOTAFIS))
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Agrega os itens para a funcao fiscal         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("TMP1")
dbGotop()
While ( !Eof() )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se a linha foi deletada                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( !TMP1->CK_FLAG .And. !Empty(TMP1->CK_PRODUTO) )
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Posiciona Registros                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SB1->(dbSetOrder(1))
		If SB1->(MsSeek(cFilSB1 + TMP1->CK_PRODUTO))
			nQtdPeso := TMP1->CK_QTDVEN*SB1->B1_PESO
		EndIf
	    SB2->(dbSetOrder(1))
	    SB2->(MsSeek(cFilSB2 + TMP1->CK_PRODUTO + TMP1->CK_LOCAL))
	    SF4->(dbSetOrder(1))
	    SF4->(MsSeek(cFilSF4 + TMP1->CK_TES))
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Calcula o preco de lista                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nValMerc  := TMP1->CK_VALOR
		nPrcLista := TMP1->CK_PRUNIT
		nQtdPeso  := 0
		nItem++
		If ( nPrcLista == 0 )
			nPrcLista := A410Arred(nValMerc/TMP1->CK_QTDVEN,"CK_PRCVEN")
		EndIf
		nAcresFin := A410Arred(TMP1->CK_PRCVEN*SE4->E4_ACRSFIN/100,"D2_PRCVEN")
		nValMerc  += A410Arred(nAcresFin*TMP1->CK_QTDVEN,"D2_TOTAL")
		nDesconto := A410Arred(nPrcLista*TMP1->CK_QTDVEN,"D2_DESCON")-nValMerc
		nDesconto := IIf(nDesconto==0,TMP1->CK_VALDESC,nDesconto)
		nDesconto := Max(0,nDesconto)
		nPrcLista += nAcresFin

		//Para os outros paises, este tratamento e feito no programas que calculam os impostos.
		If cPaisLoc=="BRA" .or. lDescSai
  			nValMerc  += nDesconto
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Verifica a data de entrega para as duplicatas³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( dDataCnd > TMP1->CK_ENTREG .And. !Empty(TMP1->CK_ENTREG) )
			dDataCnd := TMP1->CK_ENTREG
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Agrega os itens para a funcao fiscal         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		MaFisAdd(TMP1->CK_PRODUTO,;   	// 1-Codigo do Produto ( Obrigatorio )
			TMP1->CK_TES,;	   	// 2-Codigo do TES ( Opcional )
			TMP1->CK_QTDVEN,;  	// 3-Quantidade ( Obrigatorio )
			nPrcLista,;		  	// 4-Preco Unitario ( Obrigatorio )
			nDesconto,; 	// 5-Valor do Desconto ( Opcional )
			"",;	   			// 6-Numero da NF Original ( Devolucao/Benef )
			"",;				// 7-Serie da NF Original ( Devolucao/Benef )
			0,;					// 8-RecNo da NF Original no arq SD1/SD2
			0,;					// 9-Valor do Frete do Item ( Opcional )
			0,;					// 10-Valor da Despesa do item ( Opcional )
			0,;					// 11-Valor do Seguro do item ( Opcional )
			0,;					// 12-Valor do Frete Autonomo ( Opcional )
			nValMerc,;			// 13-Valor da Mercadoria ( Obrigatorio )
			0,;					// 14-Valor da Embalagem ( Opiconal )
			, , , , , , , , , , , , ,;
			TMP1->CK_CLASFIS) // 28-Classificacao fiscal)

		SB1->(dbSetOrder(1))
		If SB1->(MsSeek(cFilSB1 + TMP1->CK_PRODUTO))
			nQtdPeso := TMP1->CK_QTDVEN*SB1->B1_PESO
		Endif

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Calculo do ISS                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If SA1->A1_INCISS == "N"
			If ( SF4->F4_ISS=="S" )
				nPrcLista := a410Arred(nPrcLista/(1-(MaAliqISS(nItem)/100)),"D2_PRCVEN")
				nValMerc  := a410Arred(nValMerc/(1-(MaAliqISS(nItem)/100)),"D2_PRCVEN")
				MaFisAlt("IT_PRCUNI",nPrcLista,nItem)
				MaFisAlt("IT_VALMERC",nValMerc,nItem)
			EndIf
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Altera peso para calcular frete              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		MaFisAlt("IT_PESO",nQtdPeso,nItem)
		MaFisAlt("IT_PRCUNI",nPrcLista,nItem)
		MaFisAlt("IT_VALMERC",nValMerc,nItem)

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Analise da Rentabilidade                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If SF4->F4_DUPLIC=="S"
			nY := aScan(aRentab,{|x| x[1] == TMP1->CK_PRODUTO})
			If nY == 0
				aAdd(aRenTab,{TMP1->CK_PRODUTO,0,0,0,0,0})
				nY := Len(aRenTab)
			EndIf

			If cPaisLoc=="BRA"
				aRentab[nY][2] += (nValMerc - nDesconto)
			Else
				aRentab[nY][2] += nValMerc
			Endif
			aRentab[nY][3] += TMP1->CK_QTDVEN*SB2->B2_CM1
		EndIf
		If lM415Ipi .Or. lM415Icm .Or. lM415Soli
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Ponto de Entrada M415IPI para alterar os valores do IPI referente a palnilha financeira           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lM415Ipi
				VALORIPI    := MaFisRet(nItem,"IT_VALIPI")
				BASEIPI     := MaFisRet(nItem,"IT_BASEIPI")
				QUANTIDADE  := MaFisRet(nItem,"IT_QUANT")
				ALIQIPI     := MaFisRet(nItem,"IT_ALIQIPI")
				BASEIPIFRETE:= MaFisRet(nItem,"IT_FRETE")
				MaFisAlt("IT_VALIPI",ExecBlock("M415IPI",.F.,.F.,{ nItem }),nItem,.T.)
				MaFisLoad("IT_BASEIPI",BASEIPI ,nItem)
				MaFisLoad("IT_ALIQIPI",ALIQIPI ,nItem)
				MaFisLoad("IT_FRETE"  ,BASEIPIFRETE,nItem,"11")
				MaFisEndLoad(nItem,1)
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Ponto de Entrada M415ICM para alterar os valores do ICM referente a palnilha financeira           ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lM415Icm
				_BASEICM    := MaFisRet(nItem,"IT_BASEICM")
				_ALIQICM    := MaFisRet(nItem,"IT_ALIQICM")
				_QUANTIDADE := MaFisRet(nItem,"IT_QUANT")
				_VALICM     := MaFisRet(nItem,"IT_VALICM")
				_FRETE      := MaFisRet(nItem,"IT_FRETE")
				_VALICMFRETE:= MaFisRet(nItem,"IT_ICMFRETE")
				_DESCONTO   := MaFisRet(nItem,"IT_DESCONTO")
				ExecBlock("M415ICM",.F.,.F., { nItem } )
				MaFisLoad("IT_BASEICM" ,_BASEICM    ,nItem)
				MaFisLoad("IT_ALIQICM" ,_ALIQICM    ,nItem)
				MaFisLoad("IT_VALICM"  ,_VALICM     ,nItem)
				MaFisLoad("IT_FRETE"   ,_FRETE      ,nItem)
				MaFisLoad("IT_ICMFRETE",_VALICMFRETE,nItem)
				MaFisLoad("IT_DESCONTO",_DESCONTO   ,nItem)
				MaFisEndLoad(nItem,1)
			EndIf
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³Ponto de Entrada M415SOLI para alterar os valores do ICM Solidario referente a palnilha financeira³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If lM415Soli
				ICMSITEM    := MaFisRet(nItem,"IT_VALICM")		// variavel para ponto de entrada
				QUANTITEM   := MaFisRet(nItem,"IT_QUANT")		// variavel para ponto de entrada
				BASEICMRET  := MaFisRet(nItem,"IT_BASESOL")	    // criado apenas para o ponto de entrada
				MARGEMLUCR  := MaFisRet(nItem,"IT_MARGEM")		// criado apenas para o ponto de entrada
				aSolid := ExecBlock("M415SOLI",.f.,.f.,{nItem})
				aSolid := IIF(ValType(aSolid) == "A" .And. Len(aSolid) == 2, aSolid,{})
				If !Empty(aSolid)
					MaFisLoad("IT_BASESOL",NoRound(aSolid[1],2),nItem)
					MaFisLoad("IT_VALSOL" ,NoRound(aSolid[2],2),nItem)
					MaFisEndLoad(nItem,1)
				Endif
			EndIf
		EndIf
	EndIf
	dbSelectArea("TMP1")
	dbSkip()
EndDo
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Indica os valores do cabecalho               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
MaFisAlt("NF_FRETE",M->CJ_FRETE)
MaFisAlt("NF_SEGURO",M->CJ_SEGURO)
MaFisAlt("NF_AUTONOMO",M->CJ_FRETAUT)
MaFisAlt("NF_DESPESA",M->CJ_DESPESA)
MaFisAlt("NF_DESCONTO",MaFisRet(,"NF_DESCONTO")+MaFisRet(,"NF_VALMERC")*M->CJ_PDESCAB/100)
MaFisAlt("NF_DESCONTO",MaFisRet(,"NF_DESCONTO")+SCJ->CJ_DESCONT)
MaFisWrite(1)
//
// Template GEM - Gestao de Empreendimentos Imobiliarios
//
// Verifica se a condicao de pagamento tem vinculacao com uma condicao de venda
//
If ExistTemplate("GMCondPagto")
	lCondVenda := ExecTemplate("GMCondPagto",.F.,.F.,{M->CJ_CONDPAG,} )
	If ValType("lCondVenda") # "L"
		lCondVenda := .f.
	EndIf
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Calcula os venctos conforme a condicao de pagto  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectarea("SE4")
dbSetOrder(1)
MsSeek(xFilial("SE4")+M->CJ_CONDPAG)
If ((SE4->E4_TIPO=="9".AND.!(INCLUI.OR.ALTERA)).OR.SE4->E4_TIPO<>"9")
	If SE4->E4_TIPO=="9"
		cParc := "1"
		For nX := 1 To nNumParc
			If SCJ->(FieldPos("CJ_DATA"+cParc))<> 0 .And. SCJ->(FieldPos("CJ_PARC"+cParc))<> 0 .And. !Empty(SCJ->(FieldGet(FieldPos("CJ_PARC"+cParc))))
				aAdd(aDupl,{SCJ->(FieldGet(FieldPos("CJ_DATA"+cParc))),SCJ->(FieldGet(FieldPos("CJ_PARC"+cParc)))})
			EndIf
			cParc := Soma1(cParc)
		Next nX
	Else
		aDupl := Condicao(MaFisRet(,"NF_BASEDUP"),M->CJ_CONDPAG,MaFisRet(,"NF_VALIPI"),dDataCnd,MaFisRet(,"NF_VALSOL"),,,nAcresFin)
	EndIf
	If Empty(aDupl)
		aDupl := {{Ctod(""),0}}
	EndIf
	If ! lCondVenda
		For nX := 1 To Len(aDupl)
			nAcerto += aDupl[nX][2]
			If AllTrim(SE4->E4_COND) == "%"
				aDupl[nX][2] := aDupl[nX][2]*MaFisRet(,"NF_BASEDUP")/100
			EndIf
		Next nX

		If AllTrim(SE4->E4_COND) <> "%"
			aDupl[Len(aDupl)][2] += MaFisRet(,"NF_BASEDUP") - nAcerto
		EndIf
	EndIf
	aVencto := aClone(aDupl)
	For nX := 1 To Len(aDupl)
		aDupl[nX][2] := TransForm(aDupl[nX][2],PesqPict("SE1","E1_VALOR"))
	Next nX
Else
	aDupl   := {{Ctod(""),TransForm(MaFisRet(,"NF_BASEDUP"),PesqPict("SE1","E1_VALOR"))}}
	aVencto := {{dDataBase,MaFisRet(,"NF_BASEDUP")}}
EndIf

//
// Template GEM - Gestao de empreendimentos Imobiliarios
// Gera os vencimentos e valores das parcelas conforme a condicao de venda
//
If lCondVenda .AND. ExistTemplate("GMMA415Dupl")
	aVencto := ExecTemplate("GMMA415Dupl",.F.,.F.,{M->CJ_NUM ,M->CJ_CONDPAG,dDataCnd,,MaFisRet(,"NF_BASEDUP") ,aVencto})
	aDupl := {}
	aEval(aVencto ,{|aTitulo| aAdd( aDupl ,{transform(aTitulo[1],x3Picture("E1_VENCTO")) ,transform(aTitulo[2],x3Picture("E1_VALOR"))}) })
	If Len(aDupl) == 0
		aDupl := {{Ctod(""),TransForm(MaFisRet(,"NF_BASEDUP"),PesqPict("SE1","E1_VALOR"))}}
		aVencto := {{dDataBase,MaFisRet(,"NF_BASEDUP")}}
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Analise da Rentabilidade - Valor Presente    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
a415RentVP( @aRenTab ,@aVencto )
//Restaura cFilAnt
cFilAnt := cFil

//lRetTotal quando .T. não exibe a planilha mas retorna o NF_TOTAL de MafisRet
If !lRetTotal

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Monta a tela de exibicao dos valores fiscais ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	DEFINE MSDIALOG oDlg TITLE OemToAnsi(STR0042) FROM 09,00 TO 28,80 //"Planilha Financeira"
	oFolder := TFolder():New(001,001,aTitles,{"HEADER"},oDlg,,,, .T., .F.,315,140)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Folder 1                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	MaFisRodape(1,oFolder:aDialogs[1],,{005,001,310,60},Nil,.T.)
	@ 070,005 SAY RetTitle("F2_FRETE")		SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
	@ 070,105 SAY RetTitle("F2_SEGURO")		SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
	@ 070,205 SAY RetTitle("F2_DESCONT")	SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
	@ 085,005 SAY RetTitle("F2_FRETAUT")	SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
	@ 085,105 SAY RetTitle("F2_DESPESA")	SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
	@ 085,205 SAY RetTitle("F2_VALFAT")		SIZE 40,10 PIXEL OF oFolder:aDialogs[1]
	@ 070,050 MSGET MaFisRet(,"NF_FRETE")		PICTURE PesqPict("SF2","F2_FRETE",16,2)		SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 070,150 MSGET MaFisRet(,"NF_SEGURO")  	PICTURE PesqPict("SF2","F2_SEGURO",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 070,250 MSGET MaFisRet(,"NF_DESCONTO")	PICTURE PesqPict("SF2","F2_DESCONT",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 085,050 MSGET MaFisRet(,"NF_AUTONOMO")	PICTURE PesqPict("SF2","F2_FRETAUT",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 085,150 MSGET MaFisRet(,"NF_DESPESA")		PICTURE PesqPict("SF2","F2_DESPESA",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 085,250 MSGET MaFisRet(,"NF_BASEDUP")		PICTURE PesqPict("SF2","F2_VALFAT",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 105,005 TO 106,310 PIXEL OF oFolder:aDialogs[1]
	@ 110,005 SAY OemToAnsi(STR0047)   SIZE 40,10 PIXEL OF oFolder:aDialogs[1] //"Total da Nota"
	@ 110,050 MSGET MaFisRet(,"NF_TOTAL")      PICTURE PesqPict("SF2","F2_VALBRUT",16,2) 	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[1]
	@ 110,270 BUTTON OemToAnsi(STR0048)			SIZE 040,11 FONT oFolder:aDialogs[1]:oFont ACTION oDlg:End() OF oFolder:aDialogs[1] PIXEL		//"Sair"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Folder 2                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	@ 005,001 LISTBOX oDupl FIELDS TITLE aFlHead[1],aFlHead[2] SIZE 310,095 	OF oFolder:aDialogs[2] PIXEL
	oDupl:SetArray(aDupl)
	oDupl:bLine := {|| aDupl[oDupl:nAt] }
	@ 105,005 TO 106,310 PIXEL OF oFolder:aDialogs[2]
	If cPaisLoc == "BRA"
		@ 110,005 SAY RetTitle("F2_VALFAT")		SIZE 40,10 PIXEL OF oFolder:aDialogs[2]
	Else
		@ 110,005 SAY OemToAnsi(STR0051)    		SIZE 40,10 PIXEL OF oFolder:aDialogs[2]
	Endif
	@ 110,050 MSGET MaFisRet(,"NF_BASEDUP")		PICTURE PesqPict("SF2","F2_VALFAT",16,2)	SIZE 50,07 PIXEL WHEN .F. OF oFolder:aDialogs[2]
	@ 110,270 BUTTON OemToAnsi(STR0048)			SIZE 040,11 FONT oFolder:aDialogs[1]:oFont ACTION oDlg:End() OF oFolder:aDialogs[2] PIXEL	//"Sair"
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Folder 3                                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	@ 005,001 LISTBOX oRentab FIELDS TITLE aRFHead[1],aRFHead[2],aRFHead[3],aRFHead[4],aRFHead[5],aRFHead[6] SIZE 310,095 	OF oFolder:aDialogs[3] PIXEL
	@ 110,270 BUTTON OemToAnsi(STR0048)			SIZE 040,11 FONT oFolder:aDialogs[3]:oFont ACTION oDlg:End() OF oFolder:aDialogs[3] PIXEL		//"Sair"
	If Empty(aRentab)
		aRentab   := {{"",0,0,0,0,0}}
	EndIf
	oRentab:SetArray(aRentab)
	oRentab:bLine := {|| aRentab[oRentab:nAt] }

	//
	// Template GEM - Gestao de empreendimentos imobiliarios
	// Manutencao dos itens da condicao de venda
	//
	If ExistTemplate("GMMA415CVND",,.T.) .AND. HasTemplate("LOT")
		If ExistTemplate("GMMA415Dupl")
			@ 110,170 BUTTON OemToAnsi("Cond. de Venda") SIZE 050,11 FONT oFolder:aDialogs[1]:oFont ;
					  ACTION ( ExecTemplate("GMMA415CVND",.F.,.F.,{nOpc ,M->CJ_NUM ,M->CJ_CONDPAG ,dDataCnd ,MaFisRet(,"NF_BASEDUP")}) ;
							  ,aVencto := ExecTemplate("GMMA415Dupl",.F.,.F.,{M->CJ_NUM ,M->CJ_CONDPAG,dDataCnd,,MaFisRet(,"NF_BASEDUP"),aVencto}) ;
							  ,( aDupl := {} ,aEval(aVencto ,{|aTitulo| aAdd( aDupl ,{transform(aTitulo[1],x3Picture("E1_VENCTO")) ,transform(aTitulo[2],x3Picture("E1_VALOR"))})}) ;
								 ,a415RentVP( @aRenTab ,@aVencto ) );
							  ,(oDupl:SetArray(aDupl),	oDupl:bLine := {|| aDupl[oDupl:nAt] }) ;
							  ,(oRentab:SetArray(aRenTab) ,oRentab:bLine := {|| aRenTab[oRenTab:nAt] }) ) ;
					  OF oFolder:aDialogs[2] PIXEL
		EndIf
	EndIf
	@ 110,270 BUTTON OemToAnsi(STR0048)	SIZE 040,11 FONT oFolder:aDialogs[1]:oFont ACTION oDlg:End() OF oFolder:aDialogs[2] PIXEL	//"Sair"

	ACTIVATE MSDIALOG oDlg CENTERED
Else
	nValTotal := MaFisRet(,"NF_TOTAL")
EndIf

MaFisEnd()
MaFisRestore()

RestArea(aAreaTMP1)
RestArea(aAreaSA1)
RestArea(aArea)



aRefRentab := aRentab

If !lRetTotal
	Return(.T.)
Else
	Return(nValTotal)
EndIf

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MaCanAltOrc ³ Autor ³Eduardo Riera        ³ Data ³22/07/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Verifica se Orcamento de Venda pode ser alterado.           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpC1 := MaCanAltOrc()                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1: Alias do SCJ                                        ³±±
±±³          ³ ExpC2: Alias do SCK                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Indica se o item de Orcamento pode ser alterado   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³ O SCJ deve estar posicionado e a funcao deve ser processada³±±
±±³          ³ item a item do SCK.                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MaCanAltOrc()

Local lRetorno := .T.

If ( !SCJ->CJ_STATUS $ "A#D#E#F" )
	lRetorno := .F.
Else
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³	O alias "__GdQuery" é utilizado pela FillGetDb().           ³
	//³	Este alias não pode ser alterado ou desposicionado de modo  ³
	//³	algum, pois nela consta os registros que serão gravados na  ³
	//³	tabela temporãria "TMP1" assim que forem validados.		    ³
	//³	Foi necessária esta validação, pois neste momento a tabela  ³
	//³	TMP1 esta aberta e aguardando a liberação dos registros que ³
	//³	foram validados pelo parâmetro "uSeekFor" da FillGetDb.		³
	//³	Obs: A Tabela principal SCK estará desposicionada, pois  	³
	//³	passaram a utilizar o FillGetDb() na criação das tabelas    ³
	//³	temporárias.												³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Upper(Alias()) == Upper("__GdQuery") .And. Select("__GdQuery")<>0
		lRetorno := Empty(CK_NUMOP)
	EndIf
EndIf

If !lRetorno
	lRetAux := lRetorno
EndIf

Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MaNoDelOrc  ³ Autor ³Marco Bianchi        ³ Data ³03/01/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Verifica se Orcamento de Venda pode ser Deletado.           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpC1 := MaCanDelOrc()                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1: Alias do SCJ                                        ³±±
±±³          ³ ExpC2: Alias do SCK                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Indica se o item de Orcamento pode ser deletado   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³ O SCJ deve estar posicionado e a funcao deve ser processada³±±
±±³          ³ item a item do SCK.                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function MaNoDelOrc()

Local lRetorno := .T.
Local cPedido := ""

If Select("TMP1")<>0
	lRetorno := Empty(CK_NUMOP)
	If ( SCJ->CJ_STATUS == "B" .And. lRetorno)
		If !Empty(CK_NUMPV)
			cPedido := CK_NUMPV
			dbSelectArea("SC5")
			dbSetOrder(1)
			If MsSeek(xFilial("SC5")+cPedido)
				lRetorno := .F.
			EndIf
		EndIf
	EndIf
EndIf

If !Empty(SCJ->CJ_PROPOST) .And. lRetorno
	dbSelectArea("ADY")
	dbSetOrder(1)
	If MsSeek(xFilial("ADY")+SCJ->CJ_PROPOST)
		//Valida se processo foi chamado por execauto da proposta comercial que passou
		// operação de exclusão quando alterados dados que compõem o cabeçalho do
		// orçamento, assim excluindo registro e incluindo novo contendo alterações.
		IF !ISINCALLSTACK( " At600Orc " )
			lRetorno := .F.
			Help(" ",1,"HELP",,STR0088,1,0)
		EndIf
	EndIf
EndIf

If !lRetorno
	lRetAux := lRetorno
EndIf

Return(lRetorno)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MaCanDelOrc ³ Autor ³Eduardo Riera        ³ Data ³22/07/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Verifica se Orcamento de Venda pode ser Deletado.           ³±±
±±³          ³Esta funcao e uilizada pelo fonte FATA300.                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpC1 := MaCanDelOrc()                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1: Alias do SCJ                                        ³±±
±±³          ³ ExpC2: Alias do SCK                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Indica se o item de Orcamento pode ser deletado   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³ O SCJ deve estar posicionado e a funcao deve ser processada³±±
±±³          ³ item a item do SCK.                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Function MaCanDelOrc(cAliasSCJ,cAliasSCK)
Local lRetorno := .T.
If ( cAliasSCK <> Nil )
	lRetorno := Empty((cAliasSCK)->CK_NUMOP)
	If ( (cAliasSCJ)->CJ_STATUS == "B" .And. lRetorno)
		If !Empty((cAliasSCK)->CK_NUMPV)
			dbSelectArea("SC5")
			dbSetOrder(1)
			If MsSeek(xFilial("SC5")+(cAliasSCK)->CK_NUMPV)
				lRetorno := .F.
			EndIf
		EndIf
	EndIf
EndIf

Return(lRetorno)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MaAvalOrc   ³ Autor ³Eduardo Riera        ³ Data ³04/10/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de avaliacao dos eventos de um orcamento             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpC1 := MaCanDelOrc()                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1: Alias do SCJ ou SCK                                 ³±±
±±³          ³ ExpN2: Evento que deve ser avaliado                        ³±±
±±³          ³        [01] Implantacao do item do Orcamento de Venda (SCK)³±±
±±³          ³        [02] Estorno do item do Orcamento de Venda (SCK)    ³±±
±±³          ³        [03] Baixa do item do Orcamento de Venda (SCK)      ³±±
±±³          ³        [04] Cancelamento do Orcamento de venda ( SCK )     ³±±
±±³          ³        [11] Implantacao de um orcamento de venda ( SCJ )   ³±±
±±³          ³        [12] Estorno de um orcamento de venda ( SCJ )       ³±±
±±³          ³        [13] Baixa do Orcamento de venda                    ³±±
±±³          ³        [14] Cancelamento do Orcamento de venda             ³±±
±±³          ³ ExpC3: Status padrao para o orcamento(Default ABERTO)      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1 -> Indica se o item de Orcamento pode ser deletado   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³ O SCJ deve estar posicionado, pois sobre atualizacao, o SCK³±±
±±³          ³nao sofre atualizacao mas deve estar posicionado tb.        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function MaAvalOrc(cAlias,nEvento,cStatus)

Local aArea := GetArea()

DEFAULT cStatus := "A"

Do Case
Case nEvento == 01


	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiza os dados do contrato de parceria    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(SCK->CK_CONTRAT) .And. !Empty(SCK->CK_ITEMCON)
		dbSelectArea("ADB")
		dbSetOrder(1)
		If MsSeek(xFilial("ADB")+SCK->CK_CONTRAT+SCK->CK_ITEMCON)
			RecLock("ADB")
			If Altera 
				If SCK->CK_QTDVEN < nQtdEmp
					ADB->ADB_QTDEMP -= (nQtdEmp - SCK->CK_QTDVEN)
				Else
					ADB->ADB_QTDEMP += (SCK->CK_QTDVEN - nQtdEmp)
				EndIf	
			Else
				ADB->ADB_QTDEMP += SCK->CK_QTDVEN
			EndIf	
			If ADB->ADB_QTDEMP > ADB->ADB_QUANT
				ADB->ADB_QTDEMP -= SCK->CK_QTDVEN
				RecLock("SCK")
				SCK->CK_CONTRAT := ""
				SCK->CK_ITEMCON := ""
				MsUnLock()
				dbSelectArea("ADB")
			EndIf
			MsUnLock()
		EndIf
	EndIf
Case nEvento == 02
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiza os dados Complementares             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiza os dados do contrato de parceria    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(SCK->CK_CONTRAT) .And. !Empty(SCK->CK_ITEMCON)
		dbSelectArea("ADB")
		dbSetOrder(1)
		If MsSeek(xFilial("ADB")+SCK->CK_CONTRAT+SCK->CK_ITEMCON)
			RecLock("ADB")
			ADB->ADB_QTDEMP -= SCK->CK_QTDVEN
			MsUnLock()
		EndIf
	EndIf
Case nEvento == 03
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiza os dados Complementares             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cStatus := "B" //Baixado
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiza os dados do contrato de parceria    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(SCK->CK_CONTRAT) .And. !Empty(SCK->CK_ITEMCON)
		dbSelectArea("ADB")
		dbSetOrder(1)
		If MsSeek(xFilial("ADB")+SCK->CK_CONTRAT+SCK->CK_ITEMCON)
			RecLock("ADB")
			ADB->ADB_QTDEMP -= SCK->CK_QTDVEN
			MsUnLock()
		EndIf
	EndIf
Case nEvento == 04
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiza os dados Complementares             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiza os dados do contrato de parceria    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If !Empty(SCK->CK_CONTRAT) .And. !Empty(SCK->CK_ITEMCON)
		dbSelectArea("ADB")
		dbSetOrder(1)
		If MsSeek(xFilial("ADB")+SCK->CK_CONTRAT+SCK->CK_ITEMCON)
			RecLock("ADB")
			ADB->ADB_QTDEMP -= SCK->CK_QTDVEN
			MsUnLock()
		EndIf
	EndIf
Case nEvento == 11
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiza os dados Complementares             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cStatus := IIf(Empty((cAlias)->CJ_CLIENTE) .Or. Empty((cAlias)->CJ_CONDPAG),"D",cStatus)
	(cAlias)->CJ_STATUS := cStatus
	
Case nEvento == 12
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiza os dados Complementares             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Case nEvento == 13
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiza os dados Complementares             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	(cAlias)->CJ_STATUS := cStatus
Case nEvento == 14
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Atualiza os dados Complementares             ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	cStatus := "C"
	(cAlias)->CJ_STATUS := cStatus
	If ( ExistBlock("A415CANC") )
		ExecBlock("A415CANC",.F.,.F.,{cAlias})
	EndIf
EndCase
RestArea(aArea)
Return(.T.)
/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Ma415Auto   ³ Autor ³Eduardo Riera        ³ Data ³19/10/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de avaliacao do cancelamento dos Orcamentos por      ³±±
±±³          ³vencimento de validade.                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpC1 := Ma415Auto()                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³Esta rotina efetua o cancelamento automatico dos orcamento  ³±±
±±³          ³apos o vencimento da validade do mesmo.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ma415Auto()

Local aArea      	:= GetArea()
Local lEnd       	:= .F.
Local lContinua 	:= GetMv("MV_ORCVLD") .And. __cInterNet <> "AUTOMATICO"
If ( lContinua )
	If ( GetMv("MV_ORCUVLD") >= dDataBase )
		lContinua := .F.
	EndIf
	GetMv("MV_ORCVLD")
	dbSelectArea("SX6")
	lContinua := MsRLock()
	If ( lContinua )
		Processa({|lEnd| Ma415PAuto(@lEnd)},STR0080,OemToAnsi(STR0081),.F.)		//"Cancelamento de Orcamentos vencidos"###"Cancelando orcamentos..."
		dbSelectArea("SX6")
		GetMv("MV_ORCVLD")
		MsRUnLock()
	EndIf
EndIf
RestArea(aArea)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³Ma415PAuto  ³ Autor ³Eduardo Riera        ³ Data ³19/10/2000³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de avaliacao do cancelamento dos Orcamentos por      ³±±
±±³          ³vencimento de validade (Processamento)                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ ExpC1 := Ma415Auto()                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³Esta rotina efetua o cancelamento automatico dos orcamento  ³±±
±±³          ³apos o vencimento da validade do mesmo.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Ma415PAuto(lEnd)

Local aArea     := GetArea()
Local aAreaSCJ  := SCJ->(GetArea())
Local aAreaSCK  := SCK->(GetArea())
Local aStruSCK
Local aStruSCJ  := SCJ->(dbStruct())
Local cAlias    := "SCJ"
Local cAliasSCK := ""
Local cQuery    := ""
Local nX        := 0
Local cFilSCJ		:= xFilial("SCJ")
Local cFilSCK		:= xFilial("SCK")

ProcRegua(LastRec())

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Atualiza a data de ultima checagem                      ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
GetMv("MV_ORCUVLD")
dbSelectArea("SX6")
If ( Found() )
	PutMv("MV_ORCUVLD",Dtos(dDataBase))
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Pesquisa os Orcamentos em aberto                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

cAlias := "MA415PAUTO"
cQuery := "SELECT CJ_FILIAL,CJ_NUM,CJ_STATUS,CJ_VALIDA,R_E_C_N_O_ SCJRECNO "
cQuery += "FROM " + RetSqlName("SCJ") + " SCJ "
cQuery += "WHERE SCJ.CJ_FILIAL='" + cFilSCJ + "' AND "
cQuery += "SCJ.CJ_VALIDA<'" + Dtos(dDataBase) + "' AND "
cQuery += "SCJ.CJ_STATUS NOT IN('B','C') AND "
cQuery += "SCJ.D_E_L_E_T_=' '"
cQuery := ChangeQuery(cQuery)
dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAlias,.T.,.T.)
For nX := 1 To Len(aStruSCJ)
	If ( aStruSCJ[nX][2] <> "C" )
		TcSetField(cAlias,aStruSCJ[nX][1],aStruSCJ[nX][2],aStruSCJ[nX][3],aStruSCJ[nX][4])
	EndIf
Next nX

While ( !Eof() )
	SCJ->(MsGoto((cAlias)->SCJRECNO))
		
	Begin Transaction
		dbSelectArea("SCK")
		dbSetOrder(1)
		DEFAULT aStruSCK  := SCK->(dbStruct())
			
		cAliasSCK := "MA415PAUTO2"
		cQuery := "SELECT * "
		cQuery += "FROM " + RetSqlName("SCK") + " SCK "
		cQuery += "WHERE SCK.CK_FILIAL='" + cFilSCK + "' AND "
		cQuery += "SCK.CK_NUM='" + SCJ->CJ_NUM + "' AND "
		cQuery += "SCK.D_E_L_E_T_=' ' "
		cQuery += "ORDER BY "+SqlOrder(SCK->(IndexKey()))
		cQuery := ChangeQuery(cQuery)
		dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasSCK,.T.,.T.)
		For nX := 1 To Len(aStruSCK)
			If ( aStruSCK[nX][2] <> "C" )
				TcSetField(cAlias,aStruSCK[nX][1],aStruSCK[nX][2],aStruSCK[nX][3],aStruSCK[nX][4])
			EndIf
		Next nX
			
		While ( !Eof() )
			MaAvalOrc(cAliasSCK,4)
			dbSelectArea(cAliasSCK)
			dbSkip()
		EndDo
			
		dbSelectArea(cAliasSCK)
		dbCloseArea()
		dbSelectArea("SCK")
			
		RecLock("SCJ",.F.)
		MaAvalOrc("SCJ",14)
		MsUnLock()
	End Transaction

	dbSelectArea(cAlias)
	dbSkip()
	If lEnd
		Exit
	EndIf
EndDo

dbSelectArea(cAlias)
dbCloseArea()
dbSelectArea("SCJ")

RestArea(aAreaSCJ)
RestArea(aAreaSCK)
RestArea(aArea)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³A415VldTOk  ³ Autor ³Eduardo Riera        ³ Data ³01/06/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de validacao da tudoOk da Enchoice                   ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: Dados validos?                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³Esta rotina efetua a validacao da TudoOk da Enchoice        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415VldTOk(lBloqueia)

Local lRet      	:= .T.
Local aProdDesc 	:= {}
Local aAreaTMP  	:= {}
Local lGrvBloq		:= .F.
Local lAvalMargem	:= .F.
Local aRentab		:= {}

Default lBloqueia	:= .F.

If ( M->CJ_MOEDA == 1 .And. M->CJ_TXMOEDA <> 1 ) 
	Help("",1,"A415TXMOED",,STR0095,1,,,,,,,{STR0096} )	//"Taxa da moeda inválida."##"A taxa da moeda só poderá ser alterada para moedas diferente de 1."  
	lRet := .F. 
EndIf 

If lRet 
	
	aAreaTMP  	:= TMP1->( GetArea() )
	lGrvBloq	:= SuperGetMV("MV_FATBLOR",,.T.)		// "Permite gravacao mesmo com bloqueio de regra"
	lAvalMargem	:= SuperGetMV("MV_BLQMAR",,.F.)		// "Habilita análise de margem mínima para bloqueio por regra"

	TMP1->( DBGoTop() )
	While TMP1->( !Eof() )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Inclui os produtos para validacao                      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

		nDescon  := 0

		cProduto := TMP1->CK_PRODUTO
		cItem    := TMP1->CK_ITEM
		nDescon  := (100 - (TMP1->CK_PRCVEN / TMP1->CK_PRUNIT) * 100 )

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Inclui apenas se nao existir                           ³
		//³       Estrutura do array aProdDesc                                          ³
		//³       [1] - Codigo do Produto                                               ³
		//³       [2] - Item do Pedido de Venda                                         ³
		//³       [3] - Preco de Venda                                                  ³
		//³       [4] - Preco de Lista                                                  ³
		//³       [5] - % do Desconto Concedido no item do pedido                       ³
		//³       [6] - % do Desconto Permitido pela regra (FtRegraNeg)                 ³
		//³       [7] - Indica se sera necessario verificar o saldo de verba            ³
		//³                             01 - Bloqueio de regra de negocio               ³
		//³                             02 - Bloqueio para verificacao de verba         ³
		//³       [8] - Valor a ser abatido da verba caso seja aprovada (FtVerbaVen)    ³
		//³       [9] - Flag que indica se o item sera analisado nas regras             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aAdd( aProdDesc, {cProduto,cItem,TMP1->CK_PRCVEN,TMP1->CK_PRUNIT,nDescon,0,"",0,.T.} )

		TMP1->(dbSkip())

	Enddo

	RestArea( aAreaTMP )

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica Regra e nao analisa verba (ultimo parametro .F.)    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lRet := FtRegraNeg(M->CJ_CLIENTE,M->CJ_LOJA,M->CJ_TABELA,M->CJ_CONDPAG,,@aProdDesc, !lGrvBloq, .F. )

	lBloqueia := !lRet
	//-------------------------------------------------------------------
	//Se nao bloquear por regra avalia a margem de contribuicao
	//-------------------------------------------------------------------
	If (lAvalMargem) .AND. (!lBloqueia)
		aRentab := Mat415Rent(M->CJ_NUM)
		lBloqueia := FtRegMar(M->CJ_CLIENTE	, M->CJ_LOJA, aRentab)
	EndIf

	If !lRet .AND. lGrvBloq
		lRet := .T.
	EndIf

EndIf

Return( lRet )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³A415Bonus ³ Autor ³Eduardo Riera          ³ Data ³15.05.2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de tratamento da regra de bonificacao para interface ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpN1: Tipo de operacao                                     ³±±
±±³          ³       [1] Inclusao do bonus                                ³±±
±±³          ³       [2] Exclusao do bonus                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo avaliar a regra de bonificacao³±±
±±³          ³e adicionar na respectiva interface                         ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais/Distribuicao/Logistica                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function A415Bonus(nTipo)

Local aArea     := GetArea()
Local aBonus    := {}
Local nX        := 0
Local nY        := 0
Local nZ        := 0
Local nW		:= 0
Local cItem     := ""
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica os bonus                                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If nTipo == 1
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica os bonus por item de venda                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aBonus   := FtRgrBonus("TMP1",{"CK_PRODUTO","CK_QTDVEN","CK_TES"},M->CJ_CLIENTE,M->CJ_LOJA,M->CJ_TABELA,M->CJ_CONDPAG)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Recupera os bonus ja existentes                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aBonus   := FtRecBonus("TMP1",{"CK_PRODUTO","CK_QTDVEN","CK_TES","CK_FLAG"},aBonus)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Grava os novos bonus                                    ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	nY := Len(aBonus)
	If nY > 0
		nZ := TMP1->(RecNo())
		TMP1->(dbGoBottom())
		cItem := TMP1->CK_ITEM
		TMP1->(dbGoto(nZ))
		For nX := 1 To nY
			cItem := Soma1(cItem)
			RecLock("TMP1",.T.)
			For nW := 1 To Len(aHeader)
				If !( AllTrim(aHeader[nW,2]) $ "CK_ALI_WT/CK_REC_WT")
					FieldPut(nW,CriaVar(aHeader[nW,2],.T.))
				EndIf
			Next nW
			TMP1->CK_ITEM    := cItem
			TMP1->CK_PRODUTO := aBonus[nX][1]
			A415Prod(TMP1->CK_PRODUTO)
			TMP1->CK_QTDVEN  := aBonus[nX][2]
			TMP1->CK_TES     := aBonus[nX][3]
			If ( TMP1->CK_PRCVEN == 0 )
				TMP1->CK_PRCVEN := 1
				TMP1->CK_VALOR  := TMP1->CK_QTDVEN
			Else
				TMP1->CK_VALOR := A410Arred(TMP1->CK_QTDVEN*TMP1->CK_PRCVEN,"CK_VALOR")
			EndIf
			MsUnLock()
		Next nX
	EndIf
Else
	FtDelBonus("TMP1",{"CK_PRODUTO","CK_QTDVEN","CK_TES","CK_FLAG"})
EndIf
RestArea(aArea)
Return(.T.)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³MA415VldFl  ³ Autor ³Eduardo Riera        ³ Data ³01/06/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina de validacao das filiais de entrega/Venda            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ ExpL1: Dados validos?                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Observacao³Esta rotina efetua a validacao das filiais de entrega/Venda ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATA415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Ma415VldFl()

If !(FWModeAccess("SCJ",3)=="C" .AND. FWModeAccess("SF4",3)=="C" .AND. FWModeAccess("SB1",3)=="C" .AND.;
			FWModeAccess("SE4",3)=="C" .AND. FWModeAccess("DA0",3)=="C" .AND. FWModeAccess("SBM",3)=="C")
	Aviso(STR0075,STR0093,{STR0092}) //"Para utilizar o processo de venda compartilhada ajuste o compartilhamento entre filiais das tabelas: SCJ, SCK, SCL, SF4, SB1, SE4, SBM, SBH, DA0 e DA1."
	&(Readvar()):= cFilAnt //Retorna conteúdo inicial padrão do campo
EndIf

Return(.T.)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma415Track³ Autor ³ Sergio Silveira       ³ Data ³18/12/2001³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Faz o tratamento da chamada do System Tracker              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ .T.                                                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Static Function A415Track()

Local aEnt     := {}
Local cOrc     := M->CJ_NUM
Local nRecTMP1 := TMP1->( Recno() )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Percorre os itens do orcamento              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

TMP1->( dbGotop() )

While !TMP1->( Eof() )
	aAdd( aEnt, { "SCK", cOrc + TMP1->CK_ITEM } )
	TMP1->( dbSkip() )
EndDo

MaTrkShow( aEnt )

TMP1->( dbGoto( nRecTMP1 ) )

Return( .T. )

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A415Legend³Autor  ³ Sergio Silveira       ³ Data ³26/06/2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Demonstra a legenda das cores da mbrowse                     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina monta uma dialog com a descricao das cores da    ³±±
±±³          ³Mbrowse.                                                     ³±±
±±³          ³                                                             ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A415Legend()

Local aCores := {	{ 'ENABLE'    , STR0052 },; //'Orcamento em Aberto'
					{ 'DISABLE'   , STR0053 },; //'Orcamento Baixado'
					{ 'BR_PRETO'  , STR0054 },; //'Orcamento Cancelado'
					{ 'BR_AMARELO', STR0055 },; //'Orcamento nao Orcado'
					{ 'BR_MARROM' , STR0078 }}  //'Orcamento bloqueado'

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de Entrada para alterar cores da legenda    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ExistBlock("MA415LEG")
	aCores := ExecBlock("MA415LEG",.F.,.F.,aCores)
Endif

BrwLegenda(cCadastro,STR0057,aCores )   //Pedido Liberado

Return(.T.)

/*/
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³MyMata415 ³ Autor ³ Eduardo Riera         ³ Data ³10.10.2003 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³Rotina de teste da rotina automatica do programa MATA415     ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                       ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Esta rotina tem como objetivo efetuar testes na rotina de    ³±±
±±³          ³orcamento de venda                                           ³±±
±±³          ³                                                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Materiais                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
/*
Function MyMata415()

Local aCabec := {}
Local aItens := {}
Local aLinha := {}
Local nX     := 0
Local nY     := 0
Local cDoc   := ""
Local lOk    := .T.
PRIVATE lMsErroAuto := .F.
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Abertura do ambiente                                         |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ConOut(Repl("-",80))
ConOut(PadC("Teste de Inclusao de 10 orcamentos de venda  com 30 itens cada",80))
PREPARE ENVIRONMENT EMPRESA "99" FILIAL "01" MODULO "FAT" TABLES "SC5","SC6","SA1","SA2","SB1","SB2","SF4","SCJ","SCK","SCL"
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Verificacao do ambiente para teste                           |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SB1")
dbSetOrder(1)
If !SB1->(MsSeek(xFilial("SB1")+"PA001"))
	lOk := .F.
	ConOut("Cadastrar produto: PA001")
EndIf
dbSelectArea("SF4")
dbSetOrder(1)
If !SF4->(MsSeek(xFilial("SF4")+"501"))
	lOk := .F.
	ConOut("Cadastrar TES: 501")
EndIf
dbSelectArea("SE4")
dbSetOrder(1)
If !SE4->(MsSeek(xFilial("SE4")+"001"))
	lOk := .F.
	ConOut("Cadastrar condicao de pagamento: 001")
EndIf
If !SB1->(MsSeek(xFilial("SB1")+"PA002"))
	lOk := .F.
	ConOut("Cadastrar produto: PA002")
EndIf
dbSelectArea("SA1")
dbSetOrder(1)
If !SA1->(MsSeek(xFilial("SA1")+"CL000101"))
	lOk := .F.
	ConOut("Cadastrar cliente: CL000101")
EndIf
If lOk
	ConOut("Inicio: "+Time())
	For nY := 1 To 1
		cDoc := GetSxeNum("SCJ","CJ_NUM")
		RollBAckSx8()
		aCabec := {}
		aItens := {}
		aAdd(aCabec,{"CJ_NUM"   ,cDoc,Nil})
		aAdd(aCabec,{"CJ_CLIENTE",SA1->A1_COD,Nil})
		aAdd(aCabec,{"CJ_LOJACLI",SA1->A1_LOJA,Nil})
		aAdd(aCabec,{"CJ_LOJAENT",SA1->A1_LOJA,Nil})
		aAdd(aCabec,{"CJ_CONDPAG",SE4->E4_CODIGO,Nil})
		For nX := 1 To 1
			aLinha := {}
			aAdd(aLinha,{"CK_ITEM",StrZero(nX,2),Nil})
			aAdd(aLinha,{"CK_PRODUTO",SB1->B1_COD,Nil})
			aAdd(aLinha,{"CK_QTDVEN",1,Nil})
			aAdd(aLinha,{"CK_PRCVEN",100,Nil})
			aAdd(aLinha,{"CK_PRUNIT",100,Nil})
			aAdd(aLinha,{"CK_VALOR",100,Nil})
			aAdd(aLinha,{"CK_TES","501",Nil})
			aAdd(aItens,aLinha)
		Next nX
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//| Teste de Inclusao                                            |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		MATA415(aCabec,aItens,3)
		If !lMsErroAuto
			ConOut("Incluido com sucesso! "+cDoc)
		Else
			ConOut("Erro na inclusao!")
		EndIf
	Next nY
	ConOut("Fim  : "+Time())
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//| Teste de alteracao                                           |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aCabec := {}
	aItens := {}
	aAdd(aCabec,{"CJ_NUM",cDoc,Nil})

	aLinha := {}
	aAdd(aLinha,{"LINPOS","CK_ITEM","01"})
	aAdd(aLinha,{"AUTDELETA","S",Nil})
	aAdd(aItens,aLinha)

	For nX := 2 To 2
		aLinha := {}
		aAdd(aLinha,{"CK_ITEM",StrZero(nX,2),Nil})
		aAdd(aLinha,{"CK_PRODUTO",SB1->B1_COD,Nil})
		aAdd(aLinha,{"CK_QTDVEN",2,Nil})
		aAdd(aLinha,{"CK_PRCVEN",100,Nil})
		aAdd(aLinha,{"CK_PRUNIT",100,Nil})
		aAdd(aLinha,{"CK_VALOR",200,Nil})
		aAdd(aLinha,{"CK_TES","501",Nil})
		aAdd(aItens,aLinha)
	Next nX
	ConOut(PadC("Teste de alteracao",80))
	ConOut("Inicio: "+Time())
	MATA415(aCabec,aItens,4)
	ConOut("Fim  : "+Time())
	ConOut(Repl("-",80))
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//| Teste de Exclusao                                            |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	ConOut(PadC("Teste de exclusao",80))
	ConOut("Inicio: "+Time())
	MATA415(aCabec,aItens,5)
	If !lMsErroAuto
		ConOut("Exclusao com sucesso! "+cDoc)
	Else
		ConOut("Erro na exclusao!")
	EndIf
	ConOut("Fim  : "+Time())
	ConOut(Repl("-",80))
EndIf
RESET ENVIRONMENT
Return(.T.)
*/

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Analise da Rentabilidade - Valor Presente    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Static Function a415RentVP( aRenTab ,aVencto )
Local nItem := 0
Local nY    := 0
Local nX    := 0

If len(aRenTab) > 0 .AND. (aRentab[Len(aRentab)][1] == "")
	aSize( aRentab ,Len(aRentab)-1)
	For nX := 1 To Len(aRentab)
		aRentab[nX][2] := val(StrTran(StrTran(aRentab[nX][2],".",""),",","."))
		aRentab[nX][3] := val(StrTran(StrTran(aRentab[nX][3],".",""),",","."))
		aRentab[nX][4] := 0
		aRentab[nX][5] := 0
		aRentab[nX][6] := 0
	Next nX
EndIf

dbSelectArea("TMP1")
dbGotop()
While ( !Eof() )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica se a linha foi deletada                     ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If ( !TMP1->CK_FLAG .And. !Empty(TMP1->CK_PRODUTO) )
		nItem++
		nY := aScan(aRentab,{|x| x[1] == TMP1->CK_PRODUTO})
		If nY <> 0
			aRentab[nY][4] += Max(Ma410Custo(nItem,aVencto,TMP1->CK_TES,TMP1->CK_PRODUTO,TMP1->CK_LOCAL,TMP1->CK_QTDVEN, M->CJ_EMISSAO),0)
			aRentab[nY][5] := aRentab[nY][4]-aRentab[nY][3] //Max(aRentab[nY][4]-aRentab[nY][3],0)
			aRentab[nY][6] := aRentab[nY][5]/aRentab[nY][4]*100
		EndIf
	EndIf
	dbSelectArea("TMP1")
	dbSkip()
EndDo
aAdd(aRentab,{"",0,0,0,0,0})
For nX := 1 To Len(aRentab)
	If nX <> Len(aRentab)
		aRentab[Len(aRentab)][2] += aRentab[nX][2]
		aRentab[Len(aRentab)][3] += aRentab[nX][3]
		aRentab[Len(aRentab)][4] += aRentab[nX][4]
		aRentab[Len(aRentab)][5] += aRentab[nX][5]
		aRentab[Len(aRentab)][6] := aRentab[Len(aRentab)][5]/aRentab[Len(aRentab)][4]*100
	EndIf
	aRentab[nX][2] := TransForm(aRentab[nX][2],"@e 999,999,999.999999")
	aRentab[nX][3] := TransForm(aRentab[nX][3],"@e 999,999,999.999999")
	aRentab[nX][4] := TransForm(aRentab[nX][4],"@e 999,999,999.999999")
	aRentab[nX][5] := TransForm(aRentab[nX][5],"@e 999,999,999.999999")
	aRentab[nX][6] := TransForm(aRentab[nX][6],"@e 999,999,999.999999")
Next nX
If Existblock("MA415RVP")
	aRentab := ExecBlock("MA415RVP",.F.,.F.,aRentab)
EndIf
Return( .T. )

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³a415PCpy  ³ Autor ³ Eduardo Riera         ³ Data ³09/12/2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Prepara a funcao de copia para evitar que seja chamada a   ³±±
±±³          ³ janela de filiais                                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpC1: Alias do cabecalho do orcamento de venda             ³±±
±±³          ³ExpN2: Recno do cabecalho do orcamento de venda             ³±±
±±³          ³ExpN3: Opcao do arotina                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function a415PCpy(cAlias,nReg,nOpc)

Local aRotBkp := aClone(aRotina)


Private aRotina		:= { {STR0002,"AxPesqui"  , 0 , 1 },; 	//"Pesquisar"
	{STR0003,"A415Visual", 0 , 2 },; 						//"Visualizar"
	{STR0004,"A415Copia", 0 , 3  }}							//"Incluir"

a415Copia(cAlias,nReg,3)

aRotina := aClone(aRotBkp)

Return(.T.)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A415VldPrj³ Autor ³ Reynaldo Miyashita    ³ Data ³ 03.02.2006±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Validacao do codigo da tarefa digitada.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³MATA415                                                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A415VldPrj()
Local lRet	:= .F.
Local aArea		:= GetArea()
Local aAreaAF8	:= AF8->(GetArea())
Local aAreaAFC	:= AFC->(GetArea())
Local aAreaAF9	:= AF9->(GetArea())
Local cContVar	:=	&(ReadVar())

AF8->(dbSetOrder(1))
If !Empty(TMP1->CK_PROJPMS) .And. AF8->(dbSeek(xFilial("AF8")+TMP1->CK_PROJPMS))
	If AllTrim(ReadVar())=="M->CK_TASKPMS"
		AF9->(dbSetOrder(1))
		If AF9->(dbSeek(xFilial("AF9")+AF8->AF8_PROJET+AF8->AF8_REVISA+cContVar))
			// tarefa pode ser faturada
			If AF9->(AF9_FATURA) =="1"
				lRet := .T.
				TMP1->CK_EDTPMS := SPACE(LEN(AFC->AFC_EDT))
			Else
				HELP("   ",1,"VLDTSKRFAT")
			EndIf
		Else
			HELP("   ",1,"REGNOIS")
		EndIf
	Else
		AFC->(dbSetOrder(1))
		If AFC->(dbSeek(xFilial("AFC")+AF8->AF8_PROJET+AF8->AF8_REVISA+cContVar))
			// EDT pode ser faturada
			If AFC->(AFC_FATURA) =="1"
				lRet := .T.
				TMP1->CK_TASKPMS := SPACE(LEN(AF9->AF9_TAREFA))
			Else
				HELP("   ",1,"VLDEDTFAT")
			EndIf
		Else
			HELP("   ",1,"REGNOIS")
		EndIf
	EndIf
Else
	HELP("   ",1,"REGNOIS")
EndIf

RestArea(aAreaAF9)
RestArea(aAreaAFC)
RestArea(aAreaAF8)
RestArea(aArea)
Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³a415VldPMS³ Autor ³ Reynaldo Miyashita    ³ Data ³03.06.2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Rotina para validar as colunas do browse de itens no pedido ³±±
±±³          ³ de orcamebto. Colunas de Projeto, EDT e Tarefa referentes  ³±±
±±³          ³ ao sigapms                                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ lOk - Se as colunas do browse estao certas                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function a415VldPMS( cCK_PROJPMS ,cCK_EDTPMS ,cCK_TASKPMS )
Local lOk := .F.
Local aArea := GetArea()

// se foi informado o codigo do projeto
If ! Empty(cCK_PROJPMS)
    // verifica se projeto existe
    AF8->(dbSetOrder(1))
	If AF8->(dbSeek(xFilial("AF8")+cCK_PROJPMS ))
		// se foi informado EDT e Tarefa
		If ( !Empty(cCK_EDTPMS) .AND. !Empty(cCK_TASKPMS) )
			HELP("   ",1,"VLDPMSFAT",,STR0067 + CRLF + STR0068 ,1)
		Else
			// EDT preenchida
			If ! Empty(cCK_EDTPMS)
			    // verifica se a EDT existe
		        AFC->(dbSetOrder(1))
				If AFC->(dbSeek(xFilial("AFC")+AF8->AF8_PROJET+AF8->AF8_REVISA+cCK_EDTPMS ))
					// a EDT é faturavel
					If AFC->AFC_FATURA == "1"
						lOk := .T.
					Else
						HELP("   ",1,"VLDEDTFAT")
					EndIf
				Else
					HELP("   ",1,"VLDEDTEXIST",,STR0069 ,1)
				EndIf
			Else
				// Tarefa preenchida
				If ! Empty(cCK_TASKPMS)
				    // verifica se a TAREFA existe
			        AF9->(dbSetOrder(1))
					If AF9->(dbSeek(xFilial("AF9")+AF8->AF8_PROJET+AF8->AF8_REVISA+cCK_TASKPMS ))
						// a TAREFA é faturavel
						If AF9->AF9_FATURA == "1"
							lOk := .T.
						Else
							HELP("   ",1,"VLDTSKRFAT")
						EndIf
					Else
						HELP("   ",1,"VLDTSKEXIST",,STR0070 ,1)
					EndIf

				// EDT e Tarefa naum foram preenchidas
				Else
					lOk := .T.
				EndIf
			EndIf
		EndIf
	Else
		HELP("   ",1,"VLDPRJEXIST",,STR0071 ,1)
	EndIf
Else
	lOk := .T.
EndIf

RestArea( aArea )

Return( lOk )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³MenuDef   ³ Autor ³ Marco Bianchi         ³ Data ³01/09/2006³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Utilizacao de menu Funcional                               ³±±
±±³          ³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Array com opcoes da rotina.                                 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Parametros do array a Rotina:                               ³±±
±±³          ³1. Nome a aparecer no cabecalho                             ³±±
±±³          ³2. Nome da Rotina associada                                 ³±±
±±³          ³3. Reservado                                                ³±±
±±³          ³4. Tipo de Transa‡„o a ser efetuada:                        ³±±
±±³          ³	  1 - Pesquisa e Posiciona em um Banco de Dados           ³±±
±±³          ³    2 - Simplesmente Mostra os Campos                       ³±±
±±³          ³    3 - Inclui registros no Bancos de Dados                 ³±±
±±³          ³    4 - Altera o registro corrente                          ³±±
±±³          ³    5 - Remove o registro corrente do Banco de Dados        ³±±
±±³          ³5. Nivel de acesso                                          ³±±
±±³          ³6. Habilita Menu Funcional                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/

Static Function MenuDef()

Private aRotina := {	{STR0002,"AxPesqui"  	, 0 , 1, 0,.F.},; 	//"Pesquisar"
							{STR0003,"A415Visual"	, 0 , 2, 0,NIL},; 	//"Visualizar"
							{STR0004,"A415Inclui"	, 0 , 3, 0,NIL},; 	//"Incluir"
							{STR0005,"A415Altera"	, 0 , 4, 0,NIL},; 	//"Alterar"
							{STR0038,"A415Exclui"	, 0 , 5, 0,NIL},;		//"Exclui"
							{STR0039,"A415Cancel" 	, 0 , 2, 0,NIL},;		//"Cancela"
							{STR0006,"A415Impri" 	, 0 , 2, 0,NIL},; 	//"impRimir"
							{STR0007,"A415PCpy"  	, 0 , 4, 0,NIL},;		//"Copiar"
							{STR0057,"A415Legend"	, 0 , 2, 0,.F.},;		//"Legenda"
							{STR0076,"MsDocument"	, 0 , 4, 0 ,NIL} }		//"Conhecimento"

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica motivo de bloqueio por regra/verba                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If	SuperGetMv("MV_VEBLQRG", .F., .F.)
	aAdd(aRotina,{STR0084,"OrcBlqRegB", 0 , 0 , 0 , .F.})		// "Blq. Regra"
EndIf


If ExistBlock("MA415MNU")
	ExecBlock("MA415MNU",.F.,.F.)
EndIf

Return(aRotina)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³Ma415Opc  ³ Autor ³ Kleber Dias Gomes     ³ Data ³09/01/2007³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Funcao de retorno do codigo do opcional.                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpL1 = Repete o mesmo grupo de opcional                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ Nenhum                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Ma415Opc(lOpcPadrao)

Local cOpcional   := ""
Local aROpc       := {}
Local nI          := 0

Default lOpcPadrao:= GetNewPar("MV_REPGOPC","N") == "N"

If lOpcPadrao
	cOpcional := TMP1->CK_OPC
Else
	aROpc:=STR2Array(TMP1->CK_MOPC,.F.)
	If !Empty(aROpc)
		For nI := 1 To Len(aROpc)
			cOpcional += aROpc[nI,2]
		Next
	Endif
Endif

Return cOpcional

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³a415Tipo9 ³ Autor ³ Marcelo Alexandre     ³ Data ³ 23.04.07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Validacao da Condicao de Pagamento Tipo 9                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpL1: Logico                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ 16/02/11 ³ Flavio Novaes ³ Consistencia para MV_NUMPARC: FNC 3696.    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function A415TIPO9()

Local aArea     := GetArea()
Local aAreaSE4  := SE4->(GetArea())

Local cParcela  := "123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ0"
Local dParc     := Ctod("")
Local nParc     := 0
Local nAux      := 0
Local nTotal    := 0
Local nValor    := 0
Local nY        := 0
Local nX        := 0
Local nParcelas := SuperGetMv("MV_NUMPARC")
Local nRecBkp	:= 0

Local lRet      := .T.
Local lIPI      := (GetMV("MV_IPITP9") == "S")
Local lMt415Parc:= Existblock("MT415PC")
Local lParc     := .T.

Local cChave    := ""
Local cChave1   := ""
Local aAreaSX3  := {}

Local cHelp := ""

If GetRPORelease() <= "12.1.023"
	 cHelp := "A415TIPO9"
Else
	 cHelp := "A415DtVencParc"
EndIf


If ( ExistBlock("M415TIP9") )
	lRet := ExecBlock("M415TIP9",.F.,.F.)
Else

	nRecBkp	:= TMP1->(RECNO())
	dbSelectArea("TMP1")
	dbGotop()
	While !Eof()

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se a linha foi deletada                     ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ( !TMP1->CK_FLAG .And. !Empty(TMP1->CK_PRODUTO) )
			nValor := TMP1->CK_VALOR
		EndIf
		nTotal += nValor
		nValor := 0
		dbSkip()
	End

	nTotal := nTotal + M->CJ_FRETE + M->CJ_DESPESA + M->CJ_SEGURO + M->CJ_FRETAUT

	TMP1->(DBGOTO(nRecBkp))

	// permite que o numero de parcela possa se manipulado por customização, independente do parametro
	If lMt415Parc
		nParcelas := Execblock("MT415PC",.F.,.F.)
	Endif

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica Relacao entre nParcelas (MV_NUMPARC) e Base de Dados. ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If nParcelas > 4
		cChave := "CJ_DATA"+Subs(cParcela,nParcelas,1)
		cChave1:= "CJ_PARC"+Subs(cParcela,nParcelas,1)
		aAreaSX3 := SX3->(GetArea())
		DbSelectArea("SX3")
		DbSetOrder(2)
		If !DbSeek(cChave) .or. !DbSeek(cChave1)
			Help(" ",1,"TMKTIP905") //"A quantidade de parcelas nao esta compativel. Verificar junto ao administrador do sistema relacao entre parametro MV_NUMPARC e dicionario de dados"
			Return(.F.)
		EndIf
		Restarea(aAreaSX3)
	EndIf

	For nX:=1 to nParcelas
		nParc := &("M->CJ_PARC"+Substr(cParcela,nx,1))
		dParc := &("M->CJ_DATA"+Substr(cParcela,nx,1))
		If nParc > 0 .And. Empty(dParc)
			lParc := .F.
		EndIf
		nAux		+= nParc
	Next nX

	If !lParc
		Help(" ",1,cHelp)
		lRet := .F.
	Else
		dbSelectArea("SE4")
		dbSetOrder(1)
		If MsSeek(xFilial("SE4")+M->CJ_CONDPAG)
			If SE4->E4_TIPO =="9"
				If AllTrim(SE4->E4_COND) = "0"

					If	( lIPI .And. NoRound(nTotal,2) > NoRound(nAux,2) ) .Or. ;
						( !lIPI .And. NoRound(nTotal,2) <> NoRound(nAux,2) )

						Help(" ",1,"A415TIPO9")

						If lRet
							If ( Type("l415Auto") == "U" .or. ! l415Auto )
								OpcQW:=MsgYesNo(OemToAnsi(STR0074),OemToAnsi(STR0075))  //"Confirma a Inclusao do Orcamento ?"###"Aten‡„o"
								If !OpcQW 				// Abandona
									lRet := .F.
								EndIf
							EndIf
						EndIf
					EndIf
				ElseIf AllTrim(SE4->E4_COND) = "%" .And. nAux # 100
					Help(" ",1,"A415TIPO9P")

					If lRet
						If ( Type("l415Auto") == "U" .or. ! l415Auto )
							OpcQW:=MsgYesNo(OemToAnsi(STR0074),OemToAnsi(STR0075))  //"Confirma a Inclusao do Orcamento ?"###"Aten‡„o"
							If !OpcQW 				// Abandona
								lRet := .F.
							EndIf
						EndIf
					EndIf

				EndIf
			EndIf
		EndIf
	EndIf
Endif

RestArea(aAreaSE4)
RestArea(aArea)
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³MT415Pros ºAutor  ³Vendas CRM          º Data ³  07/02/08   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ O prospect somente pode estar preenchido se o cliente for  º±±
±±º          ³ padrao                                                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                         º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MT415Pros()

Local aArea		:= GetArea()
Local aAreaSUS	:= SUS->(GetArea())
Local lRet			:= .T.
Local cCliente	:= SuperGetMv("MV_ORCLIPD")				//Se o cliente for padrao
Local cCodPro		:= ""										//Codigo do prospect
Local cLjPro		:= ""										//Loja do prospect
Local nTmCodCli	:= GetSx3Cache("CJ_CLIENTE","X3_TAMANHO")		//Codigo do cliente
Local nTmLojCli	:= GetSx3Cache("CJ_LOJA","X3_TAMANHO")			//Loja do cliente
Local nTmProspe	:= GetSx3Cache("CJ_PROSPE","X3_TAMANHO")
Local nTmLojPro	:= GetSx3Cache("CJ_LOJPRO","X3_TAMANHO")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Certifica que o indice atual eh US_FILIAL+US_COD+US_LOJA³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SUS->(DbSetOrder(1))

cCodPro	:= Space(nTmProspe)		//Codigo do prospect
cLjPro		:= Space(nTmLojPro)		//Loja do prospect
If "CJ_PROSPE" $ ReadVar()
	lRet := ExistCpo("SUS")
ElseIf "CJ_LOJPRO" $ ReadVar()
	lRet := ExistCpo("SUS", M->CJ_PROSPE + M->CJ_LOJPRO)
ElseIf "CJ_CLIENTE" $ ReadVar() .AND. !Empty( &(ReadVar()) ) .AND. M->CJ_CLIENTE+M->CJ_LOJA <> cCliente
	M->CJ_PROSPE	:= cCodPro
	M->CJ_LOJPRO	:= cLjPro
EndIf

If lRet .AND. "CJ_PROSPE" $ ReadVar() .AND. !Empty( &(ReadVar()) ) .AND. M->CJ_CLIENTE+M->CJ_LOJA <> cCliente
	M->CJ_PROSPE	:= cCodPro
	M->CJ_LOJPRO	:= cLjPro
	lRet			:= .F.
	If !l415Auto
		Alert(	STR0077 + SubStr(cCliente,1,nTmCodCli) + " - " + SubStr(cCliente,nTmCodCli + 1,nTmLojCli) ) //"Para utilização do prospect, favor alterar o código do cliente para o padrão de orçamento: "
	Endif
Endif

RestArea(aAreaSUS)
RestArea(aArea)
Return(lRet)

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³A415FldOk ³  Autor³ Andre Anjos			³ Data ³ 28/01/09 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Valida campos na alteracao e inclusao					  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Mata415                                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A415FldOk()
Local lRet := .T.

If Type("lShowOpc") == "L"
	lShowOpc := .T.
EndIf

Return(lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A415Gr2SCKºAutor  ³Andre Anjos         º Data ³  10/11/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Converte registros em formato de grade em itens prontos a  º±±
±±º          ³ serem gravados na tabela SCK.							  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA415                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A415Gr2SCK()
Local aArea     := GetArea()
Local nX        := 0
Local nLin      := 0
Local nCol	    := 0
Local aCampos   := {}
Local cItemGrd  := "01"
Local lNewLine  := .F.
Local cProdRef  := ""
Local nRatDesc  := 0
Local nRecno    := 0
Local cFilSB1		:= xFilial("SB1")

dbSelectArea("TMP1")
dbGoTop()
While !Eof()
	cProdRef := AllTrim(CK_PRODUTO)
	nRecno := Recno()
	If MatGrdPrrf(@cProdRef)
		aCampos  := {}
		lNewLine := .F.
		cItemGrd := "01"
		For nX := 1 To Len(aHeader)
			aAdd(aCampos,&(aHeader[nX,2]))
		Next nX
		oGrade:nPosLinO := Val(CK_ITEM)

		For nLin := 1 To Len(oGrade:aColsGrade[Val(CK_ITEM)])
			For nCol := 2 To Len(oGrade:aHeadGrade[Val(CK_ITEM)])
				If oGrade:aColsFieldByName("CK_QTDVEN",Val(CK_ITEM),nLin,nCol) <> 0
					RecLock("TMP1",lNewLine)
					//Preenche todos os campos como copia
					If lNewLine
						For nX := 1 to Len(aHeader)
							FieldPut(FieldPos(aHeader[nX,2]),aCampos[nX])
						Next nX
					Else
						nRatDesc := CK_VALDESC / CK_QTDVEN
						lNewLine := .T.
					EndIf

					//Preenche campos especificos
					Replace CK_ITEMGRD With cItemGrd
					Replace CK_PRODUTO With oGrade:GetNameProd(cProdRef,nLin,nCol)
					Replace CK_DESCRI  With Posicione("SB1", 1, cFilSB1 + CK_PRODUTO, "B1_DESC")
					Replace CK_QTDVEN  With oGrade:aColsFieldByName("CK_QTDVEN",Val(CK_ITEM),nLin,nCol)
					Replace CK_VALOR   With A410Arred(CK_QTDVEN * CK_PRCVEN,"CK_VALOR")
					Replace CK_VALDESC With A410Arred(nRatDesc * CK_QTDVEN,"CK_VALDESC")

					MsUnLock()
					cItemGrd := Soma1(cItemGrd)
				EndIf
			Next nCol
		Next nLin
	EndIf
	dbGoTo(nRecno)
	dbSkip()
End

RestArea(aArea)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A415SCK2GrºAutor  ³Andre Anjos         º Data ³  10/11/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Converte itens da tabela SCK (reprentada pela TMP1, gerada º±±
±±º          ³ pela MsGetDB) em formato de grade para edicao do usuario.  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA415                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function A415SCK2Gr()
Local aArea    	  := GetArea()
Local nQtdTot  	  := 0
Local nValTot  	  := 0
Local nDesTot  	  := 0
Local cProdRef 	  := ""
Local cRefAnt  	  := ""
Local nRecno1  	  := 0
Local nRecno2  	  := 0
Local nLin	   	  := 0
Local nCol	   	  := 0
Local lReferencia := .F.
Local nTmCKItem	:= GetSX3Cache("CK_ITEM","X3_TAMANHO")

dbSelectArea("TMP1")
dbGoTop()
While !Eof()
	cProdRef := CK_PRODUTO
	lReferencia := !Empty(CK_ITEMGRD) .And. MatGrdPrrf(@cProdRef,.T.)
	//Monta objeto da grade
	oGrade:MontaGrade(Val(RetAsc(CK_ITEM, nTmCKItem, .F.)),If(lReferencia,cProdRef,CK_PRODUTO),,,lReferencia)
	If lReferencia
		nLin := oGrade:RetPosLin(Val(CK_ITEM),Substr(CK_PRODUTO,oGrade:TamRef()+1,oGrade:TamLin()))
		nCol := oGrade:RetPosCol(Val(CK_ITEM),Substr(CK_PRODUTO,oGrade:TamRef()+oGrade:TamLin()+1,oGrade:TamCol()))+1

		oGrade:aColsGrade[Val(CK_ITEM)][nLin][nCol][oGrade:GetFieldGrdPos("CK_QTDVEN")] := CK_QTDVEN
		If cRefAnt # cProdRef+"*"+CK_ITEM
			If !Empty(nRecno1)
				nRecno2 := Recno()
				dbGoTo(nRecno1)
				RecLock("TMP1",.F.)
				Replace CK_QTDVEN  With nQtdTot
				Replace CK_VALOR   With nValTot
				Replace CK_VALDESC With nDesTot
				MsUnLock()
				dbGoTo(nRecno2)
			EndIf
			cRefAnt := cProdRef+"*"+CK_ITEM
			nQtdTot := CK_QTDVEN
			nValTot := CK_VALOR
			nDesTot := CK_VALDESC

			Replace CK_PRODUTO With cProdRef
			Replace CK_DESCRI  With MaGetDescGrd(cProdRef)

			nRecno1 := Recno()
		Else
			nQtdTot += CK_QTDVEN
			nValTot += CK_VALOR
			nDesTot += CK_VALDESC

			RecLock("TMP1",.F.)
			dbDelete()
			MsUnLock()
		EndIf
	ElseIf !Empty(nRecno1)
		nRecno2 := Recno()
		dbGoTo(nRecno1)
		RecLock("TMP1",.F.)
		Replace CK_QTDVEN  With nQtdTot
		Replace CK_VALOR   With nValTot
		Replace CK_VALDESC With nDesTot
		MsUnLock()
		dbGoTo(nRecno2)
	EndIf
	dbSkip()
End

If !Empty(nRecno1)
	dbGoTo(nRecno1)
	RecLock("TMP1",.F.)
	Replace CK_QTDVEN  With nQtdTot
	Replace CK_VALOR   With nValTot
	Replace CK_VALDESC With nDesTot
	MsUnLock()
EndIf

RestArea(aArea)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A415VlCpGrºAutor  ³Andre Anjos 		 º Data ³  10/11/09   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Funcao de validacao dos campos da grade.					  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA415                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A415VlCpGr()
Local aArea     := GetArea()
Local lRet      := .T.
Local nColuna   := aScan(aHeader,{|x| AllTrim(x[2]) == AllTrim(Substr(Readvar(),4))})
Local cProdGrd  := oGrade:GetNameProd(NIL,n,nColuna)
Local aContrato := {}

If oGrade:cCpo == "CK_QTDVEN"
	If (lRet := Positivo())
		lRet := SeleOpc(3,"MATA415",cProdGrd,.T.,,,"M->CK_PRODUTO",,&(ReadVar()),TMP1->CK_ENTREG)
		If !Empty(TMP1->CK_OPC)
			oGrade:aColsGrade[oGrade:nPosLinO][n][nColuna][oGrade:GetFieldGrdPos("CK_OPC")] := TMP1->CK_OPC
			TMP1->CK_OPC := Space(Len(CK_OPC))
		EndIf
	EndIf
EndIf


RestArea(aArea)
Return lRet

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³A415CliLojºAutor  ³ Vendas CRM		 º Data ³  30/08/10   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Funcao de validacao dos campos do cliente e loja.		  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA415                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function A415CliLoj()

Local aArea		:= GetArea()
Local cFilSA1	:= xFilial("SA1")
Local cVarPosic := ReadVar()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Preenche a condição de pagamento. ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SA1")
dbSetOrder(1)

Do Case

	Case (cVarPosic == "M->CJ_CLIENTE")
	
		If !Empty(M->CJ_LOJA)
			If dbSeek(cFilSA1 + M->CJ_CLIENTE + M->CJ_LOJA,.T.)
				M->CJ_LOJA	  := SA1->A1_LOJA
				M->CJ_CONDPAG := SA1->A1_COND
				If SCJ->(ColumnPos("CJ_TIPOCLI")) > 0
					M->CJ_TIPOCLI := SA1->A1_TIPO
				Endif
			ElseIf dbSeek(cFilSA1 + M->CJ_CLIENTE,.T.)
		 		M->CJ_LOJA 	  := SA1->A1_LOJA
				M->CJ_CONDPAG := SA1->A1_COND
				If SCJ->(ColumnPos("CJ_TIPOCLI")) > 0
					M->CJ_TIPOCLI := SA1->A1_TIPO
				Endif
			EndIf
		ElseIf dbSeek(cFilSA1 + M->CJ_CLIENTE,.T.)
			M->CJ_LOJA	  := SA1->A1_LOJA
			M->CJ_CONDPAG := SA1->A1_COND
			If SCJ->(ColumnPos("CJ_TIPOCLI")) > 0
					M->CJ_TIPOCLI := SA1->A1_TIPO
			Endif
		EndIf
		
	Case (cVarPosic == "M->CJ_CLIENT")
	
		If !Empty(M->CJ_LOJAENT)
			// Nao encontra registro correspondente ao cliente e loja de entrega no cadastro de clientes, mas encontra um correspondente somente ao codigo do cliente de entrega
			If !SA1->(DbSeek(cFilSA1 + M->CJ_CLIENT + M->CJ_LOJAENT,.T.)) .AND. SA1->(DbSeek(cFilSA1 + M->CJ_CLIENT,.T.))
			 	M->CJ_LOJAENT := SA1->A1_LOJA
			EndIf
		EndIf
		
	Case (cVarPosic == "M->CJ_LOJA")
	
		dbSeek(cFilSA1 + M->CJ_CLIENTE + M->CJ_LOJA,.T.)
		M->CJ_CONDPAG := SA1->A1_COND
		If SCJ->(ColumnPos("CJ_TIPOCLI")) > 0
			M->CJ_TIPOCLI := SA1->A1_TIPO
		Endif
		M->CJ_TABELA  := A415TabCli()

End Case

RestArea(aArea)

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³OrcBlqRegBºAutor  ³ Vendas e CRM       º Data ³  27/12/11   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³Avalia e mostra motivo de bloqueio por regra a partir       º±±
±±º          ³de um Browser da SCJ (orcamento)                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³                                                            º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/

Function OrcBlqRegB(cAliasSCJ, nReg, nOpcx, cAliasSCK)

Local aArea 		:= GetArea()
Local aAreaSCK
Local aProdDesc 	:= {}  			//array com os itens do orcamento para avaliar regra de negocio
Local cFilSCK		:= ""

Default cAliasSCJ 	:= "SCJ"
Default cAliasSCK 	:= "SCK"

aAreaSCK	:= (cAliasSCK)->(GetArea())
DbSelectArea(cAliasSCK)
If cAliasSCK == "SCK" //se for a tabela temporaria (TMP1) no orcamento, nao precisa ordenar
	DbSetOrder(1)
EndIf
cFilSCK	:= xFilial(cAliasSCK)

//busca os itens do orcamento selecionado no Browser
If (cAliasSCK <> "SCK") .OR. (MsSeek(cFilSCK + (cAliasSCJ)->CJ_NUM))

	//Monta um array com os itens do orcamento para passar pra funcao que avalia as regras de negocio
	//Se utilizar a tabela temporaria (quando chama durante a edicao), nao pesquisa o item (todos os itens sao do pedido)
	While ((!(cAliasSCK)->(EOF())) .AND.(If(cAliasSCK == "SCK", ((cAliasSCK)->CK_FILIAL + (cAliasSCK)->CK_NUM == cFilSCK + (cAliasSCJ)->CJ_NUM), .T.)))
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³       Estrutura do array aProdDesc                                          ³
		//³       [1] - Codigo do Produto                                               ³
		//³       [2] - Item do Pedido                                                  ³
		//³       [3] - Preco de Venda                                                  ³
		//³       [4] - Preco de Lista                                                  ³
		//³       [5] - % do Desconto Concedido no item do pedido                       ³
		//³       [6] - % do Desconto Permitido pela regra (FtRegraNeg)                 ³
		//³       [7] - Indica se sera necessario verificar o saldo de verba            ³
		//³                             01 - Bloqueio de regra de negocio               ³
		//³                             02 - Bloqueio para verificacao de verba         ³
		//³       [8] - Valor a ser abatido da verba caso seja aprovada (FtVerbaVen)    ³
		//³       [9] - Flag que indica se o item sera analisado nas regras             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		aAdd(aProdDesc,{(cAliasSCK)->CK_PRODUTO,;
		                (cAliasSCK)->CK_ITEM,;
		                (cAliasSCK)->CK_PRCVEN,;
		                (cAliasSCK)->CK_PRUNIT,;
		                (cAliasSCK)->CK_DESCONT,;
		                0,;
		                "",;
		                0,;
		                .T.})
		DbSkip()
	EndDo

EndIf

If cAliasSCJ == "M"
	//avalia se existe bloqueio de regra
	FtRegraNeg(M->CJ_CLIENTE, M->CJ_LOJA, M->CJ_TABELA	, M->CJ_CONDPAG,NIL, @aProdDesc, .F.	, nil, .T.)
Else
	//avalia se existe bloqueio de regra
	FtRegraNeg((cAliasSCJ)->CJ_CLIENTE, (cAliasSCJ)->CJ_LOJA, (cAliasSCJ)->CJ_TABELA	, (cAliasSCJ)->CJ_CONDPAG,NIL, @aProdDesc, .F., nil, .T.)
EndIf

SCK->(RestArea(aAreaSCK))
RestArea(aArea)
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³Mat415RentºAutor  ³ Vendas & CRM       º Data ³ 05/10/2012  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Busca os valores de rentabilidade de um determinado pedido º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATA415                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function Mat415Rent(cNumOrc)
Local aArea		:= GetArea()
Local aAreaSCJ	:= SCJ->(GetArea())
Local aAreaSCK	:= SCK->(GetArea())

Local cSeek     := ""
Local aNoFields := {} //{"CK_NUM","CK_QTDEMP","CK_QTDENT","CK_QTDEMP2","CK_QTDENT2","CK_INFAD"}
Local bWhile    := {|| }
Local aRentab := {}

/*
aRentab
[n]    Item do pedido
[n][1] codigo do produto
[n][2] Valor Total (unit * qtde)
[n][3] C.M.V. (custo)
[n][4] Valor Presente
[n][5] Lucro Bruto (Valor presente - CMV)
[n][6] Margem de Contribuicao (%)
*/

Private aHeader := {}
Private aCols := {}

dbSelectArea("SCJ")
dbSetOrder(1)
If DbSeek(xFilial("SCJ") + cNumOrc)
	RegToMemory ( "SCJ" )

	DbSelectArea("SCK")
	dbSetOrder(1)
	cSeek  := xFilial("SCK")+SCJ->CJ_NUM
	bWhile := {|| CK_FILIAL+CK_NUM }

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Montagem do aHeader e aCols                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	FillGetDados(2,"SCK",1,cSeek,bWhile,/*uSeekFor*/,aNoFields,/*aYesFields*/,/*lOnlyYes*/,/*cQuery*/,/*bMontCols*/,.F.)
EndIF

//Pergunte("MTA415",.F.)
Ma415Impos(0, .T. , @aRentab)

SCK->(RestArea(aAreaSCK))
SCJ->(RestArea(aAreaSCJ))
RestArea(aArea)
Return aRentab

//------------------------------------------------------------------------------
/*/{Protheus.doc} A415GrvDPR

Rotina de manipulação das pendência de desenvolvimento no SIGADPR

@sample	A415GrvDPR(aItens)

@param		ExpA1 Item ou Itens a serem manipulados

@return	ExpL  Processamento realizado - Verdadeiro/Falso

@author	Thiago Tavares
@since		07/10/2013
@version	11.90
/*/
//------------------------------------------------------------------------------
Function A415GrvDPR(aItemDPR)

Local nX			:= 0
Local lRet 		:= .T.
Local aArea		:= GetArea()
Local aAreaSB1	:= SB1->(GetArea())
Local aAreaSF4	:= SF4->(GetARea())
Local aAreaDGC	:= DGC->(GetARea())
Local cCodProd	:= ""
Local lAchou		:= .F.
Local nTmFilial	:= GetSX3Cache("DGC_FILIAL","X3_TAMANHO")
Local nTmNRBU		:= GetSX3Cache("DGC_NRBU","X3_TAMANHO")
Local nTmNRSQBU	:= GetSX3Cache("DGC_NRSQBU","X3_TAMANHO")
Local nTmCDACBU	:= GetSX3Cache("DGC_CDACBU","X3_TAMANHO")

// aItemDPR[nX, 1] -> nOpc // 3 = Inclusão 4 = Alteração 5 = Exclusão  
// aItemDPR[nX, 2] -> CK_FILIAL->DGC_FILIAL 
// aItemDPR[nX, 3] -> DGC_NRBU/PD
// aItemDPR[nX, 4] -> DGC_NRSQBU/PD
// aItemDPR[nX, 5] -> DGC_CDACBU/PD 
// aItemDPR[nX, 6] -> DGC_CDACBU/PD Antigo

DbSelectArea("DGC")		// Pendencia de Desenvolvimento
DGC->(DbSetOrder(3))  	// DGC_FILIAL+DGC_NRBU+DGC_NRSQBU+DGC_CDACBU

// Verifica se eh 4 = Alteracao ou 5 = Exclusao e posiciona no item
cCodProd := aItemDPR[5]

If (aItemDPR[1] == MODEL_OPERATION_UPDATE) .And. ((aItemDPR[5] <> aItemDPR[6]) .And. !Empty(aItemDPR[6]))
	cCodProd := aItemDPR[6]
EndIf

lAchou := DGC->(DbSeek(PADR(aItemDPR[2], nTmFilial,	" ") + ;
                       PADR(aItemDPR[3], nTmNRBU,	" ") + ;
                       PADR(aItemDPR[4], nTmNRSQBU,	" ") + ;
                       PADR(cCodProd,    nTmCDACBU,	" ")))

If ( lAchou .And. aItemDPR[nX, 1] == MODEL_OPERATION_INSERT )
	Help(,,"A415GrvDPR",,STR0094,1,0)	//"Já existe uma pendência com esse número de orçamento. Favor, informar outro número."
	lRet	:= .F.
Else
	oModel	:= FWLoadModel('DPRA350')
	oModel:SetOperation(aItemDPR[1])
	oModel:SetPrimaryKey({"DGC_FILIAL+DGC_NRBU+DGC_NRSQBU+DGC_CDACBU"})
	oModel:Activate()

	// Inclusao
	If aItemDPR[1] == MODEL_OPERATION_INSERT
		oModel:SetValue("DGCMASTER", "DGC_FILIAL", aItemDPR[2])
		oModel:SetValue("DGCMASTER", "DGC_NRBU",   aItemDPR[3])
		oModel:SetValue("DGCMASTER", "DGC_NRSQBU", aItemDPR[4])
		oModel:SetValue("DGCMASTER", "DGC_CDACBU", aItemDPR[5])
		oModel:SetValue("DGCMASTER", "DGC_LGFTEV", "0")
	ElseIf aItemDPR[1] == MODEL_OPERATION_UPDATE
		oModel:SetValue("DGCMASTER", "DGC_NRBU",   aItemDPR[3])
		oModel:SetValue("DGCMASTER", "DGC_NRSQBU", aItemDPR[4])
		oModel:SetValue("DGCMASTER", "DGC_CDACBU", aItemDPR[5])
	EndIf

	If oModel:VldData()
		oModel:CommitData()
	Else
		aErro	:= oModel:GetErrorMessage()
		DPRXError("DPRA350", aErro[6])
		lRet	:= .F.
	Endif
EndIf

RestArea(aAreaDGC)
RestArea(aAreaSB1)
RestArea(aAreaSF4)
RestArea(aArea)
Return(lRet)

//------------------------------------------------------------------------------
/*/{Protheus.doc} A415VlMoe

Funcao para validar a existencia da moeda informada no orcamento

@return	ExpL  Moeda informado existe

@author	Reynaldo
@since		11/11/2014
@version	11.80
/*/
//------------------------------------------------------------------------------
Function A415VlMoe()
Local lRet := .f.

If M->CJ_MOEDA <= MoedFin()
	A415TabAlt()
	M->CJ_TXMOEDA := RecMoeda(M->CJ_EMISSAO,M->CJ_MOEDA)
	lRet := .T.
EndIf
Return lRet

//------------------------------------------------------------------------------
/*/
{Protheus.doc} 

Funcao para validar os Contratos de Parceria

@return	ExpL  Lógico lRet

@author		Vendas/CRM
@since		01/08/2017
@version	12
/*/
//------------------------------------------------------------------------------
Static Function A415CtrParc(cNumOrc,nQtdVen)

Local nX			:= 0
Local aAreaTMP1	:= {}
Local aContrato	:= {}
Local cQuery		:= ""
Local cAliasTMP	:= GetNextAlias()
Local lRetorno	:= .T.

DEFAULT cNumOrc	:= ""
DEFAULT nQtdVen	:= 0

dbSelectArea("TMP1")
aAreaTMP1	:= GetArea()
TMP1->(dbGoTop())
While TMP1->(!Eof())
	If !TMP1->CK_FLAG .And. TMP1->(RecNo()) <> aAreaTMP1[3] .And. !Empty(TMP1->CK_CONTRAT)
		nX	:= aScan(aContrato,{|x| x[1] == TMP1->CK_CONTRAT .And. x[2] == TMP1->CK_ITEMCON})
		If nX == 0
			aAdd(aContrato,{TMP1->CK_CONTRAT,TMP1->CK_ITEMCON,TMP1->CK_QTDVEN})
			nX	:= Len(aContrato)
		Else
			aContrato[nX][3] += TMP1->CK_QTDVEN
		EndIf
	EndIf
	TMP1->(dbSkip())
EndDo

SCK->(dbSetOrder(1))
cQuery := "SELECT SCK.CK_FILIAL, SCK.CK_NUM, SCK.CK_ITEM, SCK.CK_PRODUTO, "
cQuery += "SCK.CK_CONTRAT, SCK.CK_ITEMCON, SCK.CK_QTDVEN "
cQuery += "FROM " + RetSqlName("SCK") + " SCK "
cQuery += "WHERE "
cQuery += "SCK.CK_FILIAL = '" + xFilial("SCK") + "' AND "
cQuery += "SCK.CK_NUM = '" + cNumOrc + "' AND "
cQuery += "SCK.CK_CONTRAT <> ' ' AND "
cQuery += "SCK.D_E_L_E_T_ = ' ' "
cQuery += "ORDER BY "+SqlOrder(SCK->(IndexKey()))

cQuery := ChangeQuery(cQuery)

dbUseArea(.T.,"TOPCONN",TcGenQry(,,cQuery),cAliasTMP,.T.,.T.)	

While (cAliasTMP)->(!Eof())
	nX	:= aScan(aContrato,{|x| x[1] == (cAliasTMP)->CK_CONTRAT .And. x[2] == (cAliasTMP)->CK_ITEMCON})
	If nX == 0
		aAdd(aContrato,{(cAliasTMP)->CK_CONTRAT,(cAliasTMP)->CK_ITEMCON,0})
		nX	:= Len(aContrato)
	EndIf
	aContrato[nX][3]	-= (cAliasTMP)->CK_QTDVEN
	(cAliasTMP)->(dbSkip())
EndDo

(cAliasTMP)->(dbCloseArea())
RestArea(aAreaTMP1)

If !Empty(TMP1->CK_CONTRAT) 
	dbSelectArea("ADB")
	ADB->(dbSetOrder(1))
	If MsSeek(xFilial("ADB")+TMP1->CK_CONTRAT+TMP1->CK_ITEMCON)
		If nQtdVen == 0
			nQtdVen := TMP1->CK_QTDVEN
		EndIf
		nX := aScan(aContrato,{|x| x[1] == ADB->ADB_NUMCTR .And. x[2] == ADB->ADB_ITEM})
		If nQtdVen > ADB->ADB_QUANT-ADB->ADB_QTDEMP-If(nX>0,aContrato[nX][3],0)
			Help(" ",1,"A415QTDCTR")
			lRetorno := .F.
		EndIf
	EndIf
	RestArea(aAreaTMP1)
EndIf

Return lRetorno
